actor CrocGrabBusterWep_NormalBar : NormalBar { args 4, 104 }

actor CrocGrabBusterWep : BaseMM8BDMWep 10103
{
//$Category MM8BDM-Weapons/Ranged
//$Title Grab Buster
//$Sprite GRBUA0
Weapon.AmmoUse 2
Weapon.AmmoGive 28
Weapon.SlotNumber 2
Obituary "$OB_GRABBUSTER"
Tag "$TAG_GRABBUSTER"
Inventory.Pickupmessage "$PU_GRABBUSTER"
weapon.ammotype "CrocGrabBusterAmmo"
inventory.icon "GRABBI"
States
{
SpawnLoop:
GRBU A -1
loop
Ready:
GRBU J 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_GRABBUSTER)
GRBU J 1 A_WeaponReady
Goto Ready+1
Deselect:
GRBU J 0
goto DeselectSwap
Select:
GRBU J 0
goto SelectSwap
Fire:
GRBU J 0 A_JumpIfNoAmmo("NoAmmo")
GRBU J 0 A_PlaySoundEx("weapons/mmv/grabbusterfire","Weapon")
GRBU J 0 A_FireCustomMissile("CrocGrabBuster",0,1,8,0)
GRBU JKL 3
GRBU J 4
GRBU J 0 A_Refire
Goto Ready+1
NoAmmo:
GRBU J 1 ACS_NamedExecuteAlways("core_noammo",0)
Goto Ready+1
}
}

actor CrocGrabBusterAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}

actor CrocGrabBuster : BasicProjectile
{
    +FORCEXYBILLBOARD
    +HITTRACER
    +BRIGHT
	Obituary "$OB_GRABBUSTER"
	Damagetype "CrocGrabBuster"
	Damage (20)
	height 10
	radius 5
	Speed 35
	States
	{
	Spawn:
		GRBU B 2
    SpawnLoop:
        GRBU CDED 2 A_SpawnItemEx("CrocGrabBusterFX", 0, frandom(-6, 6), frandom(-6, 6), 0, 0, 0, 0, SXF_WEPFXCOLOR)
		loop
    XDeath:
        GRBU A 0 A_JumpIfInventory("CanBeCrocGrabBustered", 1, "TeamCheck", AAPTR_TRACER)
        stop
    TeamCheck:
		GRBU A 0 A_GiveInventory("TracerAndTargetTeamsMatchCheck",1)
		GRBU A 0 A_JumpIfInventory("TracerAndTargetTeamsMatchFlag", 1, "Failed")
        goto HealReturn
    Failed:
        TNT1 A 0
        stop
    HealReturn:
        GRBU A 0 A_CustomMissile("CrocGrabBusterHeal", 0, 0, frandom(0.0, 360.0), 0)
        stop
    }
}

actor CrocGrabBusterHeal : BasicProjectile
{
    +NOINTERACTION
    +NOCLIP
    +BRIGHT
    reactiontime 1
    speed 30
    Damage (0)
    height 10
    radius 10
	Args 30 //return speed
    States 
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 ThrustThingZ(0, random(0, 35), random(0, 1), 0)
        GRBU G 6
    SpawnLoop:
		GRBU FFFFFFGGGGGGFFFFFFGGGGGGFFFFFFGGGGGGFFFFFFGGGGGGFFFFFFGGGGGGFFFFFFGGGGGG 1 A_GiveInventory("CrocGrabBusterFunc",1)
		TNT1 A 1 A_CountDown
		wait
    Death:
        GRBU A 0 A_GiveToTarget("CrocGrabBusterHealPickup", 1)
        stop
    }
}

actor CrocGrabBusterFunc : CustomInventory
{
    states
    {
    Pickup:
        TNT1 A 0 A_JumpIfCloser(64, "PickupD")
        TNT1 A 0 A_GiveInventory("HeadTowardsTarget_Arg0",1)
        stop
    PickupD:
        TNT1 A 0 A_CountDown
        stop
    }
}

actor CrocGrabBusterHealPickup : CustomInventory
{
	inventory.pickupmessage "$PU_SMALLHEALTH"
	reactiontime 60
	Scale 2.0
	States
	{
	Spawn:
		GRBU HI 6 A_Countdown
		loop
	Pickup:
		GRBU I 0 A_PlaySoundEx("item/energyup", "voice")
		TNT1 A 0 ACS_NamedExecuteWithResult("core_HealScript",15)
		stop
	Death:
		GRBU I 0 A_ChangeFlag("NOINTERACTION", true)
		GRBU I 10
		GRBU I 1 A_Fadeout
		wait
	}
}

actor CrocGrabBusterFX : BasicGraphicEffect
{
    +FORCEXYBILLBOARD
    +BRIGHT
    Speed 60
    States
    {
    Spawn:
        GRBU ZMON 2
        stop
    }
}
