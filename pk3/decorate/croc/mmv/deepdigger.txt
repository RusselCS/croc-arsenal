actor CrocDeepDiggerWep_NormalBar : NormalBar { args 4, 227 }
actor CrocDeepDiggerWep_ScriptBar : ScriptBar {}

actor CrocDeepDiggerWep : BaseMM8BDMWep 10108
{
    //$Category MM8BDM-Weapons/Ranged
    //$Title Deep Digger
    //$Sprite DDIGA0
    Weapon.AmmoUse 2
    Weapon.AmmoGive 28
    Weapon.SlotNumber 2
    Inventory.Pickupmessage "$PU_DEEPDIGGER"
    Obituary "$OB_DEEPDIGGER"
    Tag "$TAG_DEEPDIGGER"
    weapon.ammotype "CrocDeepDiggerAmmo"
    inventory.icon "DDIGWI"
    States
    {
    SpawnLoop:
        DDIG A -1
        loop
    Ready:
        DDIG D 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_DEEPDIGGER)
        DDIG D 1 A_WeaponReady
        Goto Ready+1
    Deselect:
        DDIG D 0 A_JumpIfHealthLower(1, "FireCheck") // Don't throw rocks if you're dead
        DDIG D 0 A_JumpIfInventory("CrocDeepDiggerFlagL", 1, "FireCheck")
        DDIG D 0 A_JumpIfInventory("CrocDeepDiggerFlagR", 1, "FireCheck")
    DeselectSkip:
        DDIG D 0 A_TakeInventory("CrocDeepDiggerFlagL", 1)
        DDIG D 0 A_TakeInventory("CrocDeepDiggerFlagR", 1)
        goto DeselectSwap
    FireCheck:
        TNT1 A 0 A_JumpIfHealthLower(1,"DeselectSkip")
        TNT1 A 1 A_JumpIf(true,"Fire")
        wait
    Select:
        DDIG D 0
        goto SelectSwap
    Altfire:
        "####" "#" 0 A_JumpIfInventory("CrocDeepDiggerFlagL", 1, "CheckLeft")
        "####" "#" 0 A_JumpIfInventory("CrocDeepDiggerFlagR", 1, "CheckLeft")
        "####" "#" 1 A_JumpIf(true, "FireCont")
        wait
    Fire:
        "####" "#" 0 A_JumpIfInventory("CrocDeepDiggerFlagR", 1, "CheckRight")
        "####" "#" 0 A_JumpIfInventory("CrocDeepDiggerFlagL", 1, "CheckRight")
        "####" "#" 1 A_JumpIf(true, "FireCont")
        wait
   FireCont:
        DDIG D 0 A_JumpIfNoAmmo("NoAmmo")
        DDIG E 1
        TNT1 A 1
        DDIG D 0 A_PlaySoundEx("weapons/mmv/deepdiggerload", "weapon")
        DDIG D 0 A_GiveInventory("CrocDeepDiggerFlagL", 1)
        DDIG D 0 A_GiveInventory("CrocDeepDiggerFlagR", 1)
        DDIG A 0 A_ChangeVelocity(0, 0, momz, CVF_REPLACE)
        DDIG D 0 A_Stop
        DDIG A 0 A_SpawnItemEx("CrocDeepDiggerWarper_R", 68,  17, 16)
        DDIG A 0 A_SpawnItemEx("CrocDeepDiggerWarper_L", 60, -17, 16)
        DDIG F 1
        DDIG A 0 //A_SpawnItemEx("TempCrocDeepDigger", 52,  16, 32)
        DDIG A 0 //A_SpawnItemEx("TempCrocDeepDigger", 44, -16, 32)
        DDIG F 1
        DDIG A 0 //A_SpawnItemEx("TempCrocDeepDigger", 36,  15, 48)
        DDIG A 0 //A_SpawnItemEx("TempCrocDeepDigger", 28, -15, 48)
        DDIG G 1
        DDIG A 0 //A_SpawnItemEx("TempCrocDeepDigger", 20,  13, 64)
        DDIG A 0 //A_SpawnItemEx("TempCrocDeepDigger", 12, -13, 64)
        DDIG G 1
        DDIG A 0 //A_SpawnItemEx("TempCrocDeepDigger",  4,  10, 74)
        DDIG A 0 //A_SpawnItemEx("TempCrocDeepDigger", -4, -10, 74)
        DDIG H 1
        DDIG H 1 //A_GiveInventory("SpawnTempCrocDeepDigger", 1)
        TNT1 AAAAAAAAAA 1 //A_GiveInventory("SpawnTempCrocDeepDigger", 1)
        goto Carry
    Carry:
        TNT1 A 0 //A_GiveInventory("SpawnTempCrocDeepDigger", 1)
        TNT1 A 1 A_WeaponReady
        TNT1 A 0 A_JumpIfInventory("CrocDeepDiggerFlagR", 1, "Carry")
        TNT1 A 0 A_JumpIfInventory("CrocDeepDiggerFlagL", 1, "Carry")
        TNT1 A 0 A_JumpIf(true, "FireEnd")
        loop

    FireEnd:
        TNT1 A 1 A_RailWait
        TNT1 A 0 A_JumpIfInventory("CrocDeepDiggerAltFrames", 1, "FireEnd")
        TNT1 A 0 A_JumpIfInventory("CrocDeepDiggerMainFrames", 1, "FireEnd")
        TNT1 A 0 A_JumpIf(true, "FireEndReal")
        loop
    FireEndReal:
        TNT1 A 10
        DDIG OP 6
        DDIG D 0 A_ClearRefire
        Goto Ready+1

    CheckRight:
        TNT1 A 0 A_JumpIfInventory("CrocDeepDiggerMainFrames", 1, "FireLeft")
        TNT1 A 0 A_JumpIfInventory("CrocDeepDiggerAltFrames", 1, "FireRight")
        TNT1 A 0 A_GunFlash("FlashFireRight")
        TNT1 A 0 A_JumpIfInventory("CrocDeepDiggerFlagR", 1, "RightWait")
        TNT1 A 0 A_JumpIfInventory("CrocDeepDiggerFlagL", 1, "RightWait")
        TNT1 A 1 A_JumpIf(true, "FireEnd")
        loop
    RightWait:
        TNT1 AAAAAA 1 //A_GiveInventory("SpawnTempCrocDeepDigger", 1)
        goto Carry
    CheckLeft:
        TNT1 A 0 A_JumpIfInventory("CrocDeepDiggerMainFrames", 1, "FireLeft")
        TNT1 A 0 A_JumpIfInventory("CrocDeepDiggerAltFrames", 1, "FireRight")
        TNT1 A 0 A_GunFlash("FlashFireLeft")
        TNT1 A 0 A_JumpIfInventory("CrocDeepDiggerFlagR", 1, "LeftWait")
        TNT1 A 0 A_JumpIfInventory("CrocDeepDiggerFlagL", 1, "LeftWait")
        TNT1 A 1 A_JumpIf(true, "FireEnd")
        loop
    LeftWait:
        TNT1 AAAAAA 1 //A_GiveInventory("SpawnTempCrocDeepDigger", 1)
        goto Carry

    FireLeft:
        TNT1 A 0 A_PlaySoundEx("weapons/mmv/deepdiggerfire","Weapon")
        TNT1 A 0 A_FireCustomMissile("CrocDeepDigger",0,1,-10,4)
        TNT1 A 0 A_GiveInventory("CrocDeepDiggerTakeLeft", 1)
        DDIG QRSTUV 1 //A_GiveInventory("SpawnTempCrocDeepDigger", 1)
        goto FireEnd
    FireRight:
        TNT1 A 0 A_PlaySoundEx("weapons/mmv/deepdiggerfire","Weapon")
        TNT1 A 0 A_FireCustomMissile("CrocDeepDigger",0,1,10,-4)
        TNT1 A 0 A_GiveInventory("CrocDeepDiggerTakeRight", 1)
        DDIG IJKLMN 1 //A_GiveInventory("SpawnTempCrocDeepDigger", 1)
        goto FireEnd

    FlashFireLeft:
        DDIG F 0 A_PlaySoundEx("weapons/mmv/deepdiggerfire","Weapon")
        DDIG F 0 A_FireCustomMissile("CrocDeepDigger",0,1,-10,4)
        DDIG F 0 A_GiveInventory("CrocDeepDiggerTakeLeft", 1)
        DDIG QRSTUV 1
        stop
    FlashFireRight:
        DDIG F 0 A_PlaySoundEx("weapons/mmv/deepdiggerfire","Weapon")
        DDIG F 0 A_FireCustomMissile("CrocDeepDigger",0,1,10,-4)
        DDIG F 0 A_GiveInventory("CrocDeepDiggerTakeRight", 1)
        DDIG IJKLMN 1
        stop

    NoAmmo:
        DDIG D 1 ACS_NamedExecuteAlways("core_noammo",0)
        Goto Ready+1
    }
}

actor CrocDeepDiggerWarper_R : BasicShieldWarper
{
	States
	{
	Spawn:
	TNT1 A 0
	TNT1 A 0 A_GiveInventory("MM8BDMProjSpawnFuncClient",1)
	TNT1 A 0 ACS_NamedExecuteAlways("core_shieldwarper_deepdigger", 0, 0)
	TNT1 A 0 A_Jump(256, "Shield")
	Goto Shield
	Shield:
    DDIG B 1 
    DDIG B 1 A_Warp(AAPTR_TARGET, 52, 16, 32, 0, WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION)
    DDIG B 1 A_Warp(AAPTR_TARGET, 36, 15, 48, 0, WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION)
    DDIG B 1 A_Warp(AAPTR_TARGET, 20, 13, 64, 0, WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION)
    DDIG B 1 A_Warp(AAPTR_TARGET, 4,  10, 74, 0, WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION)
    ShieldLoop:
	DDIG B 1 A_Warp(AAPTR_TARGET, -1, 10, 56 + 16, 0, WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION)
	loop
	}
}

actor CrocDeepDiggerWarper_L : BasicShieldWarper
{
	States
	{
	Spawn:
	TNT1 A 0
	TNT1 A 0 A_GiveInventory("MM8BDMProjSpawnFuncClient",1)
	TNT1 A 0 ACS_NamedExecuteAlways("core_shieldwarper_deepdigger", 0, 1)
	TNT1 A 0 A_Jump(256, "Shield")
	Goto Shield
	Shield:
    DDIG B 1 
    DDIG B 1 A_Warp(AAPTR_TARGET, 44, -16, 32, 0, WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION)
    DDIG B 1 A_Warp(AAPTR_TARGET, 28, -15, 48, 0, WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION)
    DDIG B 1 A_Warp(AAPTR_TARGET, 12, -13, 64, 0, WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION)
    DDIG B 1 A_Warp(AAPTR_TARGET, -4, -10, 74, 0, WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION)
    ShieldLoop:
	DDIG B 1 A_Warp(AAPTR_TARGET, -9, -10, 56 + 16, 0, WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION)
	loop
	}
}

actor CrocDeepDiggerTakeRight : CustomInventory
{
    States
    {
    Pickup:
        TNT1 A 0
        TNT1 A 0 A_GiveInventory("CrocDeepDiggerMainFrames", 1)
        TNT1 A 0 A_JumpIfInventory("CrocDeepDiggerFlagR", 1, "TakeRight")
        TNT1 A 0 A_JumpIfInventory("CrocDeepDiggerFlagL", 1, "TakeLeft")
        TNT1 A 1 A_JumpIf(true, "End")
        wait
    TakeRight:
        TNT1 A 0 A_TakeInventory("CrocDeepDiggerFlagR", 1)
        goto End
    TakeLeft:
        TNT1 A 0 A_TakeInventory("CrocDeepDiggerFlagL", 1)
        goto End
    End:
        TNT1 A 0
        stop
    }
}

actor CrocDeepDiggerTakeLeft : CrocDeepDiggerTakeRight
{
    States
    {
    Pickup:
        TNT1 A 0
        TNT1 A 0 A_GiveInventory("CrocDeepDiggerAltFrames", 1)
        TNT1 A 0 A_JumpIfInventory("CrocDeepDiggerFlagL", 1, "TakeLeft")
        TNT1 A 0 A_JumpIfInventory("CrocDeepDiggerFlagR", 1, "TakeRight")
        TNT1 A 1 A_JumpIf(true, "End")
        wait
    }
}

actor CrocDeepDiggerAltFrames : Powerup
{
    powerup.duration 12
}

actor CrocDeepDiggerMainFrames : Powerup
{
    powerup.duration 12
}

actor CrocDeepDiggerAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}

actor CrocDeepDiggerFlagL : Inventory
{
    inventory.amount 1
    inventory.maxamount 2
}
actor CrocDeepDiggerFlagR : CrocDeepDiggerFlagL {}

actor CrocDeepDigger : BasicFastProjectile
{
    Radius 10
    Height 10
    damage (34)
    speed 60
    damagetype "CrocDeepDigger"
    Obituary "$OB_DEEPDIGGER"
    States
    {
    Spawn:
        DDIG B 0
        DDIG B 1
        wait
    XDeath:
        DDIG B 0 A_PlaySoundEx("weapons/mmv/deepdiggersplit", "auto")
        DDIG BBBB 0 A_SpawnItemEx("CrocDeepDiggerBreakFX",0,0,0,random(-10,10),random(-10,10),random(5,10),random(0,360), SXF_WEPFXCOLOR)
        TNT1 A 2
        stop
    Death:
        DDIG B 0 A_PlaySoundEx("weapons/mmv/deepdiggersplit", "auto")
        DDIG B 0 A_SpawnItemEx("CrocDeepDiggerPuffFX",0,0,0,0,0,0,0,SXF_WEPFXCOLOR)
        DDIG B 0 A_SpawnItemEx("CrocDeepDiggerBit", 0, 0, 0, 1, 0, 0,  67.5, SXF_MULTIPLYSPEED)
        DDIG B 0 A_SpawnItemEx("CrocDeepDiggerBit", 0, 0, 0, 1, 0, 0,  22.5, SXF_MULTIPLYSPEED)
        DDIG B 0 A_SpawnItemEx("CrocDeepDiggerBit", 0, 0, 0, 1, 0, 0, -22.5, SXF_MULTIPLYSPEED)
        DDIG B 0 A_SpawnItemEx("CrocDeepDiggerBit", 0, 0, 0, 1, 0, 0, -67.5, SXF_MULTIPLYSPEED)
        TNT1 A 2
        stop
    }
}

actor CrocDeepDiggerPuffFX : BasicGraphicEffect
{
    +FORCEXYBILLBOARD
    speed 60
    States
    {
    Spawn:
        DDIG WXYZ 2
        stop
    }
}

actor CrocDeepDiggerBreakFX : BasicGraphicEffect
{
-NOINTERACTION
-NOGRAVITY
+HEXENBOUNCE
States
{
SpawnFrame:
DDIG C 2
TNT1 A 2
DDIG C 2
TNT1 A 2
DDIG C 2
TNT1 A 2
DDIG C 2
TNT1 A 2
DDIG C 2
TNT1 A 2
stop
}
}

actor CrocDeepDiggerBit : BasicProjectile
{
    +HEXENBOUNCE
    -ALLOWBOUNCEONACTORS
    Obituary "$OB_DEEPDIGGER"
    damagetype "CrocDeepDigger"
    Radius 5
    Height 5
    damage (10)
    speed 40
	BounceFactor 1.0
    States
    {
    Spawn:
		TNT1 A 0
        DDIG C 3
        DDIG C 0 A_ChangeFlag(HEXENBOUNCE, false)
        DDIG C 1
        wait
    }
}
