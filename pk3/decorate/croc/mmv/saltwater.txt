actor CrocSaltWaterWep_NormalBar : NormalBar { args 4, 198 }

actor CrocSaltWaterWep : BaseMM8BDMWep
{
    //$Category MM8BDM-Weapons/Close
    //$Title Salt Water
    //$Sprite SWATA0
    Weapon.AmmoUse 2
    Weapon.AmmoGive 28
    Weapon.SlotNumber 4
    Obituary "$OB_SALTWATER"
    Tag "$TAG_SALTWATER"
    Inventory.Pickupmessage "$PU_SALTWATER"
    weapon.ammotype "CrocSaltWaterAmmo"
    inventory.icon "SWATWI"
    States
    {
    SpawnLoop:
        SWAT A -1
        loop
    Ready:
        SWAT J 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_SALTWATER)
        SWAT J 1 A_WeaponReady
        Goto Ready+1
    Deselect:
        SWAT J 0
        goto DeselectSwap
    Select:
        SWAT J 0
        goto SelectSwap
    Fire:
        SWAT J 0 A_JumpIfNoAmmo("NoAmmo")
        TNT1 A 1
        SWAT MN 1
        SWAT J 0 A_PlaySoundEx("weapons/mmv/saltwaterfire","Weapon")
        SWAT J 0 A_FireCustomMissile("CrocSaltWater",0,1,8,0)
        SWAT OPQR 1
        TNT1 A 13
        TNT1 A 0 A_JumpIfNoAmmo("NoAmmoReady")
        SWAT LK 2 A_Refire
        Goto Ready+1
    NoAmmoReady:
        SWAT LK 2
        Goto Ready+1
    NoAmmo:
        SWAT J 1 ACS_NamedExecuteAlways("core_noammo",0)
        Goto Ready+1
    }
}

actor CrocSaltWaterAmmo : Ammo
{
    inventory.amount 1
    inventory.maxamount 28
    +INVENTORY.IGNORESKILL
}

actor CrocSaltWater : BasicProjectile
{
    +FORCEXYBILLBOARD
    +BOUNCEONCEILINGS
    +USEBOUNCESTATE
    -NOGRAVITY
    Damage (33)
    Obituary "$OB_SALTWATER"
    damagetype "SaltWater"
    gravity 1.2
    Speed 24
    Height 10
    Radius 10
    States
    {
    Spawn:
        SWAT B 0
        SWAT B 0 ThrustThingZ(0, 30, 0, 1)
        SWAT B 1
        wait
    Bounce.Ceiling:
        SWAT B 0 A_ChangeFlag(USEBOUNCESTATE, false)
        SWAT B 0 A_ChangeFlag(BOUNCEONCEILINGS, false)
        SWAT B 0 A_ChangeVelocity(momx, momy, momz-1, CVF_REPLACE)
        goto Spawn+2
    Death:
        TNT1 A 0 A_SpawnItemEx("CrocSaltWaterBit", 0, 0, 0, 0.8, 0, 0.5,  12, SXF_MULTIPLYSPEED)
        TNT1 A 0 A_SpawnItemEx("CrocSaltWaterBit", 0, 0, 0, 0.9, 0, 0.5,   6, SXF_MULTIPLYSPEED)
        TNT1 A 0 A_SpawnItemEx("CrocSaltWaterBit", 0, 0, 0, 1.0, 0, 0.5,   0, SXF_MULTIPLYSPEED)
        TNT1 A 0 A_SpawnItemEx("CrocSaltWaterBit", 0, 0, 0, 0.9, 0, 0.5,  -6, SXF_MULTIPLYSPEED)
        TNT1 A 0 A_SpawnItemEx("CrocSaltWaterBit", 0, 0, 0, 0.8, 0, 0.5, -12, SXF_MULTIPLYSPEED)
        TNT1 A 0 A_JumpIf(z-floorz < 4, "FloorSplash")
        TNT1 A 0 A_JumpIf(true, "WallSplash")
        TNT1 A 1
        wait
    WallSplash:
        TNT1 A 0 A_SpawnItemEx("CrocSaltWaterWallSplash", 0, 0, 0, 0, 0, 0, 0, SXF_WEPFXCOLOR)
        stop
    FloorSplash:
        TNT1 A 0 A_SpawnItemEx("CrocSaltWaterSplash", 0, 0, 0, 0, 0, 0, 0, SXF_WEPFXCOLOR)
        stop
    XDeath:
        TNT1 A 0 A_SpawnItemEx("SoakifyEffect")
        TNT1 AAAAA 0 A_SpawnItemEx("CrocSaltWaterBitDie", frandom(-32, 32), frandom(-32, 32), frandom(-32, 32), 0, 0, 0, 0, SXF_WEPFXCOLOR)
        stop
    }
}

actor CrocSaltWaterSplash : BasicGraphicEffect
{
    States
    {
    SpawnFrame:
        TNT1 A 0
        TNT1 A 0 A_PlaySoundEx("weapons/mmv/saltwatersplit", "body")
        SWAT CDE 4
        stop
    }
}

actor CrocSaltWaterWallSplash : BasicGraphicEffect
{
    States
    {
    SpawnFrame:
        TNT1 A 0
        TNT1 A 0 A_PlaySoundEx("weapons/mmv/saltwatersplit", "body")
        SWAT ST 4
        stop
    }
}

actor CrocSaltWaterBit : BasicProjectile
{
    +FORCEXYBILLBOARD
    -NOGRAVITY
    +HEXENBOUNCE
    Obituary "$OB_SALTWATER"
    damagetype "SaltWater"
    bouncecount 2
    Damage (11)
    Speed 20
    Height 10
    Radius 10
    BounceFactor 1.0
    States
    {
    Spawn:
		TNT1 A 0
		SWAT F 6
        SWAT F 0 A_ChangeFlag(BOUNCEONWALLS, false)
		SWAT F 0 A_ChangeFlag(BOUNCEONFLOORS, false)
        SWAT F 1
        wait
    Death:
        TNT1 A 0 A_SpawnItemEx("CrocSaltWaterBitDie", 0, 0, 0, 0, 0, 0, 0, SXF_WEPFXCOLOR)
        stop
    }
}

actor CrocSaltWaterBitDie : CrocSaltWaterSplash
{
    +FORCEXYBILLBOARD
    States
    {
    SpawnFrame:
        TNT1 A 0
        TNT1 A 0 A_PlaySoundEx("weapons/mmv/saltwaterdrip", "body")
        SWAT GHI 3
        stop
    }
}
