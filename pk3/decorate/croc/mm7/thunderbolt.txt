actor CrocThunderBoltWep_NormalBar : NormalBar { args 228, 73 }

actor CrocThunderBoltWep : BaseMM8BDMWep
{
//$Category MM8BDM-Weapons/Ranged
//$Title Thunder Bolt
//$Sprite WEA3R0
Weapon.AmmoUse 2
Weapon.AmmoGive 28
Weapon.SlotNumber 2
Inventory.Pickupmessage "$PU_THUNDERBOLT"
Obituary "$OB_THUNDERBOLT"
Tag "$TAG_THUNDERBOLT"
weapon.ammotype "CrocThunderBoltAmmo"
inventory.icon "TBOLSI"
States
{
SpawnLoop:
WEA3 R -1
loop
Ready:
TBOL S 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_THUNDERBOLT)
TBOL S 1 A_WeaponReady
Goto Ready+1
Deselect:
TBOL S 0
goto DeselectSwap
Select:
TBOL S 0
goto SelectSwap
Fire:
TBOL S 0 A_JumpIfNoAmmo("NoAmmo")
TBOL S 0 A_PlaySoundEx("weapons/mm7/thunderboltfire","Weapon")
TBOL S 0 A_FireCustomMissile("CrocThunderBolt",0,1,8,0)
TBOL TU 5
TBOL S 14
TBOL S 0 A_Refire
Goto Ready+1
NoAmmo:
TBOL S 1 ACS_NamedExecuteAlways("core_noammo",0)
Goto Ready+1
}
}

actor CrocThunderBoltAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}

actor CrocThunderBolt : BasicProjectile
{
+FORCEXYBILLBOARD
+BRIGHT
damagetype "ThunderBolt"
Obituary "$OB_THUNDERBOLT"
Speed 33
Radius 25
Height 16
Damage (35)
var int user_CompositePierceRipper;
States
{
Spawn:
TBOL ABCD 1
Goto Flying
Flying:
TBOL N 1
TBOL M 1 A_SpawnItemEx("CrocThunderTrail", 0, 0, 0, 0, 0, 0, 0, SXF_WEPFXCOLOR)
loop
Death:
Split:
TNT1 A 0 A_SpawnItemEX("ElectrifyEffect")
TNT1 A 0 A_SpawnItemEx("CrocThunderBoltSplit",0,0,0,1,0,0,90,SXF_TRANSFERPOINTERS|SXF_MULTIPLYSPEED)
TNT1 A 0 A_SpawnItemEx("CrocThunderBoltSplit",0,0,0,1,0,0,-90,SXF_TRANSFERPOINTERS|SXF_MULTIPLYSPEED)
TNT1 A 0 A_SpawnItemEx("ExplosionEffect1", 0, 0, 0, 0, 0, 0, 0, SXF_WEPFXCOLOR)
stop
}
}

actor CrocThunderBoltSplit : CrocThunderBolt
{
+FORCEXYBILLBOARD
Radius 25
Height 16
Damage (20)
reactiontime 90
var int user_CompositePierceRipper;
States
{
Spawn:
TBOL O 1
goto SpawnLoop+1
SpawnLoop:
TBOL OP 1 A_CountDown
TBOL O 0 A_SpawnItemEx("CrocThunderTrail", 0, 0, 0, 0, 0, 0, 0, SXF_WEPFXCOLOR)
TBOL QR 1 A_CountDown
TBOL O 0 A_SpawnItemEx("CrocThunderTrail", 0, 0, 0, 0, 0, 0, 0, SXF_WEPFXCOLOR)
TBOL O 0 A_GiveInventory("CutterFlag",1)
loop
Death:
TNT1 A 0 A_SpawnItemEX("ElectrifyEffect")
TNT1 A 0
stop
XDeath:
Crash:
TNT1 A 0 A_SpawnItemEX("ElectrifyEffect")
TBOL O 0 A_JumpIfInventory("CutterFlag",1,"Split")
stop
}
}

actor CrocThunderBoltFX : BasicGraphicEffect
{
+FORCEXYBILLBOARD
+BRIGHT
States
{
SpawnFrame:
TBOL ABCD 1
stop
}
}

actor CrocThunderTrail : BasicGraphicEffect
{
+FORCEXYBILLBOARD
+BRIGHT
States
{
Spawn:
TBOL IJKL 2
stop
}
}
