actor CrocScorchWheelWep_NormalBar : NormalBar { args 93, 42 }
actor CrocScorchWheelWep_ScriptBar : ScriptBar {}

actor CrocScorchWheelWep : BaseMM8BDMWep
{
//$Category MM8BDM-Weapons/Shield
//$Title Scorch Wheel
//$Sprite WEA3M0
Weapon.AmmoUse 7
Weapon.AmmoGive 28
Weapon.SlotNumber 7
Inventory.Pickupmessage "$PU_SCORCHWHEEL"
Obituary "$OB_SCORCHWHEEL"
Tag "$TAG_SCORCHWHEEL"
weapon.ammotype "CrocScorchWheelAmmo"
inventory.icon "SWHESI"
States
{
SpawnLoop:
WEA3 M -1
loop
Ready:
SWHE G 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_SCORCHWHEEL)
TNT1 A 0 A_TakeInventory("CrocScorchWheelPowerup", 999)
SWHE G 1 A_WeaponReady
Goto Ready+1
Deselect:
SWHE G 0
goto DeselectSwap
Select:
SWHE G 0
goto SelectSwap
Fire:
SWHE G 0 A_JumpIfNoAmmo("NoAmmo")
SWHE G 0 A_GiveInventory("CrocWheelCount", 80)
Altfire:
SWHE G 0 A_JumpIfNoAmmo("NoAmmo")
SWHE HIJKL 2
SWHE G 0 A_SpawnItemEx("CrocScorchWheelWarper")
SWHE G 0 A_PlaySoundEx("weapons/mm7/scorchwheelactivate","Weapon")
TNT1 A 0 A_GiveInventory("CrocScorchWheelPowerup", 1)
Wheel:
TNT1 A 1 A_JumpIfInventory("CrocWheelCount",1,"Hold")
Goto Throw
Hold:
TNT1 A 0 A_JumpIfInventory("IsBot", 1, "BotThrow")
TNT1 A 1 A_TakeInventory("CrocWheelCount",1)
TNT1 A 1 A_Refire("Wheel")
Throw:
TNT1 A 0 A_TakeInventory("CrocScorchWheelPowerup", 999)
TNT1 A 0 A_TakeInventory("CrocWheelCount",99)
TNT1 A 0 A_PlaySoundEx("weapons/mm7/scorchwheelfire","Weapon")
TNT1 A 15 A_FireCustomMissile("CrocScorchWheel",0,1,0,0)
SWHE MN 2 A_ClearRefire
Goto Ready+1
BotThrow:
TNT1 A 0 A_Jump(10, "Throw")
goto Hold+1
NoAmmo:
SWHE G 1 ACS_NamedExecuteAlways("core_noammo",0)
Goto Ready+1
}
}

actor CrocScorchWheelPowerup : PowerSpeed
{
+POWERSPEED.NOTRAIL
Speed 1.45
}

actor CrocWheelCount : Inventory
{
inventory.amount 1
inventory.maxamount 80
}

actor CrocScorchWheelAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}

actor CrocScorchWheel : ProjSpawnFuncActor
{
-SOLID
+MISSILE
+RIPPER
+BRIGHT
+DONTREFLECT
Radius 18
Height 40
scale 2.5
speed 16
damage 0
damagetype "ScorchWheel"
Obituary "$OB_SCORCHWHEEL"
maxstepheight 24
-NOGRAVITY
+NOBLOOD
painchance 0
reactiontime 10
States
{
Spawn:
SWHE A 0
SWHE A 0 A_SpawnItemEx("OilPitIgnite",0,0,0)
SWHE ABCDEF 2 A_Explode(16,96,0)
loop
Death:
TNT1 A 0 A_CustomMissile("CrocScorchWheel2",0,0,0,2,0)
TNT1 A 1 A_SpawnItemEx("OilPitIgnite",0,0,8)
stop
}
}

actor CrocScorchWheel2 : ProjSpawnFuncActor
{
damagetype "ScorchWheel"
Obituary "$OB_SCORCHWHEEL"
+MISSILE
-NOGRAVITY
+RIPPER
+BRIGHT
+NOEXPLODEFLOOR
+STEPMISSILE
maxstepheight 24
+BOUNCEONCEILINGS
+DONTREFLECT
BounceCount 1
damage 0
Radius 18
Height 40
speed 16
scale 2.5
States
{
Spawn:
SWHE A 0
SWHE A 0 A_SpawnItemEx("OilPitIgnite",0,0,0)
SWHE ABCDEF 2 A_Explode(16,96,0)
loop
Death:
TNT1 AAAA 0 A_SpawnItemEx("CrocScorchBit",0,0,0,random(-10,10),random(-10,10),random(10,15),random(0,360), SXF_WEPFXCOLOR)
TNT1 A 1 A_SpawnItemEx("OilPitIgnite",0,0,8)
stop
}
}

actor CrocScorchBit : BasicGraphicEffect
{
-NOGRAVITY
-NOINTERACTION
+RIPPER
+BRIGHT
damage (0)
States
{
SpawnFrame:
SWHE O 1
TNT1 A 1
SWHE O 1
TNT1 A 1
SWHE O 1
TNT1 A 1
SWHE O 1
TNT1 A 1
SWHE O 1
TNT1 A 1
SWHE O 1
TNT1 A 1
SWHE O 1
TNT1 A 1
SWHE O 1
TNT1 A 1
SWHE O 1
TNT1 A 1
SWHE O 1
TNT1 A 1
stop
}
}

actor CrocScorchWheelWarper : BasicShieldWarper
{
	+BRIGHT
	States
	{
	Spawn:
	TNT1 A 0
	TNT1 A 0 A_GiveInventory("MM8BDMProjSpawnFuncClient",1)
	TNT1 A 1 ACS_NamedExecuteAlways("core_shieldwarper_scorch", 0, "test")
	Shield:
	SWHE AABBCCDDEEFF 1 A_Warp(AAPTR_TARGET, 0, 0, 0, 0, WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION)
	loop
	}
}

// Unused zone?
actor CrocWheelFrameCheck : CrocCutterFlag
{
}

actor CrocWheelCheck
{
PROJECTILE
Damage 0
speed 60
radius 32
height 32
renderstyle none
States
{
Spawn:
PLAY A 4 
stop
Death:
PLAY H 0
PLAY H 20 A_SpawnItem("CrocWheelBlocker")
stop
}
}

actor CrocWheelBlocker
{
renderstyle none
+SOLID
+SHOOTABLE
+NOGRAVITY
+DONTRIP
Health 1
States
{
Spawn:
PLAY A 18
stop
Death:
PLAY A 1
stop
}
}
