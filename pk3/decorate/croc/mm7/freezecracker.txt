actor CrocFreezeCrackerWep_NormalBar : NormalBar { args 4, 203 }

actor CrocFreezeCrackerWep : BaseMM8BDMWep 10082
{
//$Category MM8BDM-Weapons/Ranged
//$Title Freeze Cracker
//$Sprite WEA3O0
Weapon.AmmoUse 2
Weapon.AmmoGive 28
Weapon.SlotNumber 2
Inventory.Pickupmessage "$PU_FREEZECRACKER"
Obituary "$OB_FREEZECRACKER"
Tag "$TAG_FREEZECRACKER"
weapon.ammotype "CrocFreezeCrackerAmmo"
inventory.icon "FCRASI"
States
{
SpawnLoop:
WEA3 O -1
loop 
Ready:
FCRA F 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_FREEZECRACKER)
FCRA F 1 A_WeaponReady
Goto Ready+1
Deselect:
FCRA F 0
goto DeselectSwap
Select:
FCRA F 0
goto SelectSwap
Fire:
FCRA F 0 A_JumpIfNoAmmo("NoAmmo")
FCRA F 0 A_PlaySoundEx("weapons/mm7/freezecrackerfire","Weapon")
FCRA F 0 A_FireCustomMissile("CrocFreezeCracker",0,1,8,0)
FCRA GH 6
FCRA F 3
FCRA F 0 A_Refire
Goto Ready+1
NoAmmo:
FCRA F 1 ACS_NamedExecuteAlways("core_noammo",0)
Goto Ready+1
}
}

actor CrocFreezeCrackerAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}

actor CrocFreezeFX : BasicGraphicEffect
{
+FORCEXYBILLBOARD
Speed 60
States
{
SpawnFrame:
FCRA ABC 4
stop
}
}

actor CrocFreezeCracker : BasicProjectile
{
+FORCEXYBILLBOARD
Radius 10
Height 10
damage (26)
speed 50
damagetype "FreezeCracker"
Obituary "$OB_FREEZECRACKER"
States
{
Spawn:
FCRA D 0
FCRA D 0 A_SpawnItemEx("CrocFreezeFX",0,0,0,0,0,0,0,SXF_WEPFXCOLOR)
FCRA E 4 
FCRA D 4 A_SpawnItemEx("CrocFreezeCrackerFX", -8, Random(-22, 22), random(-20, 20), 0, 0, 0, 0, SXF_WEPFXCOLOR)
Goto Spawn+2
Death:
TNT1 A 0 A_SpawnItemEx("IcePitFreeze")
TNT1 A 0 A_SpawnItemEx("CrocFreezeBit",0,0,0,28,0,0,60)
TNT1 A 0 A_SpawnItemEx("CrocFreezeBit",0,0,0,28,0,0,-60)
TNT1 A 0 A_SpawnItemEx("CrocFreezeBit",0,0,0,28,0,0,120)
TNT1 A 0 A_SpawnItemEx("CrocFreezeBit",0,0,0,28,0,0,-120)
TNT1 A 0 A_SpawnItemEx("CrocFreezeBit",0,0,0,28,0,0,180)
TNT1 A 0 A_SpawnItemEx("CrocFreezeBit",0,0,0,28,0,0,0)
TNT1 A 1 A_PlaySoundEx("weapons/mm7/freezecrackersplit","Weapon")
stop
}
}

actor CrocFreezeBit : BasicProjectile
{
+THRUACTORS
+FORCEXYBILLBOARD
Radius 6
Height 6
damage (12)
damagetype "FreezeCrackerBit"
Obituary "$OB_FREEZECRACKER"
speed 30
states
{
Spawn:
FCRA I 3
FCRA I 0 A_ChangeFlag(THRUACTORS, false)
FCRA I 4
wait
Death:
TNT1 A 0 A_SpawnItemEx("IcePitFreeze")
stop
}
}

actor CrocFreezeCrackerFX : BasicGraphicEffect
{
-NOINTERACTION
+DONTSPLASH
+FORCEXYBILLBOARD
-NOGRAVITY
Gravity 0.2
damage (0)
States
{
Spawn:
FCRA J 4
FCRA K 15
FCRA L 1 A_Fadeout
wait
}
}
