actor CrocSlashClawWep_NormalBar : NormalBar { args 228, 111 }

actor CrocSlashClawWep : BaseMM8BDMWep
{
//$Category MM8BDM-Weapons/Close
//$Title Slash Claw
//$Sprite WEA3Q0
Weapon.AmmoUse 1
Weapon.AmmoGive 28
Weapon.SlotNumber 4
Inventory.Pickupmessage "$PU_SLASHCLAW"
Obituary "$OB_SLASHCLAW"
Tag "$TAG_SLASHCLAW"
weapon.ammotype "CrocSlashClawAmmo"
inventory.icon "SCLASI"
States
{
SpawnLoop:
WEA3 Q -1
loop
Ready:
SCLA A 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_SLASHCLAW)
SCLA A 1 A_WeaponReady
Goto Ready+1
Deselect:
SCLA A 0
goto DeselectSwap
Select:
SCLA A 0
goto SelectSwap
Fire:
SCLA A 0 A_JumpIfNoAmmo("NoAmmo")
SCLA BC 1
SCLA A 0 A_PlaySoundEx("weapons/mm7/slashclawfire","Weapon")
SCLA A 0 A_FireCustomMissile("CrocSlashClaw",0,1,11,-8)
SCLA DE 2
SCLA F 4
SCLA G 15
SCLA CBA 2 A_WeaponReady
Goto Ready+1
NoAmmo:
SCLA A 1 ACS_NamedExecuteAlways("core_noammo",0)
Goto Ready+1
}
}

actor CrocSlashClaw : BasicProjectile
{
Radius 5
Height 5
damage (0)
+RIPPER
+DONTBLAST
+DONTREFLECT
+FORCEXYBILLBOARD
damagetype "SlashClaw"
Obituary "$OB_SLASHCLAW"
speed 15
States
{
Spawn:
SCLA H 1
SCLA IJKL 1 A_Explode(25,80,0)
stop
}
}

actor CrocSlashClawAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}
