actor CrocDangerWrapWep_NormalBar : NormalBar { args 4, 230 }

actor CrocDangerWrapWep : BaseMM8BDMWep 10085
{
//$Category MM8BDM-Weapons/Power
//$Title Danger Wrap
//$Sprite WEA3L0
Weapon.AmmoUse 2
Weapon.AmmoGive 28
Weapon.SlotNumber 5
Inventory.Pickupmessage "$PU_DANGERWRAP"
Obituary "$OB_DANGERWRAP"
Tag "$TAG_DANGERWRAP"
weapon.ammotype "CrocDangerWrapAmmo"
inventory.icon "DWRASI"
States
{
SpawnLoop:
WEA3 L -1
loop
Ready:
DWRA J 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_DANGERWRAP)
DWRA J 1 A_WeaponReady
Goto Ready+1
Deselect:
DWRA J 0
goto DeselectSwap
Select:
DWRA J 0
goto SelectSwap
Fire:
DWRA J 0 A_JumpIfNoAmmo("NoAmmo")
DWRA J 0 A_PlaySoundEx("weapons/mm7/dangerwrapfire","Weapon")
DWRA J 0 A_FireCustomMissile("CrocDangerWrap",0,1,8,0)
DWRA KL 6
DWRA J 13
DWRA J 0 A_Refire
Goto Ready+1
NoAmmo:
DWRA J 1 ACS_NamedExecuteAlways("core_noammo",0)
Goto Ready+1
}
}

actor CrocDangerWrapAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}


actor CrocDangerWrap : BasicProjectile
{
+DONTREFLECT
Damage (0)
Radius 32
Height 12
speed 15
States
{
Spawn:
DWRA A 0
DWRA ABC 3
Goto Fly
Fly:
DWRA DEDF 5 ThrustThingZ(0,2,0,1)
loop
Death:
DWRA G 0 A_SpawnItemEx("SoakifyEffect")
DWRA G 2 A_ChangeFlag("NOGRAVITY",0)
TNT1 A 0 A_SetGravity(2)
DWRA G 1 A_JumpIf(z-floorz==0,"SpawnMine")
NoMine:
DWRA GGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGGG 1 A_JumpIf(z-floorz==0,"SpawnMine")
Crash:
XDeath:
DWRA G 0 A_Stop
DWRA G 0 A_SpawnItemEx("CrocWrapExplosion", 0, 0, 0, 0, 0, 0, 0, SXF_WEPFXCOLOR)
stop
SpawnMine:
TNT1 A 1 A_SpawnItemEx("CrocDangerWrapMine")
stop
}
}

actor CrocWrapExplosion : BasicExplosion
{
damagetype "CrocDangerWrap"
Obituary "$OB_DANGERWRAP"
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_PlaySoundEx("weapons/mm7/dangerwrapexplode", "Weapon")
TNT1 D 0 A_Explode(53, 128, 0)
TNT1 A 35 A_SpawnItemEx("ExplosionEffect2", 0, 0, 0, 0, 0, 0, 0, SXF_WEPFXCOLOR)
stop
}
}

actor CrocDangerWrapMine : BasicProjectile
{
+NOCLIP
-NOGRAVITY
Radius 32
Height 12
damage (1)
damagetype "CrocDangerWrap"
Obituary "$OB_DANGERWRAP"
speed 0
reactiontime 200
States
{
Spawn:
DWRA I 0
DWRA I 0 A_JumpIf(CallACS("core_checkmode", 0)==4 || CallACS("core_checkmode", 0)==6,"Death")
DWRA I 0 A_SetArg(0, 200)
DWRA I 5 bright
DWRA G 0 A_ChangeFlag("NOCLIP",0)
Goto Mine
Mine:
DWRA H 1 A_CountDownArg(0)
DWRA H 0 A_JumpIf(args[0] < 50, "MineLow")
DWRA H 0 A_JumpIfInTargetInventory("IsDead",1,"Death")
loop
MineLow:
DWRA HHHHH 1 A_CountDownArg(0)
DWRA IIIII 1 bright A_CountDownArg(0)
DWRA H 0 A_JumpIfInTargetInventory("IsDead",1,"Death")
loop
Death:
TNT1 A 0 A_Stop
TNT1 A 0 A_ChangeFlag("NOGRAVITY",1)
TNT1 A 0 A_SpawnItemEx("CrocWrapExplosion", 0, 0, 0, 0, 0, 0, 0, SXF_WEPFXCOLOR)
stop
}
}
