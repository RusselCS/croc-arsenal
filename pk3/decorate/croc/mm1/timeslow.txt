actor CrocTimeSlowWep_NormalBar : NormalBar { args 34, 239 }
actor CrocTimeSlowWep_ScriptBar : ScriptBar {}

actor CrocTimeSlowWep : BaseMM8BDMWep 11010
{
//$Category MM8BDM-Weapons/AoE
//$Title Time Slow
//$Sprite SLOTX0
Weapon.AmmoUse 14
Weapon.AmmoGive 28
Weapon.SlotNumber 6
Inventory.Pickupmessage "$PU_TIMESLOW"
Obituary "$OB_TIMESLOW"
Tag "$TAG_TIMESLOW"
weapon.ammotype "CrocTimeSlowAmmo"
Inventory.Icon "TIMMI2"
States
{
SpawnLoop:
SLOT X -1
loop
Ready:
TSLO A 0 ACS_NamedExecuteWithResult("core_weaponcolor", CLR_TIMESLOW)
TSLO A 1 A_WeaponReady
goto Ready+1
Deselect:
TSLO A 0
goto DeselectSwap
Select:
TSLO A 0
goto SelectSwap
Fire:
TSLO A 0 
TSLO A 0 A_JumpIfInventory("CrocPowerTime",1,"Ready")
goto Fire1
Fire1:
TSLO A 0 A_JumpIfInventory("CrocTimeSlowAmmo", 14, "Fire2")
Goto NoAmmo
Fire2:
//With Hand
TSLO A 0 A_JumpIfNoAmmo("NoAmmo")
TSLO A 0 A_JumpIfInventory("CrocTimeSlowCooldown", 1, "NoAmmo")
TSLO A 0 A_ScaleVelocity(0.5)
SLOT JKLMN 2
TNT1 A 0 A_PlaySoundEx("weapons/mm1/timeslowfire","Weapon")
TNT1 A 0 ACS_NamedExecuteAlways("core_whiteflash",0)
TNT1 A 0 A_FireCustomMissile("CrocTimeSlow",0,1,8,0)
TNT1 A 0 A_GiveInventory("CrocTimeSlowCooldown", 1)
TNT1 A 0 A_SpawnItemEx("CrocTimeSlowCooldowner")
//SLOT LMN 3
TNT1 A 2
TSLO BC 1
TSLO A 1
goto Ready+1
NoAmmo: 
TSLO A 1 ACS_NamedExecuteAlways("core_noammo",0)
goto Ready+1
NotReady:
TSLO A 1
Goto Ready+1
}
}

actor CrocTimeSlowCooldown : Once {}

actor CrocTimeSlowCooldowner : BasicCooldowner
{
reactiontime 35
States
{
Spawn:
TNT1 A 0
Spawn2:
TNT1 A 1 A_GiveInventory("CrocTimeSlowCooldownerFunc",1)
loop
Death:
TNT1 A 0 A_TakeFromTarget("CrocTimeSlowCooldown",1)
stop
}
}

actor CrocTimeSlowCooldownerFunc : BasicCooldownerFunc
{
States
{
Pickup:
TNT1 A 0 A_JumpIfInTargetInventory("CrocTimeSlowCooldown",1,"PickupBasic")
goto PickupRemove
}
}

actor CrocTimeSlowAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}

actor CrocTimeSlow : BasicExplosion
{
damagetype "TimeSlow"
Obituary "$OB_TIMESLOW"
Scale 5.16
+FORCEXYBILLBOARD
States
{
Spawn:
TNT1 A 0
TNT1 A 1 A_Explode(13, 640 ,0)
SLOD EFGHIJKLMNEFGHIJKLMN 1
stop
}
}

actor CrocTimeCoolDown : PowerupGiver
{
inventory.maxamount 0
powerup.duration 245
powerup.color "00 00 00", 0.0
powerup.type "CrocPowerTime"
+INVENTORY.AUTOACTIVATE
+INVENTORY.ALWAYSPICKUP
states
{
Pickup:
TNT1 A 175 
stop
}
}


actor CrocPowerTime : PowerUp
{
Inventory.Icon "TIMICON"
Speed 1.0
}

actor CrocSlowed : CustomInventory
{
+INVENTORY.AUTOACTIVATE
-INVBAR
States
{
Use:
TNT1 A 0 A_JumpIfInventory("CrocPowerSlow", 1, 2)
TNT1 A 0 A_SpawnItemEx("CrocTimeSlowWatcher")
TNT1 A 0 A_GiveInventory("CrocSlowedPower", 1)
TNT1 A 0 A_GiveInventory("CrocPowerSlowFade", 1)
stop
}
}

actor CrocSlowedPower : PowerupGiver
{
Speed 0.67
powerup.duration 105
powerup.type "CrocPowerSlow"
+INVENTORY.AUTOACTIVATE
+INVENTORY.ALWAYSPICKUP
states
{
Pickup:
TNT1 A 0 

TNT1 A 175
stop
}
}

actor CrocPowerSlow : PowerSpeed
{
Speed 0.67
//Powerup.Color "55 00 55", 0.4
}

actor CrocPowerSlowFade : Powerup
{
powerup.duration 0x7FFFFFFD
Powerup.Color "55 00 55", 0.4
}

actor CrocTimeSlowWatcher
{
Height 0
Radius 0
+NOINTERACTION
-SOLID
+NOGRAVITY
+MISSILE
States
{
Spawn:
TNT1 A 0 
TNT1 A 3 A_GiveToTarget("CrocTimeSlowFX", 1)
TNT1 A 0 A_JumpIfInTargetInventory("CrocPowerSlow", 1, "Spawn")
TNT1 A 0 A_TakeFromTarget("CrocPowerSlowFade", 1)
stop
}
}

actor CrocTimeSlowFX : CustomInventory
{
+INVENTORY.AUTOACTIVATE
-INVBAR
States
{
Use:
TNT1 A 0
TNT1 A 0 A_SpawnItemEx("CrocTimeSlowGFX", random(16, 48), 0, Random(16,48), 0, 0, 0, random(0, 359))
stop
}
}

actor CrocTimeSlowGFX : BasicGraphicEffect
{
+FLOATBOB
+FORCEXYBILLBOARD
+BRIGHT
Height 0
Radius 0
States
{
Spawn:
SLOD DCBA 4 A_Fadeout(0.2)
loop
}
}
