actor CrocYamatoSpearWep_NormalBar : NormalBar { args 4, 236 }

actor CrocYamatoSpearWep : BaseMM8BDMWep 10067
{
//$Category MM8BDM-Weapons/Rapid
//$Title Yamato Spear
//$Sprite WEA2R0
Weapon.AmmoUse 2
Weapon.AmmoGive 28
Weapon.SlotNumber 3
Inventory.Pickupmessage "$PU_YAMATOSPEAR"
Obituary "$OB_YAMATOSPEAR"
Tag "$TAG_YAMATOSPEAR"
weapon.ammotype "CrocYamatoSpearAmmo"
inventory.icon "YAMASI"
States
{
SpawnLoop:
WEA2 R -1
loop
Ready:
YAMA B 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_YAMATOSPEAR)
YAMA B 1 A_WeaponReady
Goto Ready+1
Deselect:
YAMA B 0
goto DeselectSwap
Select:
YAMA B 0
goto SelectSwap
Fire:
Hold:
YAMA B 0 A_JumpIfNoAmmo("NoAmmo")
YAMA B 0 A_PlaySoundEx("weapons/mm6/yamatospearfire","Weapon")
YAMA C 0 A_FireCustomMissile("CrocYamatoSpear",0,0,8,0)
YAMA C 2
YAMA D 2
YAMA B 1
YAMA B 0 A_PlaySoundEx("weapons/mm6/yamatospearfire","Weapon")
YAMA D 0 A_FireCustomMissile("CrocYamatoSpear",3,0,8,0)
YAMA C 2
YAMA D 2
YAMA B 1
YAMA B 0 A_PlaySoundEx("weapons/mm6/yamatospearfire","Weapon")
YAMA B 0 A_FireCustomMissile("CrocYamatoSpear",-3,1,8,0)
YAMA C 2
YAMA D 2
YAMA B 1
YAMA B 0 A_Refire
Goto Ready+1
NoAmmo:
YAMA B 1 ACS_NamedExecuteAlways("core_noammo",0)
Goto Ready+1
}
}

actor CrocYamatoSpearAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}


actor CrocYamatoSpear : BasicProjectile
{
Radius 5
Height 5
+RIPPER
damage (9)
damagetype "YamatoSpear"
Obituary "$OB_YAMATOSPEAR"
speed 35
States
{
Spawn:
YAMA A 1
loop
}
}
