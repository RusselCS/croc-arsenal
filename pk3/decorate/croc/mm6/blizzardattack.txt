actor CrocBlizzardAttackWep_NormalBar : NormalBar { args 4, 197 }

actor CrocBlizzardAttackWep : BaseMM8BDMWep 10051
{
//$Category MM8BDM-Weapons/Ranged
//$Title Blizzard Attack
//$Sprite WEAPW0
Weapon.AmmoUse 2
Weapon.AmmoGive 28
Weapon.SlotNumber 2
Inventory.Pickupmessage "$PU_BLIZZARDATTACK"
Obituary "$OB_BLIZZARDATTACK"
Tag "$TAG_BLIZZARDATTACK"
weapon.ammotype "CrocBlizzardAttackAmmo"
inventory.icon "BLIZSI"
States
{
SpawnLoop:
WEAP W -1
loop
Ready:
BLIZ D 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_BLIZZARDATTACK)
BLIZ D 1 A_WeaponReady
Goto Ready+1
Deselect:
BLIZ D 0
goto DeselectSwap
Select:
BLIZ D 0
goto SelectSwap
Fire:
BLIZ D 0 A_JumpIfNoAmmo("NoAmmo")
BLIZ D 0 A_PlaySoundEx("weapons/mm6/blizzardattackfire","Weapon")
BLIZ D 0 A_FireCustomMissile("CrocBlizzardAttack",12,1,52,0)
BLIZ D 0 A_FireCustomMissile("CrocBlizzardAttack",-12,0,-52,0)
BLIZ EE 2
BLIZ F 1
BLIZ D 0 A_FireCustomMissile("CrocBlizzardAttack",0,0,32,0)
BLIZ D 0 A_FireCustomMissile("CrocBlizzardAttack",0,0,-32,0)
BLIZ FD 5
BLIZ D 20
BLIZ D 0 A_Refire
Goto Ready+1
NoAmmo:
BLIZ D 1 ACS_NamedExecuteAlways("core_noammo",0)
Goto Ready+1
}
}

actor CrocBlizzardAttackAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}

actor CrocBlizzardAttack : BasicProjectile
{
+FORCEXYBILLBOARD
Damage (17)
damagetype "CrocBlizzardAttack"
Obituary "$OB_BLIZZARDATTACK"
Height 12
Radius 12
Speed 40
States
{
Spawn:
BLIZ ABC 5
BLIZ C 1 
wait
Death:
TNT1 A 0 A_SpawnItemEx("IcePitFreeze")
stop
}
}
