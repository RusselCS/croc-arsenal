actor CrocSilverTomahawkWep_NormalBar : NormalBar { args 52, 220 }

actor CrocSilverTomahawkWep : BaseMM8BDMWep
{
//$Category MM8BDM-Weapons/Ranged
//$Title Silver Tomahawk
//$Sprite WEA2U0
Weapon.AmmoUse 2
Weapon.AmmoGive 28
Weapon.SlotNumber 2
Inventory.Pickupmessage "$PU_SILVERTOMAHAWK"
Obituary "$OB_SILVERTOMAHAWK"
TAG "$TAG_SILVERTOMAHAWK"
weapon.ammotype "CrocSilverTomahawkAmmo"
inventory.icon "TOMASI"
States
{
SpawnLoop:
WEA2 U -1
loop
Ready:
TOMA C 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_SILVERTOMAHAWK)
TOMA C 1 A_WeaponReady
Goto Ready+1
Deselect:
TOMA C 0
goto DeselectSwap
Select:
TOMA C 0
goto SelectSwap
Fire:
TOMA C 0 A_JumpIfNoAmmo("NoAmmo")
TOMA C 0 A_PlaySoundEx("weapons/mm6/silvertomahawkfire","Weapon")
TOMA C 0 A_FireCustomMissile("CrocSilverTomahawk",0,1,8,0)
TOMA ED 2
TOMA C 20
TOMA C 0 A_Refire
Goto Ready+1
NoAmmo:
TOMA C 1 ACS_NamedExecuteAlways("core_noammo",0)
Goto Ready+1
}
}

actor CrocSilverTomahawkAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}

actor CrocSilverTomahawk : BasicProjectile
{
+NOEXPLODEFLOOR
Radius 16
Height 7
damage (30)
speed 50
damagetype "SilverTomahawk"
Obituary "$OB_SILVERTOMAHAWK"
States
{
Spawn:
TOMA A 1 //ThrustThingZ(0, 3, 0, 1)
SpawnLoop:
TOMA B 1 ThrustThingZ(0, 2, 0, 1)
TOMA A 1 ThrustThingZ(0, 3, 0, 1)
loop
Death:
TOMA B 1
stop
}
}
