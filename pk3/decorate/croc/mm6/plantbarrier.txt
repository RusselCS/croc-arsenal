actor CrocPlantBarrierWep_NormalBar : NormalBar { args 4, 39 }
actor CrocPlantBarrierWep_ScriptBar : ScriptBar {}

actor CrocPlantBarrierWep : BaseMM8BDMWep 10069
{
//$Category MM8BDM-Weapons/Shield
//$Title Plant Barrier
//$Sprite WEA2T0
Weapon.AmmoUse 2
Weapon.AmmoGive 28
Weapon.SlotNumber 7
Inventory.Pickupmessage "$PU_PLANTBARRIER"
Obituary "$OB_PLANTBARRIER"
Tag "$TAG_PLANTBARRIER"
weapon.ammotype "CrocPlantBarrierAmmo"
inventory.icon "PLANTSI"
States
{
SpawnLoop:
WEA2 T -1
loop
Ready:
PLAA A 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_PLANTBARRIER)
PLAA A 0 A_ClearRefire
PLAA A 1 A_WeaponReady
Goto Ready+2
Deselect:
PLAA A 0 A_TakeInventory("CrocPlantWeakener", 1)
PLAA A 0 A_JumpIfInventory("ShieldCheck",1,1)
goto DeselectSwap
PLAA A 1 A_TakeInventory("ShieldCheck",1)
goto DeselectSwap
Select:
PLAA A 0
goto SelectSwap
Fire:
PLAA A 0 A_JumpIfInventory("ShieldCheck",1,"ShieldThrow")
PLAA A 0 A_JumpIfNoAmmo("NoAmmo")
PLAA A 0 A_PlaySoundEx("weapons/mm6/plantbarrieractivate","Weapon")
PLAA A 1 A_GiveInventory("ShieldCheck",1)
goto ShieldActive
ShieldActive:
PLAA B 0 A_SpawnItemEx("CrocPlantBarrierWarper")
PLAA BCDEFGH 2
PLAA A 0 A_GiveInventory("CrocPlantWeakener",1)
Goto ShieldLoop
ShieldLoop:
PLAA AA 1 A_WeaponReady
PLAA A 0 A_GiveInventory("CrocPlantBarrierHealer", 1)
PLAA A 0 A_JumpIfNoAmmo("ShieldThrow")
PLAA A 0 A_JumpIfInventory("ShieldCheck", 1, "ShieldLoop")
Goto ShieldThrow
ShieldThrow:
PLAA A 0 A_TakeInventory("ShieldCheck",999)
PLAA A 0 A_JumpIfNoAmmo("ShieldThrow2")
PLAA A 0 A_PlaySoundEx("weapons/mm6/plantberrierfire","Weapon")
PLAA A 0 A_FireCustomMissile("CrocPlantBarrier", 0, 1, 0, 0)
Goto ShieldThrow2
ShieldThrow2:
PLAA A 0 A_TakeInventory("CrocPlantWeakener",9999)
PLAA IJ 2
PLAA K 6
PLAA JI 3
PLAA AAAA 1 
goto Ready+1
NoAmmo:
PLAA A 0 ACS_NamedExecuteAlways("core_noammo",0)
PLAA A 0 A_TakeInventory("CrocPlantWeakener", 1)
PLAA A 1 A_TakeInventory("ShieldCheck",999)
PLAA A 0 A_Refire
goto Ready+1
}
}

actor CrocPlantWeakener : PowerProtection
{
damagefactor "normal", 1.3
powerup.duration 0x7FFFFFFD
}

actor CrocPlantBarrierWarper : BasicShieldWarper
{
	States
	{
	Shield:
	PLAN ABCDEFGHIJKLMNOPQR 1 A_Warp(AAPTR_TARGET, 0, 0, 0, 0, WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION)
	loop
	}
}

actor CrocPlantBarrierHealer : CustomInventory
{
	States
	{
	Pickup:
		//TNT1 A 0 A_JumpIf(health >= 100, "Failed")
		TNT1 A 0 A_JumpIf(!CallACS("core_CanGainHealth"), "Failed")
		TNT1 A 0 A_JumpIfInventory("CrocPlantBarrierAmmoCooldown", 1, "Heal")
		TNT1 A 0 A_GiveInventory("CrocPlantBarrierAmmoCooldown", 1)
		TNT1 A 0 A_TakeInventory("CrocPlantBarrierAmmo",1,TIF_NOTAKEINFINITE)
		goto Heal
	Heal:
		//TNT1 A 0 HealThing(1)
		TNT1 A 0 ACS_NamedExecuteWithResult("core_HealScript",1)
		stop
	Failed:
		TNT1 A 0
		stop
	}
}

actor CrocPlantBarrierAmmoCooldown : Powerup
{
	powerup.duration 10
}

actor CrocPlantBarrierAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}

actor CrocPlantBarrier : BasicProjectile
{
+DONTSPLASH
Damage (33)
damagetype "CrocPlantBarrier"
Obituary "$OB_PLANTBARRIER"
Speed 32
Radius 12
Height 12
States
{
Spawn:
PLN1 A 0
PLN1 A 0 ACS_NamedExecuteWithResult("core_changeClientProperty", APROP_Speed, 3932160, AAPTR_TARGET) // 60 * 65536
SpawnLoop:
PLN1 ABCDEFGHIJ 1
loop
}
}
