actor CrocFlameBlastWep_NormalBar : NormalBar { args 52, 225 }

actor CrocFlameBlastWep : BaseMM8BDMWep 10066
{
//$Category MM8BDM-Weapons/Close
//$Title Flame Blast
//$Sprite WEA2P0
Weapon.AmmoUse 2
Weapon.AmmoGive 28
Weapon.SlotNumber 4
Inventory.Pickupmessage "$PU_FLAMEBLAST"
Obituary "$OB_FLAMEBLAST"
Tag "$TAG_FLAMEBLAST"
weapon.ammotype "CrocFlameBlastAmmo"
inventory.icon "FLMBSI"
States
{
SpawnLoop:
WEA2 P -1
loop
Ready:
FLMB P 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_FLAMEBLAST)
FLMB P 1 A_WeaponReady
Goto Ready+1
Deselect:
FLMB P 0
goto DeselectSwap
Select:
FLMB P 0
goto SelectSwap
Fire:
FLMB P 0 A_JumpIfNoAmmo("NoAmmo")
FLMB P 0 A_PlaySoundEx("weapons/mm6/flameblastfire","Weapon")
FLMB P 0 A_FireCustomMissile("CrocFlameBlast",0,1,8,0)
FLMB QRP 5
FLMB P 0 A_Refire
Goto Ready+1
NoAmmo:
FLMB P 1 ACS_NamedExecuteAlways("core_noammo",0)
Goto Ready+1
}
}

actor CrocFlameBlastAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}

actor CrocFlameBlast : BasicProjectile
{
+BRIGHT
Damage (40)
Height 5
Radius 6
damagetype "CrocFlameBlast"
Obituary "$OB_FLAMEBLAST"
-NOGRAVITY
speed 28
States
{
Spawn:
FLMB H 0
FLMB H 1 ThrustThingZ(0,20,0,1)
FLMB H 4 A_CheckFloor("Death")
goto Spawn+2
XDeath:
Crash:
FLMB H 0 A_SpawnItemEx("OilPitIgnite",0,0,8)
stop
Death:
FLMB H 0 A_JumpIf(CeilingZ - FloorZ <=48 , "FireCheckOnly")
FLMB H 0 A_SpawnItemEx("OilPitIgnite",0,0,8)
FLMB H 0 A_CheckCeiling("Ceiling")
FLMB H 0 A_JumpIf(Z - CeilingZ > -8, "Ceiling")
FLMB H 0 A_CheckFloor("Floor")
FLMB H 0 A_JumpIf(CeilingZ-Z < 8, "Floor")
Goto Wall
Wall:
FLMB H 0 A_SpawnItemEx("CrocFlameWallPillar",-24,0,0)
stop
Ceiling:
FLMB H 0 A_SpawnItemEx("CrocFlameBlast2",0,0,-1)
stop
Floor:
FLMB H 0 A_SpawnItemEx("CrocFlamePillar",0,0,0)
stop
FireCheckOnly:
TNT1 A 0 A_SpawnItemEx("OilPitIgnite",0,0,-16)
stop
}
}

actor CrocFlameBlast2 : FlameBlast
{
States
{
Spawn:
TNT1 A 0
goto "Super::Spawn+2"
}
}

actor CrocFlameWallPillar : BasicProjectile
{
+BRIGHT
Height 5
damagetype "CrocFlameBlast"
Obituary "$OB_FLAMEBLAST"
Speed 0
Radius 16
Damage (40)
reactiontime 15
States
{
Spawn:
FLMB I 0
FLMB H 0 A_SpawnItemEx("OilPitIgnite",0,0,0)
FLMB IJKLMNONONONONO 1 
FLMB A 0 A_CountDown
Loop
Death:
FLMB A 0 
stop
}
}

actor CrocFlamePillar : BasicProjectile
{
+BRIGHT
-MISSILE
-NOGRAVITY
+NOEXPLODEFLOOR
Height 48
damagetype "CrocFlameBlast"
Obituary "$OB_FLAMEBLAST"
Speed 0
Radius 5
Damage (40)
reactiontime 15
States
{
Spawn:
FLMB H 0 
FLMB H 0 A_ChangeFlag("MISSILE",1)
FLMB H 1 A_CheckFloor("Flame")
Goto Spawn+2
Flame:
TNT1 A 0 A_SpawnItemEx("OilPitIgnite",0,0,8)
FLMB A 0 A_ChangeFlag("FLOORHUGGER",1)
FLMB A 0 A_CountDown
FLMB ABCDEFGFGFGFGFG 1 
Goto Flame+2
Death:
FLMB A 0 
stop
}
}
