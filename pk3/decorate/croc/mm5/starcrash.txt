actor CrocStarCrashWep_NormalBar : NormalBar { args 229, 220 }
actor CrocStarCrashWep_ScriptBar : ScriptBar {}

actor CrocStarCrashWep : BaseMM8BDMWep 10073
{
//$Category MM8BDM-Weapons/Shield
//$Title Star Crash
//$Sprite WEA2X0
Weapon.AmmoUse 1
Weapon.AmmoGive 28
Weapon.SlotNumber 7
Inventory.Pickupmessage "$PU_STARCRASH"
Obituary "$OB_STARCRASH"
Tag "$TAG_STARCRASH"
weapon.ammotype "CrocStarCrashAmmo"
inventory.icon "STARSI"

States
{
SpawnLoop:
WEA2 X -1
loop
Ready:
STAA A 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_STARCRASH)
STAA A 0 A_ClearRefire
STAA A 1 A_WeaponReady
Goto Ready+2
Deselect:
STAA A 0 A_JumpIfInventory("ShieldCheck",1,1)
goto DeselectSwap
STAA A 0 A_TakeInventory("ShieldCheck",1)
goto DeselectSwap
Select:
STAA A 0
goto SelectSwap
Fire:
STAA A 0 A_JumpIfInventory("ShieldCheck",1,"ShieldThrow")
STAA A 0 A_JumpIfNoAmmo("NoAmmo")
STAA A 0 //A_TakeInventory("BasicArmor",9999)
STAA A 0 A_TakeInventory("CrocStarCrashAmmo",1,TIF_NOTAKEINFINITE)
STAA A 0 A_PlaySoundEx("weapons/mm5/starcrashactivate","Weapon")
STAA A 1 A_GiveInventory("ShieldCheck",1)
goto ShieldActive
ShieldActive:
STAA F 0 A_SpawnItemEx("CrocStarCrashWarper")
//TNT1 A 0 A_GunFlash
STAA BC 2
TNT1 A 0 A_SpawnItemEx("CrocStarRadius",0,0,28)
STAA DE 2 
TNT1 A 0 A_SpawnItemEx("CrocStarRadius",0,0,28)
STAA FG 2 
TNT1 A 0 A_SpawnItemEx("CrocStarRadius",0,0,28)
STAA H 2
ShieldLoop:
STAA IIII 1 A_WeaponReady
TNT1 A 0 A_SpawnItemEx("CrocStarRadius",0,0,28)
STAA IIII 1 A_WeaponReady
TNT1 A 0 A_SpawnItemEx("CrocStarRadius",0,0,28)
TNT1 A 0 A_TakeInventory("CrocStarCrashAmmo",1,TIF_NOTAKEINFINITE)
TNT1 A 0 A_JumpIfNoAmmo("NoAmmo")
loop
ShieldThrow:
STAA A 0 A_TakeInventory("ShieldCheck",999)
STAA A 0 A_PlaySoundEx("weapons/mm5/starcrashfire","Weapon")
STAA I 1 A_FireCustomMissile("CrocStarCrash",0,0,0,0)
STAA JK 1
STAA K 22
STAA J 2
goto Ready+1
NoAmmo:
STAA A 1 ACS_NamedExecuteAlways("core_noammo",0)
STAA A 0 A_TakeInventory("ShieldCheck",999)
STAA A 0 A_Refire
goto Ready+1
/*Flash:
STAA BC 2
STAA DE 2
STAA FG 2
STAA H 2
FlashLoop:
STAA I 1
STAA A 0 A_JumpIfInventory("ShieldCheck", 1, "FlashLoop")
stop*/
}
}

actor CrocStarRadius : BasicExplosion
{
Damagetype "Starcrash"
Obituary "$OB_STARCRASH"
States
{
Spawn:
TNT1 A 0
TNT1 A 1 A_Explode(17,128,0)
stop
}
}

actor CrocStarCrashWarper : BasicShieldWarper
{
	+BRIGHT
	States
	{
	Shield:
	STAR AABBCCDD 1 A_Warp(AAPTR_TARGET, 0, 0, 16, 0, WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION)
	loop
	}
}

actor CrocStarCrash1 : BasicGraphicEffect
{
+DONTSPLASH
+NOTIMEFREEZE
+BRIGHT
States
{
SpawnFrame:
STAR A 1
STAR A 1
stop
}
}

actor CrocStarCrash2 : CrocStarCrash1
{
States
{
SpawnFrame:
STAR B 1
STAR B 1 
stop
}
}

actor CrocStarCrash3 : CrocStarCrash1
{
States
{
SpawnFrame:
STAR C 1
STAR C 1
stop
}
}

actor CrocStarCrash4 : CrocStarCrash1
{
States
{
SpawnFrame:
STAR D 1
STAR D 1
stop
}
}

actor CrocStarCrash : BasicProjectile
{
+DONTSPLASH
+BRIGHT
Damage (40)
damagetype "StarCrash"
Obituary "$OB_STARCRASH"
Speed 30
Radius 12
Height 12
States
{
Spawn:
STAR E 0
STAR E 0 ACS_NamedExecuteWithResult("core_changeClientProperty", APROP_Speed, 3932160, AAPTR_TARGET) // 60 * 65536
SpawnLoop:
STAR EFGH 2
loop
}
}

actor CrocStarCrashAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}
