actor CrocCrystalEyeWep_NormalBar : NormalBar { args 4, 193 }

actor CrocCrystalEyeWep : BaseMM8BDMWep 10064
{
//$Category MM8BDM-Weapons/Power
//$Title Crystal Eye
//$Sprite WEA2N0
Weapon.AmmoUse 2
Weapon.AmmoGive 28
Weapon.SlotNumber 5
Inventory.Pickupmessage "$PU_CRYSTALEYE"
Obituary "$OB_CRYSTALEYE"
Tag "$TAG_CRYSTALEYE"
weapon.ammotype "CrocCrystalEyeAmmo"
inventory.icon "CRYSSI"
States
{
SpawnLoop:
WEA2 N -1
loop
Ready:
CRYE K 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_CRYSTALEYE)
CRYE K 1 A_WeaponReady
Goto Ready+1
Deselect:
CRYE K 0
goto DeselectSwap
Select:
CRYE K 0
goto SelectSwap
Fire:
CRYE K 0 A_JumpIfNoAmmo("NoAmmo")
CRYE K 0 A_PlaySoundEx("weapons/mm5/crystaleyefire","Weapon")
CRYE K 0 A_FireCustomMissile("CrocCrystalEyeShooter",0,1,8,0)
CRY0 ABCDEF 2
CRY0 G 35 // 55 minus frames above, and frames below
CRY0 G 0 ACS_NamedExecuteWithResult("core_clientsound", CSND_CRYSTALRELOAD) // A_PlaySoundEx("weapons/mm5/crystaleyereload", "Weapon")
CRY0 HIJK 2
CRYE K 0 A_Refire
Goto Ready+1
NoAmmo:
CRYE K 0 ACS_NamedExecuteAlways("core_noammo",0)
Goto Ready+1
}
}

actor CrocCrystalEyeAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}

actor CrocCrystalEyeShooter
{
    PROJECTILE
    Height 0
    Radius 0
    +NOINTERACTION
    +NOCLIP
    speed 1
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 ACS_NamedExecuteWithResult("core_SetVelPitch",0,0)
        TNT1 A 1 A_CustomMissile("CrocCrystalEye",0,0,0,CMF_AIMDIRECTION|CMF_SAVEPITCH,pitch)
        stop
    }
}

actor CrocCrystalEye : BasicProjectile
{
+FORCEXYBILLBOARD
Damage (50)
Height 16
Radius 16
damagetype "CrocCrystalEye"
Obituary "$OB_CRYSTALEYE"
+EXPLODEONWATER
speed 28
States
{
Spawn:
CRYE ABCDEAA 4
loop
Death:
// [Russ] Uses A_SpawnItemEx instead of A_CustomMissile becase the latter doesn't spawn bits at certain pitches.
TNT1 A 0 A_SpawnItemEx("CrocCrystalEyeBit", 0, 0, 0, cos(pitch), 0, sin(pitch), -15, SXF_MULTIPLYSPEED)
TNT1 A 0 A_SpawnItemEx("CrocCrystalEyeBit", 0, 0, 0, cos(pitch), 0, sin(pitch), 0, SXF_MULTIPLYSPEED)
TNT1 A 0 A_SpawnItemEx("CrocCrystalEyeBit", 0, 0, 0, cos(pitch), 0, sin(pitch), 15, SXF_MULTIPLYSPEED)
TNT1 A 0 A_SpawnItemEx("CrocCrystalEyeBit", 0, 0, 0, cos(pitch-15), 0, sin(pitch-15), -15, SXF_MULTIPLYSPEED)
TNT1 A 0 A_SpawnItemEx("CrocCrystalEyeBit", 0, 0, 0, cos(pitch-15), 0, sin(pitch-15), 0, SXF_MULTIPLYSPEED)
TNT1 A 0 A_SpawnItemEx("CrocCrystalEyeBit", 0, 0, 0, cos(pitch-15), 0, sin(pitch-15), 15, SXF_MULTIPLYSPEED)
TNT1 A 0 A_SpawnItemEx("CrocCrystalEyeBit", 0, 0, 0, cos(pitch+15), 0, sin(pitch+15), -15, SXF_MULTIPLYSPEED)
TNT1 A 0 A_SpawnItemEx("CrocCrystalEyeBit", 0, 0, 0, cos(pitch+15), 0, sin(pitch+15), 0, SXF_MULTIPLYSPEED)
TNT1 A 0 A_SpawnItemEx("CrocCrystalEyeBit", 0, 0, 0, cos(pitch+15), 0, sin(pitch+15), 15, SXF_MULTIPLYSPEED)
TNT1 A 0 A_SpawnItemEx("ExplosionEffect1",0,0,0,0,0,0,0,SXF_WEPFXCOLOR)
TNT1 A 0 A_PlaySoundEx("weapons/mm5/crystaleyesplit", "auto")
TNT1 A 35
stop
XDeath:
TNT1 A 0 A_PlaySoundEx("weapons/mm5/crystaleyehit", "auto")
TNT1 A 35
stop
}
}

actor CrocCrystalEyeBit : BasicBouncer
{
PROJECTILE
Scale 2.5
Height 4
Radius 4
Damage(10)
+FORCEXYBILLBOARD
+HITTARGET
damagetype "CrocCrystalEyeBit"
Obituary "$OB_CRYSTALEYE"
// bouncesound "weapons/mm5/crystaleyebounce"
bouncefactor 1.0
speed 28
reactiontime 4
States
{
Spawn:
CRYE A 0 A_CountDown
CRYE FGHIJFF 4
loop
Death:
TNT1 A 0 A_SpawnItemEx("ExplosionEffect1", 0, 0, 0, 0, 0, 0, 0, SXF_WEPFXCOLOR)
stop
}
}

actor CrocCrystalEyeHit : CustomInventory
{
    States
    {
    Pickup:
        TNT1 A 0 A_JumpIfInventory("CrocCrystalEyeStack6", 1, "Give7")
        TNT1 A 0 A_JumpIfInventory("CrocCrystalEyeStack5", 1, "Give6")
        TNT1 A 0 A_JumpIfInventory("CrocCrystalEyeStack4", 1, "Give5")
        TNT1 A 0 A_JumpIfInventory("CrocCrystalEyeStack3", 1, "Give4")
        TNT1 A 0 A_JumpIfInventory("CrocCrystalEyeStack2", 1, "Give3")
        TNT1 A 0 A_JumpIfInventory("CrocCrystalEyeStack1", 1, "Give2")
        goto Give1
    Give7:
        TNT1 A 0 A_GiveInventory("CrocCrystalEyeProtect", 1)
    Give6:
        TNT1 A 0 A_GiveInventory("CrocCrystalEyeStack6", 1)
    Give5:
        TNT1 A 0 A_GiveInventory("CrocCrystalEyeStack5", 1)
    Give4:
        TNT1 A 0 A_GiveInventory("CrocCrystalEyeStack4", 1)
    Give3:
        TNT1 A 0 A_GiveInventory("CrocCrystalEyeStack3", 1)
    Give2:
        TNT1 A 0 A_GiveInventory("CrocCrystalEyeStack2", 1)
    Give1:
        TNT1 A 0 A_GiveInventory("CrocCrystalEyeStack1", 1)
        stop
    }
}

actor CrocCrystalEyeStack1 : PowerProtection
{
    powerup.duration 5
    damagefactor "Normal", 1.0
}

actor CrocCrystalEyeStack2 : CrocCrystalEyeStack1 {}
actor CrocCrystalEyeStack3 : CrocCrystalEyeStack1 {}
actor CrocCrystalEyeStack4 : CrocCrystalEyeStack1 {}
actor CrocCrystalEyeStack5 : CrocCrystalEyeStack1 {}
actor CrocCrystalEyeStack6 : CrocCrystalEyeStack1 {}
actor CrocCrystalEyeStack7 : CrocCrystalEyeStack1 {}
actor CrocCrystalEyeStack8 : CrocCrystalEyeStack1 {}
actor CrocCrystalEyeStack9 : CrocCrystalEyeStack1 {}

actor CrocCrystalEyeProtect : CrocCrystalEyeStack1
{
    damagefactor "CrocCrystalEyeBit", 0.0
}
