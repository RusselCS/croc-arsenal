actor CrystalEyeWep_NormalBar : NormalBar { args 4, 193 }

actor CrystalEyeWep : BaseMM8BDMWep 10064
{
//$Category MM8BDM-Weapons/Power
//$Title Crystal Eye
//$Sprite WEA2N0
Weapon.AmmoUse 2
Weapon.AmmoGive 28
Weapon.SlotNumber 5
Inventory.Pickupmessage "$PU_CRYSTALEYE"
Obituary "$OB_CRYSTALEYE"
Tag "$TAG_CRYSTALEYE"
weapon.ammotype "CrystalEyeAmmo"
inventory.icon "CRYSSI"
States
{
SpawnLoop:
WEA2 N -1
loop
Ready:
CRYE K 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_CRYSTALEYE)
CRYE K 1 A_WeaponReady
Goto Ready+1
Deselect:
CRYE K 0
goto DeselectSwap
Select:
CRYE K 0
goto SelectSwap
Fire:
CRYE K 0 A_JumpIfNoAmmo("NoAmmo")
CRYE K 0 A_PlaySoundEx("weapons/mm5/crystaleyefire","Weapon")
CRYE K 0 A_FireCustomMissile("CrystalEyeShooter",0,1,8,0)
CRY0 ABCDEF 2
CRY0 G 35 // 55 minus frames above, and frames below
CRY0 G 0 ACS_NamedExecuteWithResult("core_clientsound", CSND_CRYSTALRELOAD) // A_PlaySoundEx("weapons/mm5/crystaleyereload", "Weapon")
CRY0 HIJK 2
CRYE K 0 A_Refire
Goto Ready+1
NoAmmo:
CRYE K 0 ACS_NamedExecuteAlways("core_noammo",0)
Goto Ready+1
}
}

actor CrystalEyeAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}

actor CrystalEyeShooter
{
    PROJECTILE
    Height 0
    Radius 0
    +NOINTERACTION
    +NOCLIP
    speed 1
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 ACS_NamedExecuteWithResult("core_SetVelPitch",0,0)
        TNT1 A 1 A_CustomMissile("CrystalEye",0,0,0,CMF_AIMDIRECTION|CMF_SAVEPITCH,pitch)
        stop
    }
}

actor CrystalEye : BasicProjectile
{
+FORCEXYBILLBOARD
Damage (50)
Height 16
Radius 16
damagetype "CrystalEye"
Obituary "$OB_CRYSTALEYE"
+EXPLODEONWATER
speed 28
States
{
Spawn:
CRYE ABCDEAA 4
loop
Death:
// [Russ] Uses A_SpawnItemEx instead of A_CustomMissile becase the latter doesn't spawn bits at certain pitches.
TNT1 A 0 A_SpawnItemEx("CrystalEyeBit", 0, 0, 0, cos(pitch), 0, sin(pitch), -15, SXF_MULTIPLYSPEED)
TNT1 A 0 A_SpawnItemEx("CrystalEyeBit", 0, 0, 0, cos(pitch), 0, sin(pitch), 0, SXF_MULTIPLYSPEED)
TNT1 A 0 A_SpawnItemEx("CrystalEyeBit", 0, 0, 0, cos(pitch), 0, sin(pitch), 15, SXF_MULTIPLYSPEED)
TNT1 A 0 A_SpawnItemEx("CrystalEyeBit", 0, 0, 0, cos(pitch-15), 0, sin(pitch-15), -15, SXF_MULTIPLYSPEED)
TNT1 A 0 A_SpawnItemEx("CrystalEyeBit", 0, 0, 0, cos(pitch-15), 0, sin(pitch-15), 0, SXF_MULTIPLYSPEED)
TNT1 A 0 A_SpawnItemEx("CrystalEyeBit", 0, 0, 0, cos(pitch-15), 0, sin(pitch-15), 15, SXF_MULTIPLYSPEED)
TNT1 A 0 A_SpawnItemEx("CrystalEyeBit", 0, 0, 0, cos(pitch+15), 0, sin(pitch+15), -15, SXF_MULTIPLYSPEED)
TNT1 A 0 A_SpawnItemEx("CrystalEyeBit", 0, 0, 0, cos(pitch+15), 0, sin(pitch+15), 0, SXF_MULTIPLYSPEED)
TNT1 A 0 A_SpawnItemEx("CrystalEyeBit", 0, 0, 0, cos(pitch+15), 0, sin(pitch+15), 15, SXF_MULTIPLYSPEED)
TNT1 A 0 A_SpawnItemEx("ExplosionEffect1",0,0,0,0,0,0,0,SXF_WEPFXCOLOR)
TNT1 A 0 A_PlaySoundEx("weapons/mm5/crystaleyesplit", "auto")
TNT1 A 35
stop
XDeath:
TNT1 A 0 A_PlaySoundEx("weapons/mm5/crystaleyehit", "auto")
TNT1 A 35
stop
}
}

actor CrystalEyeBit : BasicBouncer
{
PROJECTILE
Scale 2.5
Height 4
Radius 4
Damage(10)
+FORCEXYBILLBOARD
+HITTARGET
damagetype "CrystalEyeBit"
Obituary "$OB_CRYSTALEYE"
// bouncesound "weapons/mm5/crystaleyebounce"
bouncefactor 1.0
speed 28
reactiontime 4
States
{
Spawn:
CRYE A 0 A_CountDown
CRYE FGHIJFF 4
loop
Death:
TNT1 A 0 A_SpawnItemEx("ExplosionEffect1", 0, 0, 0, 0, 0, 0, 0, SXF_WEPFXCOLOR)
stop
}
}

actor CrystalEyeHit : CustomInventory
{
    States
    {
    Pickup:
        TNT1 A 0 A_JumpIfInventory("CrystalEyeStack6", 1, "Give7")
        TNT1 A 0 A_JumpIfInventory("CrystalEyeStack5", 1, "Give6")
        TNT1 A 0 A_JumpIfInventory("CrystalEyeStack4", 1, "Give5")
        TNT1 A 0 A_JumpIfInventory("CrystalEyeStack3", 1, "Give4")
        TNT1 A 0 A_JumpIfInventory("CrystalEyeStack2", 1, "Give3")
        TNT1 A 0 A_JumpIfInventory("CrystalEyeStack1", 1, "Give2")
        goto Give1
    Give7:
        TNT1 A 0 A_GiveInventory("CrystalEyeProtect", 1)
    Give6:
        TNT1 A 0 A_GiveInventory("CrystalEyeStack6", 1)
    Give5:
        TNT1 A 0 A_GiveInventory("CrystalEyeStack5", 1)
    Give4:
        TNT1 A 0 A_GiveInventory("CrystalEyeStack4", 1)
    Give3:
        TNT1 A 0 A_GiveInventory("CrystalEyeStack3", 1)
    Give2:
        TNT1 A 0 A_GiveInventory("CrystalEyeStack2", 1)
    Give1:
        TNT1 A 0 A_GiveInventory("CrystalEyeStack1", 1)
        stop
    }
}

actor CrystalEyeStack1 : PowerProtection
{
    powerup.duration 5
    damagefactor "Normal", 1.0
}

actor CrystalEyeStack2 : CrystalEyeStack1 {}
actor CrystalEyeStack3 : CrystalEyeStack1 {}
actor CrystalEyeStack4 : CrystalEyeStack1 {}
actor CrystalEyeStack5 : CrystalEyeStack1 {}
actor CrystalEyeStack6 : CrystalEyeStack1 {}
actor CrystalEyeStack7 : CrystalEyeStack1 {}
actor CrystalEyeStack8 : CrystalEyeStack1 {}
actor CrystalEyeStack9 : CrystalEyeStack1 {}

actor CrystalEyeProtect : CrystalEyeStack1
{
    damagefactor "CrystalEyeBit", 0.0
}
