actor CrocChargeKickWep_NormalBar : NormalBar { args 4, 225 }

actor CrocChargeKickWep : BaseMM8BDMWep
{
//$Category MM8BDM-Weapons/Close
//$Title Charge Kick
//$Sprite WEA2E0
Weapon.AmmoUse 2
Weapon.AmmoGive 28
Weapon.SlotNumber 4
Inventory.Pickupmessage "$PU_CHARGEKICK"
Obituary "$OB_CHARGEKICK"
Tag "$TAG_CHARGEKICK"
weapon.ammotype "CrocChargeKickAmmo"
inventory.icon "CHARSI"
States
{
SpawnLoop:
WEA2 E -1
loop
Ready:
CKIC C 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_CHARGEKICK)
CKIC C 1 A_WeaponReady
Goto Ready+1
Deselect:
CKIC C 0
goto DeselectSwap
Select:
CKIC C 0
goto SelectSwap
Fire:
CKIC C 0 A_JumpIfNoAmmo("NoAmmo")
CKIC C 0 A_PlaySoundEx("weapons/mm5/chargekickfire","Weapon")
CKIC C 0 //ACS_NamedExecuteWithResult("core_stopplayer", 0, true)
CKIC C 0 A_SpawnItemEx("PropBlowerEffect")
CKIC C 0 A_FireCustomMissile("CrocChargeKick",0,1,0,0)
CKIC C 2 A_GiveInventory("CrocChargeKickThrust")
CKIC B 2 
CKIC A 8 //ACS_NamedExecuteWithResult("core_stopplayer", 0, false)
CKIC B 5
CKIC C 10
CKIC C 0 A_Refire
Goto Ready+1
NoAmmo:
CKIC C 1 ACS_NamedExecuteAlways("core_noammo",0)
Goto Ready+1
}
}

actor CrocChargeKickThrust : Custominventory
{
    States
    {
    Pickup:
        TNT1 A 0 A_JumpIf(CallACS("core_CheckFooting"),"KickGround")
		TNT1 A 0 A_Recoil(-15)
        stop
    KickGround:
		TNT1 A 0 A_Recoil(-30)
        TNT1 A 0 A_GiveInventory("CrocChargeKickPowerup", 1)
        stop
    }
}

actor CrocChargeKickPowerup : PowerSpeed
{
+POWERSPEED.NOTRAIL
powerup.duration 8
Speed 1.25
}

actor CrocChargeKickAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}

actor CrocChargeKick : BasicProjectile
{
var int user_damagemod;
Radius 14
Height 25
damage (30-user_damagemod)
damagetype "ChargeKick"
Obituary "$OB_CHARGEKICK"
speed 60
States
{
Spawn:
CKIC D 0
CKIC D 2 //A_RadiusThrust(500,64,0)
CKIC EF 2 A_SetUserVar(user_damagemod, user_damagemod+10)
stop
Death:
CKIC C 0 A_SpawnItemEx("PropBlowerEffect")
stop
}
}