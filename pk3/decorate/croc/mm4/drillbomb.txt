actor CrocDrillBombWep_NormalBar : NormalBar { args 87, 227 }

actor CrocDrillBombWep : BaseMM8BDMWep 10050
{
//$Category MM8BDM-Weapons/Power
//$Title Drill Bomb
//$Sprite WEAPV0
Weapon.AmmoUse 2
Weapon.AmmoGive 28
Weapon.SlotNumber 5
Inventory.Pickupmessage "$PU_DRILLBOMB"
Obituary "$OB_DRILLBOMB"
Tag "$TAG_DRILLBOMB"
weapon.ammotype "CrocDrillBombAmmo"
inventory.icon "DRILLSI"
States
{
SpawnLoop:
WEAP V -1
loop
Ready:
DRIL D 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_DRILLBOMB)
DRIL D 1 A_WeaponReady
Goto Ready+1
Deselect:
DRIL D 0 A_GiveInventory("CrocDrillFlag",2)
goto DeselectSwap
Select:
DRIL D 0 A_TakeInventory("CrocDrillFlag",99)
goto SelectSwap
Fire:
DRIL D 0 A_JumpIfNoAmmo("NoAmmo")
DRIL D 0 A_PlaySoundEx("weapons/mm4/drillbombfire","Weapon")
DRIL D 0 A_FireCustomMissile("CrocDrillBomb",0,1,8,0)
DRI0 ABCDEF 2
DRIL D 6
DRIL D 0 A_JumpIfInventory("IsBot", 1, "BotCheck")
Goto Hold // 18 ticks
AltFire:
DRIL D 0 A_JumpIfNoAmmo("NoAmmo")
DRIL D 0 A_PlaySoundEx("weapons/mm4/drillbombfire","Weapon")
DRIL D 0 A_FireCustomMissile("CrocDrillBomb",0,1,8,0)
DRI0 ABCDEF 2
DRIL D 6
goto Hold // 18 ticks
Hold:
DRIL D 1 A_RailWait
DRIL D 0 A_Refire
AltHold:
DRIL D 0 A_ClearRefire
DRIL D 10 A_GiveInventory("CrocDrillFlag",2)
DRIL D 0 A_TakeInventory("CrocDrillFlag",999)
Goto Ready+1
BotCheck:
DRIL D 5 A_RailWait
DRIL D 0 A_Jump(128, "BotCheck")
Goto Hold+2
NoAmmo:
DRIL D 1 ACS_NamedExecuteAlways("core_noammo",0)
Goto Hold
}
}

actor CrocDrillFlag : Inventory
{
inventory.amount 1
inventory.maxamount 2
}

actor CrocDrillBombAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}

actor CrocDrillBomb : BasicProjectile
{
PROJECTILE
+EXPLODEONWATER
+DONTREFLECT
Speed 25
Damage (0)
damagetype "CrocDrillBomb"
Obituary "$OB_DRILLBOMB"
Height 6
Radius 6
reactiontime 1
States
{
Spawn:
DRIL A 0
DRIL A 0 A_PlaySoundEx("weapons/mm4/drillbombfly","Weapon")
DRIL A 0 A_SpawnItemEx("CrocDrillBombSmoke", -momx, -momy, -momz, 0, 0, 0, 0, SXF_ABSOLUTEPOSITION|SXF_WEPFXCOLOR)
DRIL AAA 1 A_GiveInventory("CrocDrillDetCheck",1)
DRIL A 0 A_SpawnItemEx("CrocDrillBombSmoke", -momx, -momy, -momz, 0, 0, 0, 0, SXF_ABSOLUTEPOSITION|SXF_WEPFXCOLOR)
DRIL BBB 1 A_GiveInventory("CrocDrillDetCheck",1)
DRIL A 0 A_SpawnItemEx("CrocDrillBombSmoke", -momx, -momy, -momz, 0, 0, 0, 0, SXF_ABSOLUTEPOSITION|SXF_WEPFXCOLOR)
DRIL CCC 1 A_GiveInventory("CrocDrillDetCheck",1)
loop
Death:
DRIL A 0 A_Stop
DRIL A 0 A_PlaySoundEx("weapons/mm4/drillbombexplode", "Weapon")
DRIL A 0 A_Explode(50, 96, 0, 0, 16+7)
TNT1 A 35 A_SpawnItemEx("ExplosionEffect2",0,0,0,0,0,0,0,SXF_WEPFXCOLOR)
stop
}
}

actor CrocDrillDetCheck : CustomInventory
{
States
{
Pickup:
TNT1 A 0 A_JumpIfInTargetInventory("CrocDrillFlag",2,"Pickup2")
stop
Pickup2:
TNT1 A 0 A_CountDown
stop
}
}

actor CrocDrillBombSmoke : BasicGraphicEffect
{
    Scale 2.0
    Speed 100
    +FORCEXYBILLBOARD
    States
    {
    Spawn:
        4DBS ABCDE 2
        stop
    }
}

actor CrocDrillExplode : ExplosionEffect2 {}
