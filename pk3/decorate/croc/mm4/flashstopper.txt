actor FlashStopperWep_NormalBar : NormalBar { args 4, 239 }
actor FlashStopperWep_ScriptBar : ScriptBar {}

actor FlashStopperWep : BaseMM8BDMWep 10078
{
//$Category MM8BDM-Weapons/AoE
//$Title Flash Stopper
//$Sprite WEA3E0
Weapon.AmmoUse 7
Weapon.AmmoGive 28
Weapon.SlotNumber 6
Inventory.Pickupmessage "$PU_FLASHSTOPPER"
Obituary "$OB_FLASHSTOPPER"
Tag "$TAG_FLASHSTOPPER"
weapon.ammotype "FlashStopperAmmo"
inventory.icon "BRIGHSI"
States
{
SpawnLoop:
WEA3 E -1
loop
Ready:
FSTO A 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_FLASHSTOPPER)
FSTO A 1 A_WeaponReady
Goto Ready+1
Deselect:
FSTO A 0
goto DeselectSwap
Select:
FSTO A 0
goto SelectSwap
Fire:
FSTO A 0 A_JumpIfInventory("IsBot", 1, "BotFire")
FSTO A 0 A_JumpIfInventory("FlashStopperWait", 1, "FireWait")
FSTO A 0 A_JumpIfNoAmmo("NoAmmo")
FSTO EF 1 
FSTO A 0 A_PlaySoundEX("weapons/mm4/flashstopperfire","Weapon")
FSTO A 0 A_FireCustomMissile("FlashStopperStarter",0,1,8,0)
FSTO A 0 A_SpawnItemEx("FlashStopper")
FSTO A 0 A_GiveInventory("FlashStopperWait", 1)
FSTO A 0 A_SpawnItemEx("FlashStopperCooldowner")
FSTO GHIJ 1
TNT1 A 8
FSTO KL 1
FSTO A 0 A_Refire
Goto Ready+1
FireWait:
FSTO A 1
goto Ready+1
NoAmmo:
FSTO A 1 ACS_NamedExecuteAlways("core_noammo",0)
Goto Ready+1

BotFire:
FSTO A 0 A_JumpIfInventory("BotFlashStopperCooldown", 1, "BotFireBuster")
FSTO A 0 A_GiveInventory("BotFlashStopperCooldown", 1)
goto Fire+1
BotFireBuster:
BUST B 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_MEGABUSTER)
BUST B 0 A_PlaySoundEx("weapons/busters/megabusterfire","SoundSlot6")
BUST B 0 A_FireCustomMissile("MegaShot",0,0,8,0)
BUST CD 3
BUST B 2
BUST B 0 A_JumpIfInventory("BotFlashStopperCooldown", 1, 2)
BUST B 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_FLASHSTOPPER)
BUST B 0 A_Refire
goto Ready
}
}

actor FlashStopperWait : Powerup { powerup.duration 43 }

actor BotFlashStopperCooldown : Powerup
{
powerup.duration -5
}

actor FlashStopperCooldowner : BasicCooldowner
{
reactiontime 43
States
{
Spawn:
TNT1 A 0
Spawn2:
TNT1 A 1 A_GiveInventory("FlashStopperCooldownerFunc",1)
loop
Death:
TNT1 A 0 A_TakeFromTarget("FlashStopperWait",1)
stop
}
}

actor FlashStopperCooldownerFunc : BasicCooldownerFunc
{
States
{
Pickup:
TNT1 A 0 A_JumpIfInTargetInventory("FlashStopperWait",1,"PickupBasic")
goto PickupRemove
}
}

actor FlashStopperGiver : CustomInventory //10078
{
//$Category MM8BDM-Weapons
//$Title Flash Stopper
Inventory.Pickupmessage "$PU_FLASHSTOPPER"
inventory.pickupsound "weapon/weaponup"
Inventory.RespawnTics 1800
scale 2.0
states
{
Spawn:
WEA3 E 1
loop
Pickup:
WEA3 E 0 A_JumpIfInventory("FlashStopperWep",1,"Failure")
WEA3 E 0 A_GiveInventory("FlashStopperWep")
stop
Failure:
WEA3 E 0 
fail
}
}

actor FlashStopper : BasicExplosion
{
damagetype "FlashStopper"
Obituary "$OB_FLASHSTOPPER"
States
{
Spawn:
TNT1 A 0
TNT1 A 25
TNT1 A 0 A_SpawnItemEx("FlashStopperFX",0,0,64, 0, 0, 0, 0, SXF_WEPFXCOLOR)
TNT1 A 0 A_PlaySoundEx("weapons/mm4/flashstopperexplode","Weapon")
//TNT1 A 0 A_GiveToTarget("FlashStopperFlash", 1)
TNT1 A 1 A_Explode(22, 450,0)
TNT1 A 0 ACS_NamedExecuteAlways("core_flashstopperlight", 0, 450)
TNT1 A 1
stop
}
}



actor FlashStopperStarter : FlashStopper
{
+BRIGHT
speed 2
States
{
Spawn:
FST2 G 0
FST2 G 0 ThrustThingZ(0,10,0,0)
FST2 GHGHG 5
FST2 G 0 A_Stop
Death:
FST2 G 0
stop
}
}

actor FlashStopperFlash : CustomInventory
{
	+INVENTORY.AUTOACTIVATE
	States
	{
	Use:
		TNT1 A 0 A_SetBlend("99 99 99", 0.95, 15)
		stop
	}
}

actor FlashStopperAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}

actor Flashed : PowerDamage
{
powerup.duration 105
damagefactor "normal", 1.0
}

actor BrightBlind : Inventory
{
inventory.amount 1
inventory.maxamount 175
}

actor FlashStopperFX : BasicGraphicEffect
{
renderstyle translucent
+FORCEXYBILLBOARD
+BRIGHT
alpha 1.0
scale 5.0
states
{
Spawn:
FST2 ABCDEF 1 A_FadeOut(0.12)
stop
}
}

actor FlashStopperHit : CustomInventory
{
    States
    {
    Pickup:
        TNT1 A 0 A_GiveInventory("FlashStopperHitCheck", 1)
        goto End
    End:
        TNT1 A 0
        stop
    }
}

actor FlashStopperHitCheck : powerup
{
    Powerup.duration 10
}

actor FlashStopperBlind : FlashStopperHit
{
    States
    {
    Pickup:
        TNT1 A 0 A_JumpIfInventory("BrightBlind", 1,3)
		TNT1 A 0 A_SpawnItemEx("FlashBlindWatcher")
        TNT1 A 0 A_GiveInventory("BrightBlind",175)
        TNT1 A 0 ACS_NamedExecuteAlways("core_flashstopper", 0)
        goto End
        TNT1 A 0 A_GiveInventory("BrightBlind",55)
        goto End
    }
}

actor FlashBlindWatcher
{
Height 0
Radius 0
+NOINTERACTION
-SOLID
+NOGRAVITY
+MISSILE
States
{
Spawn:
TNT1 A 0 
TNT1 A 4 A_GiveToTarget("FlashBlindFX", 1)
TNT1 A 0 A_JumpIfInTargetInventory("BrightBlind", 100, "Spawn")
stop
}
}

actor FlashBlindFX : CustomInventory
{
+INVENTORY.AUTOACTIVATE
-INVBAR
States
{
Use:
TNT1 A 0
TNT1 A 0 A_SpawnItemEx("FlashBlindGFX", random(16,20), random(16, -16), random (40,56), 0, 0, 0, 0)
stop
}
}

actor FlashBlindGFX : BasicGraphicEffect
{
+FORCEXYBILLBOARD
States
{
Spawn:
FST2 IJK 4
stop
}
}