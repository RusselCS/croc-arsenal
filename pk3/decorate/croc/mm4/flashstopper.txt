actor CrocFlashStopperWep_NormalBar : NormalBar { args 4, 239 }
actor CrocFlashStopperWep_ScriptBar : ScriptBar {}

actor CrocFlashStopperWep : BaseMM8BDMWep
{
//$Category MM8BDM-Weapons/AoE
//$Title Flash Stopper
//$Sprite WEA3E0
Weapon.AmmoUse 7
Weapon.AmmoGive 28
Weapon.SlotNumber 6
Inventory.Pickupmessage "$PU_FLASHSTOPPER"
Obituary "$OB_FLASHSTOPPER"
Tag "$TAG_FLASHSTOPPER"
weapon.ammotype "CrocFlashStopperAmmo"
inventory.icon "BRIGHSI"
States
{
SpawnLoop:
WEA3 E -1
loop
Ready:
FSTO A 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_FLASHSTOPPER)
FSTO A 1 A_WeaponReady
Goto Ready+1
Deselect:
FSTO A 0
goto DeselectSwap
Select:
FSTO A 0
goto SelectSwap
Fire:
FSTO A 0 A_JumpIfInventory("IsBot", 1, "BotFire")
FSTO A 0 A_JumpIfInventory("CrocFlashStopperWait", 1, "FireWait")
FSTO A 0 A_JumpIfNoAmmo("NoAmmo")
FSTO EF 1 
FSTO A 0 A_PlaySoundEX("weapons/mm4/flashstopperfire","Weapon")
FSTO A 0 A_FireCustomMissile("CrocFlashStopperStarter",0,1,8,0)
FSTO A 0 A_SpawnItemEx("CrocFlashStopper")
FSTO A 0 A_GiveInventory("CrocFlashStopperWait", 1)
FSTO A 0 A_SpawnItemEx("CrocFlashStopperCooldowner")
FSTO GHIJ 1
TNT1 A 8
FSTO KL 1
FSTO A 0 A_Refire
Goto Ready+1
FireWait:
FSTO A 1
goto Ready+1
NoAmmo:
FSTO A 1 ACS_NamedExecuteAlways("core_noammo",0)
Goto Ready+1

BotFire:
FSTO A 0 A_JumpIfInventory("CrocBotFlashStopperCooldown", 1, "BotFireBuster")
FSTO A 0 A_GiveInventory("CrocBotFlashStopperCooldown", 1)
goto Fire+1
BotFireBuster:
BUST B 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_MEGABUSTER)
BUST B 0 A_PlaySoundEx("weapons/busters/megabusterfire","SoundSlot6")
BUST B 0 A_FireCustomMissile("MegaShot",0,0,8,0)
BUST CD 3
BUST B 2
BUST B 0 A_JumpIfInventory("CrocBotFlashStopperCooldown", 1, 2)
BUST B 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_FLASHSTOPPER)
BUST B 0 A_Refire
goto Ready
}
}

actor CrocFlashStopperWait : Powerup { powerup.duration 43 }

actor CrocBotFlashStopperCooldown : Powerup
{
powerup.duration -5
}

actor CrocFlashStopperCooldowner : BasicCooldowner
{
reactiontime 43
States
{
Spawn:
TNT1 A 0
Spawn2:
TNT1 A 1 A_GiveInventory("CrocFlashStopperCooldownerFunc",1)
loop
Death:
TNT1 A 0 A_TakeFromTarget("CrocFlashStopperWait",1)
stop
}
}

actor CrocFlashStopperCooldownerFunc : BasicCooldownerFunc
{
States
{
Pickup:
TNT1 A 0 A_JumpIfInTargetInventory("CrocFlashStopperWait",1,"PickupBasic")
goto PickupRemove
}
}

actor CrocFlashStopperGiver : CustomInventory //10078
{
//$Category MM8BDM-Weapons
//$Title Flash Stopper
Inventory.Pickupmessage "$PU_FLASHSTOPPER"
inventory.pickupsound "weapon/weaponup"
Inventory.RespawnTics 1800
scale 2.0
states
{
Spawn:
WEA3 E 1
loop
Pickup:
WEA3 E 0 A_JumpIfInventory("CrocFlashStopperWep",1,"Failure")
WEA3 E 0 A_GiveInventory("CrocFlashStopperWep")
stop
Failure:
WEA3 E 0 
fail
}
}

actor CrocFlashStopper : BasicExplosion
{
damagetype "FlashStopper"
Obituary "$OB_FLASHSTOPPER"
States
{
Spawn:
TNT1 A 0
TNT1 A 25
TNT1 A 0 A_SpawnItemEx("CrocFlashStopperFX",0,0,64, 0, 0, 0, 0, SXF_WEPFXCOLOR)
TNT1 A 0 A_PlaySoundEx("weapons/mm4/flashstopperexplode","Weapon")
//TNT1 A 0 A_GiveToTarget("CrocFlashStopperFlash", 1)
TNT1 A 1 A_Explode(22, 450,0)
TNT1 A 0 ACS_NamedExecuteAlways("core_flashstopperlight", 0, 450)
TNT1 A 1
stop
}
}



actor CrocFlashStopperStarter : FlashStopper
{
+BRIGHT
speed 2
States
{
Spawn:
FST2 G 0
FST2 G 0 ThrustThingZ(0,10,0,0)
FST2 GHGHG 5
FST2 G 0 A_Stop
Death:
FST2 G 0
stop
}
}

actor CrocFlashStopperFlash : CustomInventory
{
	+INVENTORY.AUTOACTIVATE
	States
	{
	Use:
		TNT1 A 0 A_SetBlend("99 99 99", 0.95, 15)
		stop
	}
}

actor CrocFlashStopperAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}

actor CrocFlashed : PowerDamage
{
powerup.duration 105
damagefactor "normal", 1.0
}

actor CrocBrightBlind : Inventory
{
inventory.amount 1
inventory.maxamount 175
}

actor CrocFlashStopperFX : BasicGraphicEffect
{
renderstyle translucent
+FORCEXYBILLBOARD
+BRIGHT
alpha 1.0
scale 5.0
states
{
Spawn:
FST2 ABCDEF 1 A_FadeOut(0.12)
stop
}
}

actor CrocFlashStopperHit : CustomInventory
{
    States
    {
    Pickup:
        TNT1 A 0 A_GiveInventory("CrocFlashStopperHitCheck", 1)
        goto End
    End:
        TNT1 A 0
        stop
    }
}

actor CrocFlashStopperHitCheck : powerup
{
    Powerup.duration 10
}

actor CrocFlashStopperBlind : CrocFlashStopperHit
{
    States
    {
    Pickup:
        TNT1 A 0 A_JumpIfInventory("CrocBrightBlind", 1,3)
		TNT1 A 0 A_SpawnItemEx("CrocFlashBlindWatcher")
        TNT1 A 0 A_GiveInventory("CrocBrightBlind",175)
        TNT1 A 0 ACS_NamedExecuteAlways("core_flashstopper", 0)
        goto End
        TNT1 A 0 A_GiveInventory("CrocBrightBlind",55)
        goto End
    }
}

actor CrocFlashBlindWatcher
{
Height 0
Radius 0
+NOINTERACTION
-SOLID
+NOGRAVITY
+MISSILE
States
{
Spawn:
TNT1 A 0 
TNT1 A 4 A_GiveToTarget("CrocFlashBlindFX", 1)
TNT1 A 0 A_JumpIfInTargetInventory("CrocBrightBlind", 100, "Spawn")
stop
}
}

actor CrocFlashBlindFX : CustomInventory
{
+INVENTORY.AUTOACTIVATE
-INVBAR
States
{
Use:
TNT1 A 0
TNT1 A 0 A_SpawnItemEx("CrocFlashBlindGFX", random(16,20), random(16, -16), random (40,56), 0, 0, 0, 0)
stop
}
}

actor CrocFlashBlindGFX : BasicGraphicEffect
{
+FORCEXYBILLBOARD
States
{
Spawn:
FST2 IJK 4
stop
}
}
