actor CrocDiveMissileWep_NormalBar : NormalBar { args 4, 198 }

actor CrocDiveMissileWep : BaseMM8BDMWep 10068
{
//$Category MM8BDM-Weapons/Ranged
//$Title Dive Missile
//$Sprite WEA2S0
Weapon.AmmoUse 1
Weapon.AmmoGive 28
Weapon.SlotNumber 2
Inventory.Pickupmessage "$PU_DIVEMISSILE"
Obituary "$OB_DIVEMISSILE"
Tag "$TAG_DIVEMISSILE"
weapon.ammotype "CrocDiveMissileAmmo"
inventory.icon "DIVESI"
States
{
SpawnLoop:
WEA2 S -1
loop
Ready:
AIRS D 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_DIVEMISSILE)
AIRS D 1 A_WeaponReady
Goto Ready+1
Deselect:
AIRS D 0
goto DeselectSwap
Select:
AIRS D 0
goto SelectSwap
Fire:
AIRS D 0 A_JumpIfNoAmmo("NoAmmo")
AIRS D 0 A_PlaySoundEx("weapons/mm4/divemissilefire","Weapon")
AIRS D 0 A_FireCustomMissile("CrocDiveMissile",0,1,8,0)
AIRS D 0 A_GiveInventory("CrocDiveCounter",23)
AIRS EFD 3
Waiting:
AIRS D 1 A_TakeInventory("CrocDiveCounter", 1 + CallACS("core_checkrune", RUNE_RAGE))
AIRS D 0 A_JumpIfInventory("CrocDiveCounter",1,"Waiting")
AIRS D 4
AIRS D 0 A_Refire
Goto Ready+1
NoAmmo:
AIRS D 1 ACS_NamedExecuteAlways("core_noammo",0)
Goto Ready+1
}
}

actor CrocDiveMissileAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}

actor CrocDiveMissile : BasicProjectile
{
+SEEKERMISSILE
+SCREENSEEKER
Radius 5
Height 5
damage (12)
damagetype "DiveMissile"
Obituary "$OB_DIVEMISSILE"
speed 35
ReactionTime 85
States
{
Spawn:
DIVE A 0 
DIVE ABAB 1 A_SeekerMissile (2, 10, SMF_LOOK, 256, 10)
NoSeek:
DIVE AB 1 A_SeekerMissile (2, 10, 0, 0, 10)
DIVE A 0 A_CountDown
loop
Crash:
TNT1 A 0 A_TakeFromTarget("CrocDiveCounter",9999)
Death:
TNT1 A 2 A_SpawnItemEx("ExplosionEffect1", 0, 0, 0, 0, 0, 0, 0, SXF_WEPFXCOLOR)
stop
}
}

actor CrocDiveCounter : Inventory
{
inventory.amount 1
inventory.maxamount 23
}
