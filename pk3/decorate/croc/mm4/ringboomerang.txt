actor CrocRingBoomerangWep_NormalBar : NormalBar { args 215, 230 }

actor CrocRingBoomerangWep : BaseMM8BDMWep 10058
{
//$Category MM8BDM-Weapons/Close
//$Title Ring Boomerang
//$Sprite WEA2H0
Weapon.AmmoUse 2
Weapon.AmmoGive 28
Weapon.SlotNumber 4
Inventory.Pickupmessage "$PU_RINGBOOMERANG"
Obituary "$OB_RINGBOOMERANG"
Tag "$TAG_RINGBOOMERANG"
weapon.ammotype "CrocRingBoomerangAmmo"
inventory.icon "RINGSI"
States
{
SpawnLoop:
WEA2 H -1
loop
Ready:
RING E 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_RINGBOOMERANG)
RING E 1 A_WeaponReady
Goto Ready+1
Deselect:
RING E 0
goto DeselectSwap
Select:
RING E 0
goto SelectSwap
Fire:
RING E 0 A_JumpIfNoAmmo("NoAmmo")
RING E 0 A_PlaySoundEx("weapons/mm4/ringboomberangfire","Weapon")
RING E 0 A_FireCustomMissile("CrocRingBoomerang",0,1,8,0)
RING FG 4
RING E 15
RING E 0 A_Refire
Goto Ready+1
NoAmmo:
RING E 1 ACS_NamedExecuteAlways("core_noammo",0)
Goto Ready+1
}
}

actor CrocRingBoomerangAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}

actor CrocRingBoomerangBase : BasicProjectile
{
Radius 12
Height 12
damage (18)
+RIPPER
damagetype "RingBoomerang"
Obituary "$OB_RINGBOOMERANG"
speed 65
args 65 //return speed
}

actor RingBoomerang : CrocRingBoomerangBase
{
+HEXENBOUNCE
+CANBOUNCEWATER
+HITTRACER
States
{
Spawn:
RING A 0
RING AABBCCDD 1 A_GiveInventory("BouncerReflectedCheck",1)
RING A 0 A_GiveInventory("BouncerReflectedCheck",1)
TNT1 A 0 A_ChangeFlag("RIPPER",0)
TNT1 A 0 A_ChangeFlag("HEXENBOUNCE",0)
TNT1 A 0 A_ChangeFlag("HITTRACER",0)
TNT1 A 0 A_Stop
RING ABCD 2
Death:
TNT1 A 0 A_SpawnItemEx("CrocReturningRingMaster")
stop
}
}

actor CrocReturningRingMaster : CrocRingBoomerangBase
{
+NOCLIP
+NOINTERACTION
states
{
Spawn:
TNT1 A 0
TNT1 A 0 ACS_NamedExecuteWithResult("core_lookatptr", AAPTR_TARGET)
TNT1 A 0 A_SpawnItemEx("CrocReturningRing",0,0,0,0,0,0,0,SXF_TRANSFERPITCH,0,CallACS("core_NewUniqueTID"))
TNT1 A 0 ACS_NamedExecuteWithResult("core_setTIDsPointerToThis",CallACS("core_CurrentUniqueTID"),AAPTR_MASTER)
TNT1 A 0 ACS_NamedExecuteWithResult("core_ClearUniqueTID",0)
RING AABBCCDDAA 1 A_GiveInventory("CrocReturningRingMasterFunc",1)
Death:
TNT1 A 0 A_RemoveChildren(true)
stop
}
}

actor CrocReturningRingMasterFunc : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_GiveInventory("CountChildrenFunc",1)
TNT1 A 0 A_JumpIfInventory("ChildCount",1,"Pickup2")
TNT1 A 0 Thing_Remove(0)
Pickup2:
TNT1 A 0
stop
}
}

actor CrocReturningRing : CrocRingBoomerangBase
{
+INVISIBLE
var int user_NoTranslation;
states
{
Spawn:
TNT1 A 0
Spawn2:
BUST A 1 A_GiveInventory("CrocReturningRingFunc",1)
loop
Death:
TNT1 A 0 A_SpawnItemEx("CrocReturningRing2",0,0,0,0,0,0,0,SXF_TRANSFERPITCH|SXF_TRANSFERPOINTERS)
stop
}
}

actor CrocReturningRing2 : ReturningRing
{
+NOCLIP
+NOINTERACTION
states
{
Spawn:
TNT1 A 0
Spawn2:
BUST A 1 A_GiveInventory("CrocReturningRingFunc",1)
TNT1 A 0 A_SpawnItemEx("CrocReturningRing",0,0,0,0,0,0,0,SXF_TRANSFERPITCH|SXF_TRANSFERPOINTERS)
stop
Death:
TNT1 A 0
stop
}
}

actor CrocReturningRingFunc : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIfCloser(32,"RemoveThis")
//TNT1 A 0 A_GiveInventory("HeadTowardsTarget_Arg0",1)
TNT1 A 0 A_ChangeVelocity(cos(-pitch)*args[0],0,sin(-pitch)*args[0],CVF_RELATIVE|CVF_REPLACE)
TNT1 A 0 A_GiveInventory("WarpMasterHere",1)
stop
RemoveThis:
TNT1 A 0 A_RemoveMaster
TNT1 A 0 Thing_Remove(0)
stop
}
}
