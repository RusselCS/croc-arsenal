actor CrocBalladeCrackerWep_NormalBar : NormalBar { args 64, 62 }

actor CrocBalladeCrackerWep : BaseMM8BDMWep 10076
{
//$Category MM8BDM-Weapons/Power
//$Title Ballade Cracker
//$Sprite WEA3C0
Weapon.AmmoUse 3
Weapon.AmmoGive 30
Weapon.SlotNumber 5
Inventory.Pickupmessage "$PU_BALLADECRACKER"
Obituary "$OB_BALLADECRACKER"
Tag "$TAG_BALLADECRACKER"
weapon.ammotype "CrocBalladeCrackerAmmo"
inventory.icon "BALLSI"
States
{
SpawnLoop:
WEA3 C -1
loop
Ready:
BHND A 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_BALLADECRACKER)
BHND A 0 A_JumpIfNoAmmo("ReadyNoAmmo")
BHND A 1 A_WeaponReady
Goto Ready+1
ReadyNoAmmo:
BHND J 1 A_WeaponReady
BHND J 0 A_JumpIfNoAmmo("ReadyNoAmmo")
Goto Ready+1
Deselect:
BHND A 0
goto DeselectSwap
Select:
BHND A 0
goto SelectSwap
Fire:
BHND A 0 A_JumpIfNoAmmo("NoAmmo")
BHND A 0 A_PlaySoundEx("weapons/mmk/balladecrackerfire","Weapon")
BHND A 0 A_FireCustomMissile("CrocBalladeCracker",0,1,8,0)
BHND BCDEFG 2
BHND H 0 A_JumpIfNoAmmo("NoAmmoDelay")
BHND HI 2
BHND A 13
BHND A 0 A_Refire
Goto Ready+1
NoAmmoDelay:
BHND KL 2
BHND J 13
Goto ReadyNoAmmo
NoAmmo:
BHND J 1 ACS_NamedExecuteAlways("core_noammo",0)
Goto ReadyNoAmmo
}
}

actor CrocBalladeCrackerAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 30
+INVENTORY.IGNORESKILL
}

actor CrocBalladeCracker : BasicProjectile
{
Radius 6
Height 7
damagetype "CrocBalladeCracker"
Obituary "$OB_BALLADECRACKER"
damage (1)
speed 50
+EXPLODEONWATER
States
{
Spawn:
BALA ABCD 3
loop
Death:
BALA A 0 A_Stop
BALA A 0 A_PlaySoundEx("weapons/mmk/balladecrackerexplode", "Weapon")
BALA A 0 A_Explode(40, 96, 0)
TNT1 A 35 A_SpawnItemEx("ExplosionEffect2", 0, 0, 0, 0, 0, 0, 0, SXF_WEPFXCOLOR)
stop
}
}
