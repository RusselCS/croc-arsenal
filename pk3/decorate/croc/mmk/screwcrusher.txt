actor CrocScrewCrusherWep_NormalBar : NormalBar { args 54, 42 }

actor CrocScrewCrusherWep : BaseMM8BDMWep 10077
{
//$Category MM8BDM-Weapons/Rapid
//$Title Screw Crusher
//$Sprite WEA3D0
Weapon.AmmoUse 1
Weapon.AmmoGive 56
Weapon.SlotNumber 3
Inventory.Pickupmessage "$PU_SCREWCRUSHER"
Obituary "$OB_SCREWCRUSHER"
Tag "$TAG_SCREWCRUSHER"
weapon.ammotype "CrocScrewCrusherAmmo"
inventory.icon "SCRWSI"
States
{
SpawnLoop:
WEA3 D -1
loop
Ready:
SCRH A 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_SCREWCRUSHER)
SCRH A 0 A_JumpIfNoAmmo("ReadyNoAmmo")
SCRH A 1 A_WeaponReady
Goto Ready+2
ReadyNoAmmo:
SCRH J 1 A_WeaponReady
SCRH J 0 A_JumpIfNoAmmo("ReadyNoAmmo")
Goto Ready+1
Deselect:
SCRH A 0
goto DeselectSwap
Select:
SCRH A 0
goto SelectSwap
Fire:
SCRH A 0 A_JumpIfNoAmmo("NoAmmo")
SCRH A 0 A_PlaySoundEx("weapons/mmk/screwcrusherfire","Weapon")
SCRH A 0 A_FireCustomMissile("CrocScrewCrusherSpawner",0,1,8,0)
SCRH BCDE 2
SCRH F 0 A_JumpIfNoAmmo("NoAmmoRaise")
SCRH F 0 A_Refire
SCRH FFGG 1 A_WeaponReady
SCRH IH 1 A_WeaponReady
SCRH A 4 A_WeaponReady
Goto Ready+1
NoAmmoRaise:
SCRH FFGG 1 A_WeaponReady(WRF_NOFIRE)
SCRH KL 1 A_WeaponReady(WRF_NOFIRE)
SCRH J 4 A_WeaponReady(WRF_NOFIRE)
Goto ReadyNoAmmo
NoAmmo:
SCRH J 1 ACS_NamedExecuteAlways("core_noammo",0)
Goto ReadyNoAmmo
}
}

actor CrocScrewCrusherAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 56
+INVENTORY.IGNORESKILL
}

actor CrocScrewCrusherSpawner
{
PROJECTILE
+NOINTERACTION
+NOCLIP
Damage(0)
Height 1
Radius 1
Speed 1
States
{
Spawn:
TNT1 A 0
TNT1 A 1 A_SpawnItemEx("CrocScrewCrusher", 0, 0, 0, momx*0.8, momy*0.8, 0.5+momz*1.0, 0, SXF_MULTIPLYSPEED|SXF_ABSOLUTEMOMENTUM)
stop
}
}

actor CrocScrewCrusher : BasicProjectile
{
damagetype "CrocScrewCrusher"
Obituary "$OB_SCREWCRUSHER"
-NOGRAVITY
+BOUNCEONWALLS
+BOUNCEONFLOORS
+BOUNCEONCEILINGS
+CANBOUNCEWATER
+BOUNCEAUTOOFFFLOORONLY
+USEBOUNCESTATE
bouncefactor 0.8
Speed 20
Radius 15
Height 8
gravity 1.8
Damage (20)
reactiontime 30
args 2
States
{
Spawn:
TNT1 A 2
SCRE AB 3 A_CountDown
goto Spawn+1
Bounce.floor:
SCRE A 0 A_CountDownArg(0)
goto Spawn+1
Death:
SCRE A 0
stop
}
}
