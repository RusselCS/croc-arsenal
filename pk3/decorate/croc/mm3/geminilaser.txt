actor CrocGeminiLaserWep_NormalBar : NormalBar { args 4, 197 }

actor CrocGeminiLaserWep : BaseMM8BDMWep 10041
{
//$Category MM8BDM-Weapons/Ranged
//$Title Gemini Laser
//$Sprite WEAPM0
Weapon.AmmoUse 2
Weapon.AmmoGive 28
Weapon.SlotNumber 2
Inventory.Pickupmessage "$PU_GEMINILASER"
Obituary "$OB_GEMINILASER"
Tag "$TAG_GEMINILASER"
weapon.ammotype "CrocGeminiLaserAmmo"
inventory.icon "GEMISI"
States
{
SpawnLoop:
WEAP M -1
loop
Ready:
BLIZ D 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_GEMINILASER)
BLIZ D 1 A_WeaponReady
Goto Ready+1
Deselect:
BLIZ D 0
goto DeselectSwap
Select:
BLIZ D 0
goto SelectSwap
Fire:
BLIZ D 0 A_JumpIfNoAmmo("NoAmmo")
BLIZ D 0 A_PlaySoundEx("weapons/mm3/geminilaserfire","Weapon")
BLIZ D 0 A_FireCustomMissile("CrocGeminiLaserShooter",0,1,8,0)
BLIZ EF 4
BLIZ D 14
BLIZ D 0 A_Refire
Goto Ready+1
NoAmmo:
BLIZ D 1 ACS_NamedExecuteAlways("core_noammo",0)
Goto Ready+1
}
}

actor CrocGeminiLaserAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}

// Separate spawner to prevent CrocGeminiLaser from *not* having its angle on spawn
//
actor CrocGeminiLaserShooter
{
	PROJECTILE
	Height 0
	Radius 0
	+NOINTERACTION
	+NOCLIP
	speed 1
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 ACS_NamedExecuteWithResult("core_SetVelPitch",0,0)
		TNT1 A 1 A_CustomMissile("CrocGeminiLaserFast",0,0,0,CMF_AIMDIRECTION|CMF_SAVEPITCH,pitch)
		stop
	}
}

actor CrocGeminiLaserFast : BasicProjectile
{
	+HITTARGET
	+USEBOUNCESTATE
	+FORCEXYBILLBOARD
	+BRIGHT
	WALLBOUNCEFACTOR 0.91
	BOUNCEFACTOR 0.91
	Obituary "$OB_GEMINILASER"
	damagetype "GeminiLaser"
	damage (30)
	speed 50
	Radius 5
	Height 5
	States
	{
	Spawn:
		GEMI A 0
		GEMI A 0 A_SpawnItemEx("CrocGeminiTrail", -0.0*momx, -0.0*momy, -0.0*momz, 0, 0, 0, 0, SXF_ABSOLUTEPOSITION|SXF_WEPFXCOLOR)
		GEMI A 0 A_SpawnItemEx("CrocGeminiTrail", -0.1*momx, -0.1*momy, -0.1*momz, 0, 0, 0, 0, SXF_ABSOLUTEPOSITION|SXF_WEPFXCOLOR)
		GEMI A 0 A_SpawnItemEx("CrocGeminiTrail", -0.2*momx, -0.2*momy, -0.2*momz, 0, 0, 0, 0, SXF_ABSOLUTEPOSITION|SXF_WEPFXCOLOR)
		GEMI A 0 A_SpawnItemEx("CrocGeminiTrail", -0.3*momx, -0.3*momy, -0.3*momz, 0, 0, 0, 0, SXF_ABSOLUTEPOSITION|SXF_WEPFXCOLOR)
		GEMI A 0 A_SpawnItemEx("CrocGeminiTrail", -0.4*momx, -0.4*momy, -0.4*momz, 0, 0, 0, 0, SXF_ABSOLUTEPOSITION|SXF_WEPFXCOLOR)
		GEMI A 0 A_SpawnItemEx("CrocGeminiTrail", -0.5*momx, -0.5*momy, -0.5*momz, 0, 0, 0, 0, SXF_ABSOLUTEPOSITION|SXF_WEPFXCOLOR)
		GEMI A 0 A_SpawnItemEx("CrocGeminiTrail", -0.6*momx, -0.6*momy, -0.6*momz, 0, 0, 0, 0, SXF_ABSOLUTEPOSITION|SXF_WEPFXCOLOR)
		GEMI A 0 A_SpawnItemEx("CrocGeminiTrail", -0.7*momx, -0.7*momy, -0.7*momz, 0, 0, 0, 0, SXF_ABSOLUTEPOSITION|SXF_WEPFXCOLOR)
		GEMI A 0 A_SpawnItemEx("CrocGeminiTrail", -0.8*momx, -0.8*momy, -0.8*momz, 0, 0, 0, 0, SXF_ABSOLUTEPOSITION|SXF_WEPFXCOLOR)
		GEMI A 0 A_SpawnItemEx("CrocGeminiTrail", -0.9*momx, -0.9*momy, -0.9*momz, 0, 0, 0, 0, SXF_ABSOLUTEPOSITION|SXF_WEPFXCOLOR)
		GEMI A 1
		loop
	Death:
		GEMI A 0 A_SpawnItemEx("CrocGeminiLaserRicochet", -momx, -momy, -momz, 0, 0, 0, 0, SXF_ABSOLUTEPOSITION|SXF_TRANSFERPITCH)
		stop
	XDeath:
		GEMI A 0 A_SpawnItemEx("CrocGeminiImpact", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPOINTERS|SXF_WEPFXCOLOR)
		stop
	}
}

// Directly spawning GeminiLaser from the position of CrocGeminiLaserFast would occasionally lead to GeminiLaser dying
// this is likely due to how fast CrocGeminiLaserFast goes
// so i need a separate ricochet actor to handle the thing
//
actor CrocGeminiLaserRicochet : CrocGeminiLaserShooter
{
	States
	{
	Spawn:
		GEMI AA 0 A_CustomMissile("CrocGeminiLaser",0,0,0,CMF_AIMDIRECTION,pitch)
		stop
	}
}

actor CrocGeminiLaser : BasicBouncer
{
	WALLBOUNCEFACTOR 0.91
	BOUNCEFACTOR 0.91
	Obituary "$OB_GEMINILASER"
	speed 22
	Radius 5
	Height 5
	damagetype "GeminiLaser"
	damage (30)
	+FORCEXYBILLBOARD
	+USEBOUNCESTATE
	+BRIGHT
	reactiontime 150
	States
	{
	Spawn:
		GEMI A 0
		GEMI A 0 A_SpawnItemEx("CrocGeminiTrail", -0.00*momx, -0.00*momy, -0.00*momz, 0, 0, 0, 0, SXF_ABSOLUTEPOSITION|SXF_WEPFXCOLOR)
		GEMI A 0 A_SpawnItemEx("CrocGeminiTrail", -0.33*momx, -0.33*momy, -0.33*momz, 0, 0, 0, 0, SXF_ABSOLUTEPOSITION|SXF_WEPFXCOLOR)
		GEMI A 0 A_SpawnItemEx("CrocGeminiTrail", -0.66*momx, -0.66*momy, -0.66*momz, 0, 0, 0, 0, SXF_ABSOLUTEPOSITION|SXF_WEPFXCOLOR)
		GEMI A 1 A_CountDown
		loop
	Bounce:
		GEMI A 0 A_SpawnItemEx("CrocGeminiImpact",0,0,0,0,0,0,0,SXF_WEPFXCOLOR)
		goto Spawn
	Death:
		GEMI A 0 A_JumpIf(reactiontime < 150, "Effect")
		GEMI A 1
	Effect:
		GEMI A 0 A_SpawnItemEx("CrocGeminiImpact", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPOINTERS|SXF_WEPFXCOLOR)
		stop
	}
}

actor CrocGeminiTrail : BasicGraphicEffect
	{
	+FORCEXYBILLBOARD
	+BRIGHT
	Speed 60
	States
	{
	SpawnFrame:
		GEMI A 5
		stop
	}
}

actor CrocGeminiImpact : BasicGraphicEffect
{
	Speed 80
	+FORCEXYBILLBOARD
	+BRIGHT
	States
	{
	SpawnFrame:
		TNT1 A 0
		TNT1 AAA 1 A_SpawnItemEx("CrocGeminiImpactFX", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPOINTERS|SXF_WEPFXCOLOR)
		stop
	}
}

actor CrocGeminiImpactFX : GeminiImpact
{
	States
	{
	SpawnFrame:
		GEMI BCDE 2
		stop
	}
}
