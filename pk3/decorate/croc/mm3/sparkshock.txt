actor SparkShockWep_NormalBar : NormalBar { args 4, 225 }
actor SparkShockWep_ScriptBar : ScriptBar {}

actor SparkShockWep : BaseMM8BDMWep 10057
{
//$Category MM8BDM-Weapons/Ranged
//$Title Spark Shock
//$Sprite WEA2G0
Weapon.AmmoUse 2
Weapon.AmmoGive 28
Weapon.SlotNumber 2
Inventory.Pickupmessage "$PU_SPARKSHOCK"
Obituary "$OB_SPARKSHOCK"
Tag "$TAG_SPARKSHOCK"
weapon.ammotype "SparkShockAmmo"
inventory.icon "SPARSI"
States
{
SpawnLoop:
WEA2 G -1
loop
Ready:
SPAS C 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_SPARKSHOCK)
SPAS C 0 A_JumpIfInventory("SparkShockCooldownFlag",1,"ReadyCooldown")
SPAS C 1 A_WeaponReady
Goto Ready+1
ReadyCooldown:
SPAS C 1 A_WeaponReady(WRF_NOFIRE)
Goto Ready+1
Deselect:
SPAS C 0
goto DeselectSwap
Select:
SPAS C 0
goto SelectSwap
Fire:
SPAS C 0 A_JumpIfNoAmmo("NoAmmo")
SPAS C 0 A_PlaySoundEx("weapons/mm3/sparkshockfire","Weapon")
SPAS C 0 A_FireCustomMissile("SparkShock",0,1,8,0)
SPAS ED 2
SPAS D 0 A_GiveInventory("SparkShockCooldownFlag",1)
SPAS D 0 A_SpawnItemEx("SparkShockCooldowner")
SPAS DDDDDDDD 1 A_WeaponReady(WRF_NOFIRE)
Goto Ready+1
NoAmmo:
SPAS C 1 ACS_NamedExecuteAlways("core_noammo",0)
Goto Ready+1
}
}

actor SparkShockCooldownFlag : Once {}

actor SparkShockCooldowner : BasicCooldowner
{
reactiontime 24
States
{
Spawn:
TNT1 A 0
Spawn2:
TNT1 A 1 A_GiveInventory("SparkShockCooldownerFunc",1)
loop
Death:
TNT1 A 0 A_TakeFromTarget("SparkShockCooldownFlag",1)
stop
}
}

actor SparkShockCooldownerFunc : BasicCooldownerFunc
{
States
{
Pickup:
TNT1 A 0 A_JumpIfInTargetInventory("SparkShockCooldownFlag",1,"PickupBasic")
goto PickupRemove
}
}

actor SparkShockAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}

actor SparkShock : BasicProjectile
{
+FORCEXYBILLBOARD
+BRIGHT
Radius 6
Height 6
scale 2.5
damage (1)
speed 45
damagetype "SparkShock"
Obituary "$OB_SPARKSHOCK"
States
{
Spawn:
SPAS AB 4
loop
Crash:
SPAS A 0 A_SpawnItemEx("SparkShockExplode")
Death:
SPAS A 0 A_SpawnItemEx("ElectrifyEffect")
stop
}
}

actor SparkShockExplode : BasicExplosion
{
    damagetype "SparkShock"
    Obituary "$OB_SPARKSHOCK"
    scale 3.6
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_Explode(15, 80, 0, 0, 80)
        SPAS AHIJ 1
        stop
    }
}

actor ShockFX : BasicGraphicEffect
{
+BRIGHT
States
{
Spawn:
SPAS F 0
SPAS FFGGFFGGFFGGFFGGFFGGFFGGFFGG 1
stop
}
}

actor SparkShockFX : ShockFX
{
    States
    {
    Spawn:
        SPAS F 0
        SPAS FFGGFFGGFFGGFFGGFFGGFFGGFFGG 1 A_Warp(AAPTR_TARGET, 6, 0, 32, 0, WARPF_INTERPOLATE)
        stop
    }
}
