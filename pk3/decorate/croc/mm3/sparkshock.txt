actor CrocSparkShockWep_NormalBar : NormalBar { args 4, 225 }
actor CrocSparkShockWep_ScriptBar : ScriptBar {}

actor CrocSparkShockWep : BaseMM8BDMWep
{
//$Category MM8BDM-Weapons/Ranged
//$Title Spark Shock
//$Sprite WEA2G0
Weapon.AmmoUse 2
Weapon.AmmoGive 28
Weapon.SlotNumber 2
Inventory.Pickupmessage "$PU_SPARKSHOCK"
Obituary "$OB_SPARKSHOCK"
Tag "$TAG_SPARKSHOCK"
weapon.ammotype "CrocSparkShockAmmo"
inventory.icon "SPARSI"
States
{
SpawnLoop:
WEA2 G -1
loop
Ready:
SPAS C 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_SPARKSHOCK)
SPAS C 0 A_JumpIfInventory("CrocSparkShockCooldownFlag",1,"ReadyCooldown")
SPAS C 1 A_WeaponReady
Goto Ready+1
ReadyCooldown:
SPAS C 1 A_WeaponReady(WRF_NOFIRE)
Goto Ready+1
Deselect:
SPAS C 0
goto DeselectSwap
Select:
SPAS C 0
goto SelectSwap
Fire:
SPAS C 0 A_JumpIfNoAmmo("NoAmmo")
SPAS C 0 A_PlaySoundEx("weapons/mm3/sparkshockfire","Weapon")
SPAS C 0 A_FireCustomMissile("CrocSparkShock",0,1,8,0)
SPAS ED 2
SPAS D 0 A_GiveInventory("CrocSparkShockCooldownFlag",1)
SPAS D 0 A_SpawnItemEx("CrocSparkShockCooldowner")
SPAS DDDDDDDD 1 A_WeaponReady(WRF_NOFIRE)
Goto Ready+1
NoAmmo:
SPAS C 1 ACS_NamedExecuteAlways("core_noammo",0)
Goto Ready+1
}
}

actor CrocSparkShockCooldownFlag : Once {}

actor CrocSparkShockCooldowner : BasicCooldowner
{
reactiontime 24
States
{
Spawn:
TNT1 A 0
Spawn2:
TNT1 A 1 A_GiveInventory("CrocSparkShockCooldownerFunc",1)
loop
Death:
TNT1 A 0 A_TakeFromTarget("CrocSparkShockCooldownFlag",1)
stop
}
}

actor CrocSparkShockCooldownerFunc : BasicCooldownerFunc
{
States
{
Pickup:
TNT1 A 0 A_JumpIfInTargetInventory("CrocSparkShockCooldownFlag",1,"PickupBasic")
goto PickupRemove
}
}

actor CrocSparkShockAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}

actor CrocSparkShock : BasicProjectile
{
+FORCEXYBILLBOARD
+BRIGHT
Radius 6
Height 6
scale 2.5
damage (1)
speed 45
damagetype "SparkShock"
Obituary "$OB_SPARKSHOCK"
States
{
Spawn:
SPAS AB 4
loop
Crash:
SPAS A 0 A_SpawnItemEx("CrocSparkShockExplode")
Death:
SPAS A 0 A_SpawnItemEx("ElectrifyEffect")
stop
}
}

actor CrocSparkShockExplode : BasicExplosion
{
    damagetype "SparkShock"
    Obituary "$OB_SPARKSHOCK"
    scale 3.6
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_Explode(15, 80, 0, 0, 80)
        SPAS AHIJ 1
        stop
    }
}

actor CrocShockFX : BasicGraphicEffect
{
+BRIGHT
States
{
Spawn:
SPAS F 0
SPAS FFGGFFGGFFGGFFGGFFGGFFGGFFGG 1
stop
}
}

actor CrocSparkShockFX : ShockFX
{
    States
    {
    Spawn:
        SPAS F 0
        SPAS FFGGFFGGFFGGFFGGFFGGFFGGFFGG 1 A_Warp(AAPTR_TARGET, 6, 0, 32, 0, WARPF_INTERPOLATE)
        stop
    }
}
