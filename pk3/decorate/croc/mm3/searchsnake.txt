actor CrocSearchSnakeWep_NormalBar : NormalBar { args 4, 110 }

actor CrocSearchSnakeWep : BaseMM8BDMWep 10042
{
//$Category MM8BDM-Weapons/Ranged
//$Title Search Snake
//$Sprite WEAPK0
Weapon.AmmoUse 1
Weapon.AmmoGive 56
Weapon.SlotNumber 2
Inventory.Pickupmessage "$PU_SEARCHSNAKE"
Obituary "$OB_SEARCHSNAKE"
Tag "$TAG_SEARCHSNAKE"
weapon.ammotype "CrocSearchSnakeAmmo"
inventory.icon "SNAKESI"
States
{
SpawnLoop:
WEAP K -1
loop
Ready:
SNAK I 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_SEARCHSNAKE)
SNAK I 1 A_WeaponReady
Goto Ready+1
Deselect:
SNAK I 0
goto DeselectSwap
Select:
SNAK I 0
goto SelectSwap
Fire:
SNAK I 0 A_JumpIfNoAmmo("NoAmmo")
SNAK I 0 A_PlaySoundEx("weapons/mm3/searchsnakefire","Weapon")
SNAK I 0 A_FireCustomMissile("CrocSearchSnakeStart",0,1,8,0)//A_SpawnItemEx("CrocSearchSnakeStart",6,0,0,8,0,8,0,0,0)
SNAK JK 5
SNAK I 4
SNAK I 0 A_Refire
Goto Ready+1
NoAmmo:
SNAK I 1 ACS_NamedExecuteAlways("core_noammo",0)
Goto Ready+1
}
}

actor CrocSearchSnakeAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 56
+INVENTORY.IGNORESKILL
}


actor CrocSearchSnakeBase : BasicProjectile
{
Damage (32)
Speed 15
damagetype "SearchSnake"
Obituary "$OB_SEARCHSNAKE"
Height 5
Radius 6
}

actor CrocSearchSnakeStart : CrocSearchSnakeBase
{
Speed 23
-NOGRAVITY
States
{
Spawn:
SNAK A 4
loop
Death:
TNT1 A 0 A_SpawnItemEx("CrocSearchSnakeMaster")
stop
XDeath:
TNT1 A 0
stop
}
}

const int SXF_SEARCHSNAKESTART = (SXF_MULTIPLYSPEED|SXF_TRANSFERPITCH);
const int SXF_SEARCHSNAKE = (SXF_TRANSFERPOINTERS|SXF_SEARCHSNAKESTART);

actor CrocSearchSnakeMaster : CrocSearchSnakeBase
{
+NOCLIP
+NOINTERACTION
//reactiontime 10
reactiontime 100
states
{
Spawn:
TNT1 A 0
TNT1 A 0 A_SetPitch(PITCH_FORWARD)
TNT1 A 0 A_SpawnItemEx("CrocSearchSnakeCrawl",0,0,0,cos(pitch),0,-sin(pitch),0,SXF_SEARCHSNAKESTART,0,CallACS("core_NewUniqueTID"))
TNT1 A 0 ACS_NamedExecuteWithResult("core_setTIDsPointerToThis",CallACS("core_CurrentUniqueTID"),AAPTR_MASTER)
TNT1 A 0 ACS_NamedExecuteWithResult("core_ClearUniqueTID",0)
SpawnLoop:
SNAK A 1 A_GiveInventory("CrocSearchSnakeMasterFunc",1)
loop
CustomState1:
SNAK A 1 A_ChangeVelocity(15,0,0,CVF_RELATIVE|CVF_REPLACE)
SpawnForward:
SNAK AAABBBBA 1 A_GiveInventory("CrocSearchSnakeMasterFunc",1)
loop
CustomState2:
SNAK E 1 A_ChangeVelocity(0,0,-15,CVF_RELATIVE|CVF_REPLACE)
SpawnDown:
SNAK EEEFFFFE 1 A_GiveInventory("CrocSearchSnakeMasterFunc",1)
loop
CustomState3:
SNAK C 1 A_ChangeVelocity(0,0,15,CVF_RELATIVE|CVF_REPLACE)
SpawnUp:
SNAK CCCDDDDC 1 A_GiveInventory("CrocSearchSnakeMasterFunc",1)
loop
Death:
TNT1 A 0 A_RemoveChildren(true)
stop
}
}

actor CrocSearchSnakeMasterFunc : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_GiveInventory("CountChildrenFunc",1)
TNT1 A 0 A_JumpIfInventory("ChildCount",1,"Pickup2")
TNT1 A 0 Thing_Remove(0)
stop
Pickup2:
TNT1 A 0 A_GiveInventory("CrocSearchSnakeMasterStateFunc",1)
TNT1 A 0 A_CountDown
stop
}
}

actor CrocSearchSnakeMasterStateFunc : CustomInventory
{
states
{
Pickup:
TNT1 A 0 A_JumpIf(Pitch==PITCH_FORWARD,"PickupF")
TNT1 A 0 A_JumpIf(Pitch==PITCH_DOWN,"PickupD")
TNT1 A 0 A_JumpIf(Pitch==PITCH_UP,"PickupU")
stop
PickupF:
TNT1 A 0 ACS_NamedExecuteWithResult("core_updatecustomstate",1)
stop
PickupD:
TNT1 A 0 ACS_NamedExecuteWithResult("core_updatecustomstate",2)
stop
PickupU:
TNT1 A 0 ACS_NamedExecuteWithResult("core_updatecustomstate",3)
stop
}
}

actor CrocSearchSnakeCrawl : CrocSearchSnakeBase
{
+INVISIBLE
+MOVEWITHSECTOR
+DONTSPLASH
+SERVERSIDEONLY
+NONETID
var int user_NoTranslation;
states
{
Spawn:
TNT1 A 0
BUST A 1 A_GiveInventory("WarpMasterHere",1)
TNT1 A 0 ACS_NamedExecuteWithResult("core_setpointerpitch",pitch,AAPTR_MASTER)
TNT1 A 0 A_JumpIf(pitch==PITCH_DOWN,"SpawnD")
TNT1 A 0 A_SetPitch(pitch+90)
TNT1 A 0 A_SpawnItemEx("CrocSearchSnakeCrawl",0,0,0,cos(pitch)*2,0,-sin(pitch)*2,0,SXF_SEARCHSNAKE)
stop
SpawnD:
TNT1 A 0 A_SpawnItemEx("CrocSearchSnakeCrawl",0,0,0,cos(pitch),0,-sin(pitch),0,SXF_SEARCHSNAKE)
stop
Death:
TNT1 A 0 A_JumpIf(pitch==PITCH_UP,"DeathU")
TNT1 A 0 A_SetPitch(pitch-90)
TNT1 A 0 A_SpawnItemEx("CrocSearchSnakeCrawl",0,0,0,cos(pitch),0,-sin(pitch),0,SXF_SEARCHSNAKE)
stop
DeathU:
TNT1 A 0 A_SpawnItemEx("CrocSearchSnakeCrawlD",0,0,0,cos(pitch-45),0,-sin(pitch-45),0,SXF_SEARCHSNAKE)
stop
XDeath:
TNT1 A 0 A_RemoveMaster
stop
}
}

actor CrocSearchSnakeCrawlD : SearchSnakeCrawl
{
states
{
Death:
TNT1 A 0
goto XDeath
}
}
