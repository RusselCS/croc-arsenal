actor CrocSolarBlazeWep_NormalBar : NormalBar { args 248, 222 }

actor CrocSolarBlazeWep : BaseMM8BDMWep 10096
{
//$Category MM8BDM-Weapons/Power
//$Title Solar Blaze
//$Sprite SOLAA0
Weapon.AmmoUse 2
Weapon.AmmoGive 30
Weapon.SlotNumber 5
Obituary "$OB_SOLARBLAZE"
Tag "$TAG_SOLARBLAZE"
Inventory.Pickupmessage "$PU_SOLARBLAZE"
weapon.ammotype "CrocSolarBlazeAmmo"
inventory.icon "SOLARI"
States
{
SpawnLoop:
SOLA A -1
loop
Ready:
SOLA F 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_SOLARBLAZE)
SOLA F 1 A_WeaponReady
Goto Ready+1
Deselect:
SOLA F 0
goto DeselectSwap
Select:
SOLA F 0
goto SelectSwap
Fire:
SOLA F 0 A_JumpIfNoAmmo("NoAmmo")
SOLA F 0 A_PlaySoundEx("weapons/mm10/solarblazefire","Weapon")
SOLA F 0 A_FireCustomMissile("CrocSolarBlaze",0,1,8,-16)
SOLA F 0 A_GiveInventory("CrocSolarBlazeCooldown", 1)
SOLA GH 3
SOLA F 0 A_JumpIfInventory("IsBot", 1, "BotCheck")
SOLA F 0 A_Refire
Goto Hold
AltFire:
SOLA F 0 A_JumpIfNoAmmo("NoAmmo")
SOLA F 0 A_PlaySoundEx("weapons/mm10/solarblazefire","Weapon")
SOLA F 0 A_FireCustomMissile("CrocSolarBlaze",0,1,8,-16)
SOLA GH 3
SOLA F 1
SOLA F 2 A_GiveInventory("CrocSolarBlazeFlag",2)
SOLA F 27
SOLA F 0 A_TakeInventory("CrocSolarBlazeFlag",999)
SOLA F 0 A_ClearRefire
Goto Ready+1
Hold:
SOLA F 1 A_RailWait
SOLA F 0 A_Refire
Release:
SOLA F 5 A_GiveInventory("CrocSolarBlazeFlag",2)
SOLA F 0 A_TakeInventory("CrocSolarBlazeFlag",999)
ReleaseWait:
SOLA F 1 A_RailWait
SOLA F 0 A_JumpIfInventory("CrocSolarBlazeCooldown", 1, "ReleaseWait")
SOLA F 1 A_JumpIf(true, "ReleaseEnd")
wait
ReleaseEnd:
SOLA F 0
Goto Ready+1
BotCheck:
SOLA F 5 A_RailWait
SOLA F 0 A_Jump(128, "BotCheck")
Goto Hold+2
NoAmmo:
SOLA F 1 ACS_NamedExecuteAlways("core_noammo",0)
Goto Ready+1
}
}

actor CrocSolarBlazeFlag : Inventory
{
inventory.amount 1
inventory.maxamount 2
}

actor CrocSolarBlazeCooldown : Powerup { Powerup.duration 36 }

actor CrocSolarBlazeAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}

actor CrocSolarBlaze : BasicBouncer
{
+FORCEXYBILLBOARD
+BRIGHT
Obituary "$OB_SOLARBLAZE"
DamageType "SolarBlaze"
bouncefactor 0.75
wallbouncefactor 0.75
bouncecount 9999
Radius 8
Height 32
damage (34)
speed 40
reactiontime 1
States
{
Spawn:
SOLA BBBCCCBBBCCCBBBCCCBBBCCCBBBCCCBBBCCC 1 A_JumpIfInTargetInventory("CrocSolarBlazeFlag", 2, "FlyEnd")
FlyEnd:
SOLA B 0 A_Stop
SOLA BC 3
SOLA B 1 A_Countdown
wait
Death:
TNT1 B 0 A_PlaySoundEx("weapons/mm10/solarblazesplit","weapon")
TNT1 B 0 A_SpawnItemEx("CrocSolarWave",0,0,0,1.0,0,0,0,SXF_MULTIPLYSPEED)
TNT1 B 0 A_SpawnItemEx("CrocSolarWave",0,0,0,1.0,0,0,90,SXF_MULTIPLYSPEED)
TNT1 B 0 A_SpawnItemEx("CrocSolarWave",0,0,0,1.0,0,0,-90,SXF_MULTIPLYSPEED)
TNT1 B 0 A_SpawnItemEx("CrocSolarWave",0,0,0,1.0,0,0,180,SXF_MULTIPLYSPEED)
TNT1 B 0 A_SpawnItemEx("OilPitIgnite",0,0,-16)
TNT1 A 35 A_SpawnItemEx("ExplosionEffect1", 0, 0, 16, 0, 0, 0, 0, SXF_WEPFXCOLOR)
stop
}
}

actor CrocSolarBlazeAlt : SolarBlaze
{
    States
    {
    Spawn:
        SOLA BCBC 3
        goto FlyEnd
    }
}

actor CrocSolarWave : BasicProjectile
{
+RIPPER
+THRUACTORS
+BRIGHT
Obituary "$OB_SOLARBLAZE"
DamageType "SolarBlaze"
Radius 8
Height 32
damage (17)
speed 28
States
{
Spawn:
SOLA D 0
SOLA Z 1
SOLA Z 0 A_ChangeFlag(THRUACTORS, false)
SOLA DE 2
goto Spawn+3
}
}
