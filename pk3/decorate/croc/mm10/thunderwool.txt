actor CrocThunderWoolWep_NormalBar : NormalBar { args 248, 95 }

actor CrocThunderWoolWep : BaseMM8BDMWep 10101
{
//$Category MM8BDM-Weapons/Close
//$Title Thunder Wool
//$Sprite THWOA0
Weapon.AmmoUse 3
Weapon.AmmoGive 30
Weapon.SlotNumber 4
Obituary "$OB_THUNDERWOOL"
Tag "$TAG_THUNDERWOOL"
Inventory.Pickupmessage "$PU_THUNDERWOOL"
weapon.ammotype "CrocThunderWoolAmmo"
inventory.icon "TWOOLI"
States
{
SpawnLoop:
THWO A -1
loop
Ready:
THWO N 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_THUNDERWOOL)
THWO N 1 A_WeaponReady
Goto Ready+1
Deselect:
THWO N 0
goto DeselectSwap
Select:
THWO N 0
goto SelectSwap
Fire:
THWO N 0 A_JumpIfNoAmmo("NoAmmo")
THWO N 0 A_PlaySoundEx("weapons/mm10/thunderwoolfire","Weapon")
THWO N 0 A_FireCustomMissile("CrocThunderWool",0,1,8,0)
THWO OP 6
THWO N 18
THWO N 0 A_Refire
Goto Ready+1
NoAmmo:
THWO N 1 ACS_NamedExecuteAlways("core_noammo",0)
Goto Ready+1
}
}

actor CrocThunderWoolAmmo : Ammo
{
    inventory.amount 1
    inventory.maxamount 30
    +INVENTORY.IGNORESKILL
}

const int WEPC_10TW_FLOORDIST = 96;

actor CrocThunderWool : BasicProjectile
{
Radius 16
Height 7
+DOOMBOUNCE
-ALLOWBOUNCEONACTORS
+SKYEXPLODE
+RIPPER
Obituary "$OB_THUNDERWOOL"
Damagetype "CrocThunderWool"
bouncecount 9999
bouncefactor 0.9
wallbouncefactor 0.9
+FORCEXYBILLBOARD
damage (0)
speed 20
reactiontime 7
var int user_atFloor;
States
{
Spawn:
THWO B 0
THWO BCBC 3 ThrustThingZ(0,9,0,1)
THWO E 0 ThrustThingZ(0,9,0,1)
THWO EEBDDCEEB 1
THWO B 0 A_SpawnItemEx("CrocThunderWoolLightninginator") // This could be on the same actor, but syncing that sucks.
stop
Death:
TNT1 A 0 A_SpawnItemEx("ExplosionEffect1", 0, 0, 0, 0, 0, 0, 0, SXF_WEPFXCOLOR)
stop
}
}

actor CrocThunderWoolLightninginator : ThunderWool
{
	+BRIGHT
    States
    {
    Spawn:
        THWO F 0
        THWO F 0 A_PlaySoundEx("weapons/mm10/thunderwoolexplode","Weapon")
        THWO F 0 A_SetUserVar(user_atFloor, (z-floorz)<96)
        THWO F 0 A_JumpIf(z-floorz < 64, "SpawnLoopFloor")
        goto SpawnLoop
    Spawnloop:
        THWO B 0 A_SpawnItemEx("CrocThunderWoolLightning", 0, 0, !user_atFloor*-96 + user_atFloor*-(z-floorz))
        THWO B 0 A_SpawnItemEx("CrocThunderWoolRadius", 0, 0, -(z-floorz))
        THWO CGGG 1
        THWO B 0 A_SpawnItemEx("CrocThunderWoolLightning", 0, 0, !user_atFloor*-96 + user_atFloor*-(z-floorz))
        THWO BFFF 1
        THWO C 0 A_CountDown
        loop
    SpawnLoopFloor:
        THWO B 0 A_SpawnItemEx("CrocThunderWoolRadius", 0, 0, -(z-floorz))
        THWO CGGGBFFF 1
        THWO C 0 A_CountDown
        loop
    }
}

// Lightning bolt damage
actor CrocThunderWoolLightning : BasicProjectile
{
	+THRUSPECIES
	species "MovingPlatform"
    Obituary "$OB_THUNDERWOOL"
	Damagetype "CrocThunderWool"
	speed 0
	height 32
	radius 16
    damage (10)
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SpawnItemEX("ElectrifyEffect")
        TNT1 A 0 A_JumpIf(z-floorz < 64, "FloorCheck")
		TNT1 A 0 A_SpawnItemEx("CrocThunderWoolLightning", 0, 0, -64)
        goto SpawnWait
    FloorCheck:
        TNT1 A 0 A_JumpIf(z-floorz < 8, "SpawnWait")
        TNT1 A 0 A_SpawnItemEx("CrocThunderWoolLightning", 0, 0, -(z-floorz))
        goto SpawnWait
    SpawnWait:
		TNT1 A 1
		TNT1 A 0 A_SpawnItemEx("CrocThunderWoolLightningFX", 0, 0, 0, 0, 0, 0, 0, SXF_WEPFXCOLOR)
		TNT1 A 1
		stop
	}
}

// Explosion damage from the floor
const int WEPC_10TW_ARCMAX = 8;
const int WEPC_10TW_RADIUS = 128;

actor CrocThunderWoolRadius : BasicExplosion
{
	Obituary "$OB_THUNDERWOOL"
	Damagetype "CrocThunderWool"
	var int user_angle;
	var int user_arcnum;
	States
	{
	Spawn:
        TNT1 A 0
        TNT1 A 0 A_SpawnItemEx("CrocThunderWoolImpact", 0, 0, 0, 0, 0, 0, 0, SXF_WEPFXCOLOR)
		TNT1 A 0 A_SpawnItemEX("ElectrifyEffect")
        TNT1 A 0 A_Explode(13, WEPC_10TW_RADIUS, 0, 0, WEPC_10TW_RADIUS)
        TNT1 AAAAAAAAAA 0 A_SpawnItemEx("CrocThunderWoolSparkFX", frandom(-WEPC_10TW_RADIUS, WEPC_10TW_RADIUS), frandom(-WEPC_10TW_RADIUS, WEPC_10TW_RADIUS), frandom(0, WEPC_10TW_RADIUS), 0, 0, 0, 0, SXF_WEPFXCOLOR)
        TNT1 A 1
		TNT1 A 0 A_SetUserVar(user_arcnum, WEPC_10TW_ARCMAX+1)
	SpawnArcs:
		TNT1 A 0 A_SpawnItemEx("CrocThunderWoolArc", 16, 0, 0, 0, 0, 0, user_arcnum*(360/WEPC_10TW_ARCMAX), SXF_CLIENTSIDE|SXF_ABSOLUTEANGLE|SXF_WEPFXCOLOR)
		TNT1 A 0 A_SetUserVar(user_arcnum, user_arcnum-1)
		TNT1 A 0 A_JumpIf(user_arcnum > 0, "SpawnArcs")
		TNT1 A 1 A_JumpIf(true, "End")
		wait
	End:
		TNT1 A 10
		stop
	}
}

actor CrocThunderWoolArc : BasicGraphicEffect
{
	+BRIGHT
	var int user_length;
	States
	{
	SpawnFrame:
		TNT1 A 0	
		TNT1 A 0 A_SetUserVar(user_length, WEPC_10TW_RADIUS/12-1)
		TNT1 A 0 A_SetPitch(WEPC_10TW_RADIUS/12-1)
		TNT1 A 0 A_SpawnItemEx("CrocThunderWoolArcFX", 16, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPITCH|SXF_WEPFXCOLOR)
		stop
	}
}

// Particles

actor CrocThunderWoolArcFX : ThunderWoolArc
{
	+BRIGHT
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_SetPitch(pitch-1)
		TNT1 A 0 A_JumpIf(pitch < 2, "SizeDown")
		TNT1 A 0 A_SpawnItemEx("CrocThunderWoolArcFX", 16, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPITCH|SXF_WEPFXCOLOR)
		goto Frames
	SizeDown:
		TNT1 A 0 A_SetScale(1.75)
		goto Frames
	Frames:
		THWO LMLMLMLML 1
		stop
	}
}

actor CrocThunderWoolLightningFX : BasicGraphicEffect
{
	+BRIGHT
    States
    {
    SpawnFrame:
        THWO H 0
        THWO HIH 2
        stop
    }
}

actor CrocThunderWoolImpact : BasicGraphicEffect
{
	+FLOORHUGGER
	+FORCEXYBILLBOARD
	+BRIGHT
	Scale 2.0
	States
	{
	SpawnFrame:
		THWO JKJKJ 2
		stop
	}
}

actor CrocThunderWoolSparkFX : CrocThunderWoolImpact
{
    Scale 1.75
    States
    {
    SpawnFrame:
        THWO VSTU 1
        stop
    }
}
