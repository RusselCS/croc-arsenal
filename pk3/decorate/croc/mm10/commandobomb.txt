const int WEPC_10CB_COOLDOWN = 20;

actor CommandoBombWep_NormalBar : NormalBar { args 88, 222 }

actor CommandoBombWep : BaseMM8BDMWep 10099
{
//$Category MM8BDM-Weapons/Power
//$Title Commando Bomb
//$Sprite COMMA0
Weapon.AmmoUse 5
Weapon.AmmoGive 30
Weapon.SlotNumber 5
Obituary "$OB_COMMANDOBOMB"
Tag "$TAG_COMMANDOBOMB"
Inventory.Pickupmessage "$PU_COMMANDOBOMB"
weapon.ammotype "CommandoBombAmmo"
inventory.icon "COMMAI"
States
{
SpawnLoop:
COMM A -1
loop
Ready:
COMM I 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_COMMANDOBOMB)
COMM I 1 A_WeaponReady
Goto Ready+1
Deselect:
COMM I 0 A_JumpIfInventory("CommandoSwitch",1,1)
goto DeselectSwap
COMM I 10 A_TakeInventory("CommandoSwitch",999)
goto DeselectSwap
Select:
COMM I 0
goto SelectSwap
AltFire:
COMM I 0 A_JumpIfNoAmmo("NoAmmo")
COMM I 0 A_PlaySoundEx("weapons/mm10/commandobombfire","Weapon")
COMM I 0 A_FireCustomMissile("CommandoBombSecondary",0,1,8,0)
COMM B 0 A_GiveInventory("CommandoCooldown", WEPC_10CB_COOLDOWN)
COMM JK 4
goto Cooldown
Fire:
COMM I 0 A_JumpIfInventory("CommandoSwitch",1,"Change")
COMM I 0 A_JumpIfInventory("CommandoCooldown", 1, "Cooldown")
COMM I 0 A_JumpIfNoAmmo("NoAmmo")
COMM I 0 A_PlaySoundEx("weapons/mm10/commandobombfire","Weapon")
COMM I 0 A_FireCustomMissile("CommandoBomb",0,1,8,0)
COMM B 0 A_GiveInventory("CommandoSwitch",1)
COMM B 0 A_GiveInventory("CommandoCooldown", WEPC_10CB_COOLDOWN)
COMM B 0 A_JumpIfInventory("IsBot", 1, "BotWait")
COMM JK 4
goto Hold
Hold:
//COMM I 0 A_TakeInventory("CommandoCooldown", 1)
COMM I 1 A_RailWait
COMM I 0 A_Refire
COMM I 1 A_GiveInventory("CommandoSwitch",1)
COMM I 0 A_TakeInventory("CommandoSwitch",999)
Goto Cooldown
Cooldown:
COMM I 1 A_TakeInventory("CommandoCooldown", 1)
COMM I 0 A_JumpIfInventory("CommandoCooldown", 1, "Cooldown")
goto Ready+1
BotWait:
COMM I 10
goto Ready+1
Change:
COMM I 1 A_GiveInventory("CommandoSwitch",1)
COMM I 0 A_TakeInventory("CommandoSwitch",2)
Goto Cooldown
NoAmmo:
COMM I 1 ACS_NamedExecuteAlways("core_noammo",0)
Goto Ready+1
}
}

actor CommandoBombAmmo : Ammo
{
    inventory.amount 1
    inventory.maxamount 30
    +INVENTORY.IGNORESKILL
}

actor CommandoCooldown : Inventory
{
    inventory.amount 1
    inventory.maxamount 20
}

actor CommandoBomb : BasicProjectile
{
+NOTARGETSWITCH
+DONTBLAST
+SKYEXPLODE
+HITTRACER
Obituary "$OB_COMMANDOBOMB"
damagetype "CommandoBomb"
Radius 15
Height 8
damage (20)
speed 37
States
{
Spawn:
COMM B 0
COMM B 0 A_GiveToTarget("TargetMarker", 1)
COMM BBBB 1 A_JumpIfInTargetInventory("CommandoSwitch",2,"Change")
loop
Change:
COMM B 0 A_PlaySoundEx("weapons/mm10/commandobombswitch","Weapon")
// COMM B 0 A_TakeFromTarget("CommandoSwitch", 2)
COMM B 0 A_SpawnItemEx("CommandoTrailFX", 0, 0, 0, 0, 0, 0, 0, SXF_WEPFXCOLOR)
COMM B 0 A_GiveToTarget("TargetMarker", 1)
COMM B 0 ACS_NamedExecuteAlways("core_aimattargetcursor", 0, CallACS("core_gettarget")+PLN_TARGETPOINT_OFFSET, 0)
COMM B 1 A_Stop
COMM B 0 A_ChangeVelocity(Cos(pitch)*37, 0, -sin(pitch)*37,CVF_RELATIVE)
LoopSpawn:
COMM B 2 A_SpawnItemEx("CommandoTrailFX",0,0,0,0,0,0,0,SXF_WEPFXCOLOR)
loop
Death:
COMM B 0 A_JumpIf(z-floorz<=8,"Death2")
COMM B 0 A_PlaySoundEx("weapons/mm10/commandobombswitch","Weapon")
COMM B 0 A_SpawnItemEx("CommandoTrailFX",0,0,0,0,0,0,0,SXF_WEPFXCOLOR)
DeathLoop:
COMM C 0 A_JumpIf(z-floorz<=8,"Death2")
COMM C 0 A_Stop
COMM C 0 ThrustThingZ(0, 35*4, 1, 0)
COMM C 2 A_SpawnItemEx("CommandoTrailFX",0,0,0,0,0,0,0,SXF_WEPFXCOLOR)
loop
Death2:
COMM B 0 A_PlaySoundEx("weapons/mm10/commandobombexplosion","Weapon")
COMM B 0 A_SpawnItemEx("CommandoBombExplosion", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPOINTERS)
TNT1 A 35 A_SpawnItemEx("CommandoTrailFX",0,0,0,0,0,0,0,SXF_WEPFXCOLOR)
stop
}
}

actor CommandoBombSecondary : CommandoBomb
{
    Speed 37
    States
    {
    Spawn:
        COMM B 0
        COMM B 0 A_PlaySoundEx("weapons/mm10/commandobombswitch","Weapon")
        goto Super::LoopSpawn
    }
}

actor CommandoBombExplosion
{
    PROJECTILE
    +THRUACTORS
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_CheckFlag(SOLID, "Migrate", AAPTR_TRACER)
    Explode:
        TNT1 A 0 A_SpawnItemEx("CommandoBombWaveTrail")
        TNT1 A 0 A_SpawnItemEx("CommandoBombWave", 0, 0, 0, 1, 0, 0, 0*45, SXF_MULTIPLYSPEED)
        TNT1 A 0 A_SpawnItemEx("CommandoBombWave", 0, 0, 0, 1, 0, 0, 1*45, SXF_MULTIPLYSPEED)
        TNT1 A 0 A_SpawnItemEx("CommandoBombWave", 0, 0, 0, 1, 0, 0, 2*45, SXF_MULTIPLYSPEED)
        TNT1 A 0 A_SpawnItemEx("CommandoBombWave", 0, 0, 0, 1, 0, 0, 3*45, SXF_MULTIPLYSPEED)
        TNT1 A 0 A_SpawnItemEx("CommandoBombWave", 0, 0, 0, 1, 0, 0, 4*45, SXF_MULTIPLYSPEED)
        TNT1 A 0 A_SpawnItemEx("CommandoBombWave", 0, 0, 0, 1, 0, 0, 5*45, SXF_MULTIPLYSPEED)
        TNT1 A 0 A_SpawnItemEx("CommandoBombWave", 0, 0, 0, 1, 0, 0, 6*45, SXF_MULTIPLYSPEED)
        TNT1 A 0 A_SpawnItemEx("CommandoBombWave", 0, 0, 0, 1, 0, 0, 7*45, SXF_MULTIPLYSPEED)
        TNT1 A 70
        stop
    Migrate:
        TNT1 A 0 A_Warp(AAPTR_TRACER, 0, 0, -(z-floorz), 0, WARPF_ABSOLUTEANGLE)
        goto Explode
    }
}

actor CommandoWaveball : BasicProjectile
{
    +RIPPER
    -NOGRAVITY
    +BRIGHT
    damage (0)
    radius 16
    height 16
    speed 10
    gravity 2.0
    States
    {
    Spawn:
        MMFX C 1
        loop
    Death:
        MMFX CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC 1 A_JumpIf(z-floorz<=8,"Death2")
        MMFX CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCC 1 A_JumpIf(z-floorz<=8,"Death2")
        MMFX C 0 A_ChangeFlag(NOGRAVITY, true)
    ForceFall:
        MMFX C 0 ThrustThingZ(0, 5, 1, 1)
        MMFX C 1 A_CheckFloor("Death")
        loop
    Death2:
        TNT1 B 0 A_PlaySoundEx("weapons/mm10/commandobombexplosion","Weapon")
        TNT1 A 15 A_SpawnItemEX("CommandoBombWave",0,0,0,1,0,0,0,SXF_MULTIPLYSPEED)
        stop
    }
}

actor CommandoBombWave : BasicProjectile
{
    Obituary "$OB_COMMANDOBOMB"
    damagetype "CommandoBomb"
    +RIPPER
    +STEPMISSILE
    maxstepheight 24
    Speed 15
    Damage (0)
    height 2
    radius 1
    reactiontime 6
    States
    {
    Spawn:
        TNT1 A 1
	Spawn1: 
		TNT1 A 1
        TNT1 A 0 A_JumpIf(z-floorz > 24, "Death")
        TNT1 A 0 A_JumpIf(z-floorz > 0, "WarpToFloor1")
        goto Spawn2
    Spawn2:
        TNT1 A 1
        TNT1 A 0 A_JumpIf(z-floorz > 24, "Death")
        TNT1 A 0 A_JumpIf(reactiontime % 2 == 0, "SpawnFake")
        goto SpawnReal
    SpawnFake:
        TNT1 A 0 A_SpawnItemEx("CommandoBombWaveTrailFake",0,0,0,0,0,0,0,SXF_WEPFXCOLOR)
        goto SpawnEnd
    SpawnReal:
        TNT1 A 0 A_SpawnItemEx("CommandoBombWaveTrail")
    SpawnEnd:
        TNT1 A 0 A_Countdown
        TNT1 A 0 A_JumpIf(z-floorz > 0, "WarpToFloor2")
        goto Spawn1
    WarpToFloor1:
        TNT1 A 0 A_Warp(AAPTR_DEFAULT, 0, 0, -(z-floorz))
        goto Spawn2
    WarpToFloor2:
        TNT1 A 0 A_Warp(AAPTR_DEFAULT, 0, 0, -(z-floorz))
        goto Spawn1
    Death:
        TNT1 A 0
        stop
    }
}

actor SpawnCommandoBombTrail : CustomInventory
{
    States
    {
    Pickup:
        TNT1 A 0 A_JumpIf(reactiontime % 2 == 0, "Fake")
        TNT1 A 0 A_SpawnItemEx("CommandoBombWaveTrail")
        stop
    Fake:
        TNT1 A 0 A_SpawnItemEx("CommandoBombWaveTrailFake")
        stop
    }
}

actor CommandoBombDamagerA
{
    Obituary "$OB_COMMANDOBOMB"
    damagetype "CommandoBomb"
    PROJECTILE
    +FLOORHUGGER
    Damage (6)
    Radius 16
    Height 30
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 1
        stop
    }
}

actor CommandoBombDamagerB : CommandoBombDamagerA { height 40  }
actor CommandoBombDamagerC : CommandoBombDamagerA { height 75  }
actor CommandoBombDamagerD : CommandoBombDamagerA { height 112 }

actor CommandoBombDamagerX : CommandoBombDamagerA { damage (3) }
actor CommandoBombDamagerY : CommandoBombDamagerB { damage (3) }
actor CommandoBombDamagerZ : CommandoBombDamagerC { damage (3) }

actor CommandoBombWaveTrailFake : BasicGraphicEffect
{
    +FORCEYBILLBOARD
    +BRIGHT
    +FLOORHUGGER
    States
    {
    SpawnFrame:
        ETRA A 1
        ETRA B 1
        ETRA C 1
        ETRA D 1
        ETRA C 1
        ETRA B 1
        ETRA A 1
        stop
    }
}

actor CommandoBombWaveTrail : CommandoBombWave
{
    speed 0
    +FLOORHUGGER
    +FORCEYBILLBOARD
    +BRIGHT
    States
    {
    Spawn:
        ETRA A 1
        ETRA B 1 A_SpawnItemEx("CommandoBombDamagerY")
        ETRA C 1 A_SpawnItemEx("CommandoBombDamagerZ")
        ETRA D 1 A_SpawnItemEx("CommandoBombDamagerD")
        ETRA C 1 A_SpawnItemEx("CommandoBombDamagerC")
        ETRA B 1 A_SpawnItemEx("CommandoBombDamagerB")
        ETRA A 1 A_SpawnItemEx("CommandoBombDamagerA")
        stop
    }
}

actor CommandoSwitch : Inventory
{
inventory.amount 1
inventory.maxamount 2
}

actor CommandoTrailFX : ExplosionEffect1
{
    States
    {
    SpawnFrame:
        TNT1 A 2
        goto Super::SpawnFrame+1
    }
}
