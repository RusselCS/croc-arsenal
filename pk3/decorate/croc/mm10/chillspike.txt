actor CrocChillSpikeWep_NormalBar : NormalBar { args 202, 207 }

actor CrocChillSpikeWep : BaseMM8BDMWep
{
//$Category MM8BDM-Weapons/Close
//$Title Chill Spike
//$Sprite CHILA0
Weapon.AmmoUse 2
Weapon.AmmoGive 28
Weapon.SlotNumber 4
Obituary "$OB_CHILLSPIKE"
Tag "$TAG_CHILLSPIKE"
Inventory.Pickupmessage "$PU_CHILLSPIKE"
weapon.ammotype "CrocChillSpikeAmmo"
inventory.icon "CHILLI"
States
{
SpawnLoop:
CHIL A -1
loop
Ready:
CHIS D 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_CHILLSPIKE)
CHIS D 1 A_WeaponReady
Goto Ready+1
Deselect:
CHIS D 0
goto DeselectSwap
Select:
CHIS D 0
goto SelectSwap
Fire:
CHIS D 0 A_JumpIfNoAmmo("NoAmmo")
CHIS D 0 A_PlaySoundEx("weapons/mm10/chillspikefire","Weapon")
CHIS D 0 A_FireCustomMissile("CrocChillShot",0,1,8,0)
CHIS EFD 3
CHIS D 10
CHIS D 0 //A_Refire
Goto Ready+1
NoAmmo:
CHIS D 1 ACS_NamedExecuteAlways("core_noammo",0)
Goto Ready+1
}
}

actor CrocChillSpikeAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}

actor CrocChillShot : BasicProjectile
{
-NOGRAVITY
Obituary "$OB_CHILLSPIKE"
Radius 5
Height 10
gravity 1.8
damage (15)
speed 45
damagetype "ChillShot"
States
{
Spawn:
CHIL B 0
CHIL B 0 A_SetPitch(5)
CHIL B 2 ThrustThingZ(0,20,0,1)
CHIL CD 2
SpawnLoop:
CHIL BCD 2
loop
Death:
CHIL B 0 A_SpawnItemEx("IcePitFreeze")
CHIL B 0 A_JumpIf(z-floorz<=16,"Death2")
CHIL B 0 A_JumpIf(ceilingz-z<=25,"Death3")
// [Russ] Compensation for the object's height.
CHIL B 0 A_SpawnItemEx("CrocWallSpike", -6, 0, 0)
CHIL E 0
stop
XDeath:
TNT1 A 0
TNT1 A 0 A_SpawnItemEx("IcePitFreeze")
Stop
Death2:
CHIL B 0 A_SpawnItemEx("CrocChillSpike", 0, 0, -(z-floorz), 0, 0, 0, 0, SXF_TRANSFERPITCH)
CHIL E 0
stop
Death3:
CHIL B 0 A_SpawnItemEx("CrocCeilingSpike", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERPITCH)
CHIL E 0
stop
}
}

const int WEPC_10CS_SPIKERADIUS = 20;
const int WEPC_10CS_SPIKEEXPLODE = 26;

actor CrocChillSpike : BasicProjectile
{
+RIPPER
+DONTSPLASH
+DONTBLAST
Obituary "$OB_CHILLSPIKE2"
damagetype "ChillShot"
Speed 0
Radius 5
Height 10
Damage (0)
reactiontime 1
args 140
var int user_DamageKill;
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_SetAngle(random(1, 360))
TNT1 A 0 A_PlaySoundEx("weapons/mm10/chillspikeland","Weapon")
CHIL E 0 A_SpawnItemEx("CrocChillSpikeFloorFX1", WEPC_10CS_SPIKERADIUS, 0, 0, 0, 0, 0, 0, SXF_WEPFXCOLOR)
CHIL E 0 A_SpawnItemEx("CrocChillSpikeFloorFX1", WEPC_10CS_SPIKERADIUS, 0, 0, 0, 0, 0, 120, SXF_WEPFXCOLOR)
CHIL E 0 A_SpawnItemEx("CrocChillSpikeFloorFX1", WEPC_10CS_SPIKERADIUS, 0, 0, 0, 0, 0, 240, SXF_WEPFXCOLOR)
CHIL EE 1 A_Explode(27, WEPC_10CS_SPIKEEXPLODE, 0, 0, WEPC_10CS_SPIKEEXPLODE)
CHIL E 0 A_SetDamageType("ChillSpike")
Spawn2:
CHIL F 0 A_SpawnItemEx("CrocChillSpikeFloorFX2", WEPC_10CS_SPIKERADIUS, 0, 0, 0, 0, 0, 0, SXF_WEPFXCOLOR)
CHIL F 0 A_SpawnItemEx("CrocChillSpikeFloorFX2", WEPC_10CS_SPIKERADIUS, 0, 0, 0, 0, 0, 120, SXF_WEPFXCOLOR)
CHIL F 0 A_SpawnItemEx("CrocChillSpikeFloorFX2", WEPC_10CS_SPIKERADIUS, 0, 0, 0, 0, 0, 240, SXF_WEPFXCOLOR)
CHIL F 0 A_Explode(27, WEPC_10CS_SPIKEEXPLODE, 0, 0, WEPC_10CS_SPIKEEXPLODE)
CHIL F 1 A_CountDownArg(0)
loop
XDeath:
CHIL B 0
stop
Death:
CHIL B 0 A_JumpIf(reactiontime == 0, "XDeath")
CHIL F 1 A_JumpIf(true, "DeathTimeout")
wait
DeathTimeout:
CHIL B 0 A_PlaySoundEx("weapons/mm10/chillspikebreak","Weapon")
CHIL H 0 A_SpawnItemEx("CrocChillDebris", 0, 0, 0, random(6,12),random(6,12),random(6,12), 0, SXF_WEPFXCOLOR)
CHIL H 0 A_SpawnItemEx("CrocChillDebris", WEPC_10CS_SPIKERADIUS, 0, 0, random(6,12),random(6,12),random(6,12), 0, SXF_WEPFXCOLOR)
CHIL H 0 A_SpawnItemEx("CrocChillDebris", WEPC_10CS_SPIKERADIUS, 0, 0, random(6,12),random(6,12),random(6,12), 120, SXF_WEPFXCOLOR)
CHIL H 0 A_SpawnItemEx("CrocChillDebris", WEPC_10CS_SPIKERADIUS, 0, 0, random(6,12),random(6,12),random(6,12), 240, SXF_WEPFXCOLOR)
TNT1 A 15
stop
}
}

actor CrocChillSpikeFloorFX1 : BasicGraphicEffect
{
	Scale 2.5
	States
	{
	Spawn:
		CHIL E 3
		stop
	}
}

actor CrocChillSpikeFloorFX2 : CrocChillSpikeFloorFX1
{
	States
	{
	Spawn:
		CHIL F 2
		stop
	}
}

actor CrocWallSpike : CrocChillSpike
{
Radius 5
Height 10
-FLOORHUGGER
var int user_angle;
States
{
Spawn:
TNT1 A 0
TNT1 A 1
TNT1 A 0 A_SetUserVar(user_angle, random(1, 120))
TNT1 A 0 A_PlaySoundEx("weapons/mm10/chillspikeland","Weapon")
CHIL G 0 A_SpawnItemEx("CrocChillSpikeWallFX1", 3, cos(user_angle)*WEPC_10CS_SPIKERADIUS, sin(user_angle)*WEPC_10CS_SPIKERADIUS, 0, 0, 0, 0, SXF_WEPFXCOLOR)
CHIL G 0 A_SpawnItemEx("CrocChillSpikeWallFX1", 3, cos(user_angle+120)*WEPC_10CS_SPIKERADIUS, sin(user_angle+120)*WEPC_10CS_SPIKERADIUS, 0, 0, 0, 0, SXF_WEPFXCOLOR)
CHIL G 0 A_SpawnItemEx("CrocChillSpikeWallFX1", 3, cos(user_angle+240)*WEPC_10CS_SPIKERADIUS, sin(user_angle+240)*WEPC_10CS_SPIKERADIUS, 0, 0, 0, 0, SXF_WEPFXCOLOR)
CHIL GG 1 A_Explode(27, WEPC_10CS_SPIKEEXPLODE, 0, 0, WEPC_10CS_SPIKEEXPLODE)
CHIL E 0 A_SetDamageType("ChillSpike")
Spawn2:
CHIL H 0 A_SpawnItemEx("CrocChillSpikeWallFX2", 3, cos(user_angle)*WEPC_10CS_SPIKERADIUS, sin(user_angle)*WEPC_10CS_SPIKERADIUS, 0, 0, 0, 0, SXF_WEPFXCOLOR)
CHIL H 0 A_SpawnItemEx("CrocChillSpikeWallFX2", 3, cos(user_angle+120)*WEPC_10CS_SPIKERADIUS, sin(user_angle+120)*WEPC_10CS_SPIKERADIUS, 0, 0, 0, 0, SXF_WEPFXCOLOR)
CHIL H 0 A_SpawnItemEx("CrocChillSpikeWallFX2", 3, cos(user_angle+240)*WEPC_10CS_SPIKERADIUS, sin(user_angle+240)*WEPC_10CS_SPIKERADIUS, 0, 0, 0, 0, SXF_WEPFXCOLOR)
CHIL H 0 A_Explode(27, WEPC_10CS_SPIKEEXPLODE, 0, 0, WEPC_10CS_SPIKEEXPLODE)
CHIL H 1 A_CountDownArg(0)
loop
Death:
CHIL B 0 A_JumpIf(reactiontime == 0, "XDeath")
CHIL F 1 A_JumpIf(true, "DeathTimeout")
wait
DeathTimeout:
CHIL B 0 A_PlaySoundEx("weapons/mm10/chillspikebreak","Weapon")
CHIL F 0 A_SpawnItemEx("CrocChillDebris", 0, 0, 28, -random(6,12), 0, random(2,6), random(-6, 6), SXF_WEPFXCOLOR)
CHIL F 0 A_SpawnItemEx("CrocChillDebris", 0, cos(user_angle)*WEPC_10CS_SPIKERADIUS, sin(user_angle)*WEPC_10CS_SPIKERADIUS, -random(6,12), 0, random(4,8), random(-6, 6), SXF_WEPFXCOLOR)
CHIL F 0 A_SpawnItemEx("CrocChillDebris", 0, cos(user_angle+120)*WEPC_10CS_SPIKERADIUS, sin(user_angle+120)*WEPC_10CS_SPIKERADIUS, -random(6,12), 0, random(4,8), random(-6, 6), SXF_WEPFXCOLOR)
CHIL F 0 A_SpawnItemEx("CrocChillDebris", 0, cos(user_angle+240)*WEPC_10CS_SPIKERADIUS, sin(user_angle+240)*WEPC_10CS_SPIKERADIUS, -random(6,12), 0, random(4,8), random(-6, 6), SXF_WEPFXCOLOR)
DeathVanish:
TNT1 A 0 A_ChangeFlag(NOINTERACTION, true)
TNT1 A 15
stop
}
}

actor CrocChillSpikeWallFX1 : CrocChillSpikeFloorFX1
{
	States
	{
	Spawn:
		CHIL G 3
		stop
	}
}

actor CrocChillSpikeWallFX2 : CrocChillSpikeFloorFX1
{
	States
	{
	Spawn:
		CHIL H 2
		stop
	}
}

actor CrocCeilingSpike : CrocChillSpike
{
-FLOORHUGGER
+CEILINGHUGGER
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_SetAngle(random(1, 360))
TNT1 A 0 A_PlaySoundEx("weapons/mm10/chillspikeland","Weapon")
CHIL I 0 A_SpawnItemEx("CrocChillSpikeCeilingFX1", WEPC_10CS_SPIKERADIUS, 0, 0, 0, 0, 0, 0, SXF_WEPFXCOLOR)
CHIL I 0 A_SpawnItemEx("CrocChillSpikeCeilingFX1", WEPC_10CS_SPIKERADIUS, 0, 0, 0, 0, 0, 120, SXF_WEPFXCOLOR)
CHIL I 0 A_SpawnItemEx("CrocChillSpikeCeilingFX1", WEPC_10CS_SPIKERADIUS, 0, 0, 0, 0, 0, 240, SXF_WEPFXCOLOR)
CHIL II 1 A_Explode(27, WEPC_10CS_SPIKEEXPLODE, 0, 0, WEPC_10CS_SPIKEEXPLODE)
CHIL E 0 A_SetDamageType("ChillSpike")
Spawn2:
CHIL J 0 A_SpawnItemEx("CrocChillSpikeCeilingFX2", WEPC_10CS_SPIKERADIUS, 0, 0, 0, 0, 0, 0, SXF_WEPFXCOLOR)
CHIL J 0 A_SpawnItemEx("CrocChillSpikeCeilingFX2", WEPC_10CS_SPIKERADIUS, 0, 0, 0, 0, 0, 120, SXF_WEPFXCOLOR)
CHIL J 0 A_SpawnItemEx("CrocChillSpikeCeilingFX2", WEPC_10CS_SPIKERADIUS, 0, 0, 0, 0, 0, 240, SXF_WEPFXCOLOR)
CHIL J 0 A_Explode(27, WEPC_10CS_SPIKEEXPLODE, 0, 0, WEPC_10CS_SPIKEEXPLODE)
CHIL J 1 A_CountDownArg(0)
loop
}
}

actor CrocChillSpikeCeilingFX1 : CrocChillSpikeFloorFX1
{
	States
	{
	Spawn:
		CHIL I 3
		stop
	}
}

actor CrocChillSpikeCeilingFX2 : CrocChillSpikeFloorFX1
{
	States
	{
	Spawn:
		CHIL J 2
		stop
	}
}

actor CrocChillDebris : BasicGraphicEffect
{
-NOINTERACTION
-NOGRAVITY
+DOOMBOUNCE
-BOUNCEONFLOORS
+FORCEXYBILLBOARD
height 1
radius 1
States
{
Spawn:
CHID A 0
TNT1 A 1 A_Jump(256, "SpawnA", "SpawnB")
wait
SpawnLoop:
"----" A 2 A_JumpIf(momz==0, "Death")
wait
SpawnA:
CHID A 0
Goto SpawnLoop
SpawnB:
CHID B 0
Goto SpawnLoop
Death:
TNT1 A 0
stop
}
}

// Damage effects

actor CrocChillFreeze : CustomInventory
{
	+INVENTORY.AUTOACTIVATE
	States
	{
	Use:
		TNT1 A 0 A_JumpIfInventory("CrocChillFreezeTimer", 1, "End")
		TNT1 A 0 A_PlaySoundEx("weapons/mm10/chillspikeland","Body")
		TNT1 A 0 A_GiveInventory("CrocChillFreezeGravityGiver", 1)
		TNT1 A 0 A_GiveInventory("CrocChillFreezeJumpGiver", 1)
		TNT1 A 0 A_GiveInventory("CrocChillFreezeVision", 1)
		TNT1 A 0 A_Stop
		TNT1 A 0 A_SpawnItemEx("CrocChillFreezeWarper")
		TNT1 A 0 ACS_NamedExecuteAlways("core_chillspikefreeze", 0)
	End:
		TNT1 A 0 A_GiveInventory("CrocChillFreezeTimer", 1)
		TNT1 A 0
		stop
	}
}

actor CrocChillFreezeTimer : Powerup
{
	powerup.duration 45
}

actor CrocChillFreezeGravityGiver : PowerApropGiver {}
actor CrocChillFreezeGravity : PowerUp { powerup.duration 0x7FFFFFFD }

actor CrocChillFreezeJumpGiver : PowerApropGiver {}
actor CrocChillFreezeJump : PowerUp { powerup.duration 0x7FFFFFFD }

actor CrocChillFreezeVision : PowerSpeed
{
	speed 0
	powerup.color "98 F8 F0"
	powerup.duration 0x7FFFFFFD
	+POWERSPEED.NOTRAIL
}

actor CrocChillFreezeWatcher : BasicWatcher
{
	States
	{
	Spawn:
		CHIL K 1 
		CHIL K 0 A_JumpIfInTargetInventory("CrocChillFreezeTimer", 1, "Spawn")
		CHIL K 1 A_JumpIf(true, "Death")
		wait
	Death:
		TNT1 A 0 A_GiveToTarget("CrocChillUnfreeze", 1)
		stop
	}
}

actor CrocChillFreezeWarper : BasicShieldWarper
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_GiveInventory("MM8BDMProjSpawnFuncClient",1)
		TNT1 A 0 ACS_NamedExecuteAlways("core_shieldwarper_chillspike", 0, 0)
	ShieldLoop:
		CHIL K 1
		loop
	}
}

actor CrocChillGravity : CustomInventory
{
States
{
Pickup:
TNT1 A 0
TNT1 A 0 A_JumpIf(z-floorz<5, "End")
TNT1 A 0 ThrustThingZ(0, 20, 1, 1)
stop
End:
TNT1 A 0
stop
}
}

actor CrocChillUnfreeze : CrocChillFreeze
{
	States
	{
	Use:
		CHIL B 0 A_PlaySoundEx("weapons/mm10/chillspikebreak","Weapon")
		TNT1 A 0 A_TakeInventory("CrocChillFreezeVision", 1)
		TNT1 A 0 A_TakeInventory("CrocChillFreezeGravity", 1)
		TNT1 A 0 A_TakeInventory("CrocChillFreezeJump", 1)
		CHIL H 0 A_SpawnItemEx("CrocChillDebris", WEPC_10CS_SPIKERADIUS, 0, 0, random(6,12),random(6,12),random(6,12), 0)
		CHIL H 0 A_SpawnItemEx("CrocChillDebris", WEPC_10CS_SPIKERADIUS, 0, 0, random(6,12),random(6,12),random(6,12), 120)
		CHIL H 0 A_SpawnItemEx("CrocChillDebris", WEPC_10CS_SPIKERADIUS, 0, 0, random(6,12),random(6,12),random(6,12), 240)
		stop
	}
}
