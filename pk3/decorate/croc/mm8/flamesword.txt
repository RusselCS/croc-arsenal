actor CrocFlameSwordWep_NormalBar : NormalBar { args 248, 41 }

actor CrocFlameSwordWep : BaseMM8BDMWep
{
//$Category MM8BDM-Weapons/Close
//$Title Flame Sword
//$Sprite WEA4F0
Weapon.AmmoUse 2
Weapon.AmmoGive 28
Weapon.SlotNumber 4
Inventory.Pickupmessage "$PU_FLAMESWORD"
Obituary "$OB_FLAMESWORD"
Tag "$TAG_FLAMESWORD"
weapon.ammotype "CrocFlameSwordAmmo"
inventory.icon "FSWORDI"
+WEAPON.NOAUTOAIM
States
{
SpawnLoop:
WEA4 F -1
loop
Ready:
FHUD A 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_FLAMESWORD)
FHUD A 1 A_WeaponReady
Goto Ready+1
Deselect:
FHUD A 0
goto DeselectSwap
Select:
FHUD A 0
goto SelectSwap
Fire:
FHUD A 0 A_JumpIfNoAmmo("NoAmmo")
FHUD A 0 A_PlaySoundEx("weapons/mm8/flameswordfire","Weapon")
FHUD A 0 A_TakeInventory("CrocFlameSwordAmmo",2,TIF_NOTAKEINFINITE)
FHUD A 0
FHUD A 0 A_CheckFloor("GroundSwing")
Goto AirSwing
GroundSwing:
FHUD C 1 A_FireCustomMissile("CrocFlameSword",50,0,-16,0,0,-10)
FHUD D 1 A_FireCustomMissile("CrocFlameSword",25,0,-8,0,0,-5)
FHUD E 1 A_FireCustomMissile("CrocFlameSword",0,0,0,0,0,0)
FHUD F 1 A_FireCustomMissile("CrocFlameSword",-25,0,8,0,0,10)
FHUD G 1 A_FireCustomMissile("CrocFlameSword",-50,0,16,0,0,20)
Goto Finish
AirSwing:
FHUD J 1 A_FireCustomMissile("CrocFlameSword2",-50,0,16,0,0,20)
FHUD K 1 A_FireCustomMissile("CrocFlameSword2",-25,0,8,0,0,10)
FHUD L 1 A_FireCustomMissile("CrocFlameSword2",0,0,0,0,0,0)
FHUD M 1 A_FireCustomMissile("CrocFlameSword2",25,0,-8,0,0,-5)
FHUD N 1 A_FireCustomMissile("CrocFlameSword2",50,0,-16,0,0,-10)
Goto Finish
Finish:
TNT1 A 14
FHUD HI 2
FHUD A 10
FHUD A 0 A_Refire
Goto Ready+1
NoAmmo:
FHUD A 1 ACS_NamedExecuteAlways("core_noammo",0)
Goto Ready+1
}
}

actor CrocFlameSwordAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}

actor CrocFlameSword : BasicProjectile
{
+RIPPER
+BRIGHT
damage (0)
height 5
radius 5
Speed 26
states
{
Spawn:
TNT1 A 0
TNT1 AAAAA 1 A_SpawnItemEx("CrocFlameSwordTrail")
Goto Death
Death:
TNT1 A 0 
TNT1 A 0 A_SpawnItemEx("OilPitIgnite",0,0,8)
stop
}
}

actor CrocFlameSword2 : CrocFlameSword
{
States
{
Spawn:
TNT1 A 0
TNT1 AAAAA 1 A_SpawnItemEx("CrocFlameSwordTrail2")
Goto Death
}
}

actor CrocFlameSwordTrail : BasicProjectile
{
+FORCEXYBILLBOARD
+EXPLODEONDEATH
//seesound "weapons/mm8/flameswordfire"
+BRIGHT
//+RIPPER
height 8
radius 8
damage (25) 
damagetype "CrocFlameSword"
Obituary "$OB_FLAMESWORD"
States
{
Spawn:
FSWD D 0 
FSWD D 0 A_ChangeVelocity(0, -15, 0, CVF_RELATIVE | CVF_REPLACE)
FSWD DEF 1 
stop
}
}

actor CrocFlameSwordTrail2 : CrocFlameSwordTrail
{
States
{
Spawn:
FSWD D 0 
FSWD D 0 A_ChangeVelocity(0, 15, 0, CVF_RELATIVE | CVF_REPLACE)
FSWD DEF 1 
stop
}
}
