actor FlameSwordWep_NormalBar : NormalBar { args 248, 41 }

actor FlameSwordWep : BaseMM8BDMWep 11095
{
//$Category MM8BDM-Weapons/Close
//$Title Flame Sword
//$Sprite WEA4F0
Weapon.AmmoUse 2
Weapon.AmmoGive 28
Weapon.SlotNumber 4
Inventory.Pickupmessage "$PU_FLAMESWORD"
Obituary "$OB_FLAMESWORD"
Tag "$TAG_FLAMESWORD"
weapon.ammotype "FlameSwordAmmo"
inventory.icon "FSWORDI"
+WEAPON.NOAUTOAIM
States
{
SpawnLoop:
WEA4 F -1
loop
Ready:
FHUD A 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_FLAMESWORD)
FHUD A 1 A_WeaponReady
Goto Ready+1
Deselect:
FHUD A 0
goto DeselectSwap
Select:
FHUD A 0
goto SelectSwap
Fire:
FHUD A 0 A_JumpIfNoAmmo("NoAmmo")
FHUD A 0 A_PlaySoundEx("weapons/mm8/flameswordfire","Weapon")
FHUD A 0 A_TakeInventory("FlameSwordAmmo",2,TIF_NOTAKEINFINITE)
FHUD A 0
FHUD A 0 A_CheckFloor("GroundSwing")
Goto AirSwing
GroundSwing:
FHUD C 1 A_FireCustomMissile("FlameSword",50,0,-16,0,0,-10)
FHUD D 1 A_FireCustomMissile("FlameSword",25,0,-8,0,0,-5)
FHUD E 1 A_FireCustomMissile("FlameSword",0,0,0,0,0,0)
FHUD F 1 A_FireCustomMissile("FlameSword",-25,0,8,0,0,10)
FHUD G 1 A_FireCustomMissile("FlameSword",-50,0,16,0,0,20)
Goto Finish
AirSwing:
FHUD J 1 A_FireCustomMissile("FlameSword2",-50,0,16,0,0,20)
FHUD K 1 A_FireCustomMissile("FlameSword2",-25,0,8,0,0,10)
FHUD L 1 A_FireCustomMissile("FlameSword2",0,0,0,0,0,0)
FHUD M 1 A_FireCustomMissile("FlameSword2",25,0,-8,0,0,-5)
FHUD N 1 A_FireCustomMissile("FlameSword2",50,0,-16,0,0,-10)
Goto Finish
Finish:
TNT1 A 14
FHUD HI 2
FHUD A 10
FHUD A 0 A_Refire
Goto Ready+1
NoAmmo:
FHUD A 1 ACS_NamedExecuteAlways("core_noammo",0)
Goto Ready+1
}
}

actor FlameSwordAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}

actor FlameSword : BasicProjectile
{
+RIPPER
+BRIGHT
damage (0)
height 5
radius 5
Speed 26
states
{
Spawn:
TNT1 A 0
TNT1 AAAAA 1 A_SpawnItemEx("FlameSwordTrail")
Goto Death
Death:
TNT1 A 0 
TNT1 A 0 A_SpawnItemEx("OilPitIgnite",0,0,8)
stop
}
}

actor FlameSword2 : FlameSword
{
States
{
Spawn:
TNT1 A 0
TNT1 AAAAA 1 A_SpawnItemEx("FlameSwordTrail2")
Goto Death
}
}

actor FlameSwordTrail : BasicProjectile
{
+FORCEXYBILLBOARD
+EXPLODEONDEATH
//seesound "weapons/mm8/flameswordfire"
+BRIGHT
//+RIPPER
height 8
radius 8
damage (25) 
damagetype "FlameSword"
Obituary "$OB_FLAMESWORD"
States
{
Spawn:
FSWD D 0 
FSWD D 0 A_ChangeVelocity(0, -15, 0, CVF_RELATIVE | CVF_REPLACE)
FSWD DEF 1 
stop
}
}

actor FlameSwordTrail2 : FlameSwordTrail
{
States
{
Spawn:
FSWD D 0 
FSWD D 0 A_ChangeVelocity(0, 15, 0, CVF_RELATIVE | CVF_REPLACE)
FSWD DEF 1 
stop
}
}


actor FSSParkSpawner : BasicProjectile
{
scale 2.5
+RIPPER
-NOGRAVITY
+NOINTERACTION
damage (0)
radius 1
height 1
speed 0
ReactionTime 10
states
{
Spawn:
TNT1 A 0
TNT1 A 0 A_SpawnItemEx("FlameSwordSpark",Random(0,16),Random(-48, -8),8,random(50,55),random(-15,15),random(0,15),0, 0, 0)
TNT1 A 0 A_SpawnItemEx("FlameSwordSpark",Random(0,16),0,8,random(45,55),random(-15,15),random(-15,15),0, 0, 0)
TNT1 A 0 A_SpawnItemEx("FlameSwordSpark",Random(0,16),Random(8, 48),8,random(40,55),random(-15,15),random(0,15),0, 0, 0)
TNT1 A 1 A_CountDown
loop
Death:
TNT1 A 0
stop
}
}

actor FlameSwordSpark : FlameSwordTrail
{
damagetype "FlameSwordSpark"
Obituary "$OB_FLAMESWORD"
+EXPLODEONDEATH
+DONTSPLASH
damage (1)
height 2
radius 2
Scale 2.0
States
{
Spawn:
FSPK A 0
FSPK ABCD 1
stop
}
}

actor FlameSwordProtect : PowerProtection
{
Powerup.Duration 5
damagefactor "FlameSword", 0.28
}


actor FlameSwordBit : BasicGraphicEffect
{
-NOINTERACTION
-NOGRAVITY
+RIPPER
+BRIGHT
damage (0)
States
{
Spawn:
FSPK E 2
TNT1 A 2
FSPK E 2
TNT1 A 2
FSPK E 2
TNT1 A 2
FSPK E 2
TNT1 A 2
FSPK E 2
TNT1 A 2
stop
}
}
