actor CrocWaterBalloonWep_NormalBar : NormalBar { args 192, 62 }

actor CrocWaterBalloonWep : BaseMM8BDMWep 11098
{
//$Category MM8BDM-Weapons/Rapid
//$Title Water Balloon
//$Sprite WEA4I0
Weapon.AmmoUse 1
Weapon.AmmoGive 42
Weapon.SlotNumber 3
Inventory.Pickupmessage "$PU_WATERBALLOON"
Obituary "$OB_WATERBALLOON"
Tag "$TAG_WATERBALLOON"
weapon.ammotype "CrocWaterBalloonAmmo"
inventory.icon "WATERBI"
States
{
SpawnLoop:
WEA4 I -1
loop
Ready:
WTRB H 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_WATERBALLOON)
WTRB H 1 A_WeaponReady
Goto Ready+1
Deselect:
WTRB H 0
goto DeselectSwap
Select:
WTRB H 0
goto SelectSwap
Fire:
WTRB H 0 A_JumpIfNoAmmo("NoAmmo")
WTRB H 0 A_PlaySoundEx("weapons/mm8/waterballoonfire","Weapon")
WTRB H 0 A_FireCustomMissile("CrocWaterBalloon",random(-1,1),1,8,0)
WTRB IJ 3
WTRB J 1
WTRB H 1 A_Refire
Goto Ready+1
NoAmmo:
WTRB H 1 ACS_NamedExecuteAlways("core_noammo",0)
Goto Ready+1
}
}

actor CrocWaterBalloonAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 42
+INVENTORY.IGNORESKILL
}

actor CrocWaterBalloon : BasicProjectile
{
-NOGRAVITY
+FORCEXYBILLBOARD
+DONTREFLECT
height 6
radius 9
damage (5)
speed 45
damagetype "WaterBalloon"
Obituary "$OB_WATERBALLOON"
States
{
Spawn:
WTRB B 0
WTRB B 0 ThrustThingZ(0, 1, 0, 1)
WTRB BCD 2
loop
Death:
WTRB K 0 A_SpawnItemEx("SoakifyEffect")
WTRB K 0 A_ChangeFlag("NOGRAVITY", 1)
WTRB K 0 A_Explode(8,64,0)
WTRB K 0 A_JumpIf(ceilingz-z<16,"DeathUp")
WTRB KLMN 2
stop
DeathUp:
WTRB O 0
WTRB OPQR 2
stop
}
}
