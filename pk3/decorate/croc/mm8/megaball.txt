actor CrocMegaBallWep_NormalBar : NormalBar { args 197, 239 }

actor CrocMegaBallWep : BaseMM8BDMWep 11093
{
//$Category MM8BDM-Weapons/Ranged
//$Title Mega Ball
//$Sprite WEA4D0
Weapon.AmmoUse 2
Weapon.AmmoGive 28
Weapon.SlotNumber 2
Inventory.Pickupmessage "$PU_MEGABALL"
Obituary "$OB_MEGABALL"
Tag "$TAG_MEGABALL"
weapon.ammotype "CrocMegaBallAmmo"
inventory.icon "MEGABALI"
States
{
SpawnLoop:
WEA4 D -1
loop
Ready:
MBAL N 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_MEGABALL)
Ready0:
MBAL N 0 A_TakeInventory("CrocBallKickCounter", 999)
Ready1:
MBAL N 0 A_JumpIfInventory("CrocKickedBall",1,"KickConfirm")
MBAL N 1 A_WeaponReady
loop
Deselect:
MBAL N 0 A_TakeInventory("CrocBallFlag", 999)
goto DeselectSwap
Select:
MBAL N 0
goto SelectSwap
Fire:
MBAL N 0 A_JumpIfNoAmmo("NoAmmo")
MBAL N 0 A_JumpIfInventory("CrocBallFlag",1,"NewBall")
MBAL N 0 A_GiveInventory("CrocBallFlag",100)
MBAL N 0 A_PlaySoundEx("weapons/mm8/megaballfire","Weapon")
MBAL N 0 A_TakeInventory("CrocMegaBallAmmo",2,TIF_NOTAKEINFINITE)
MBAL N 0 A_SpawnItemEx("CrocMegaBallStart",0,8,32,1.0,0,0,0,SXF_MULTIPLYSPEED)
MBAL NOP 2
MBAL N 4
MBAL N 1 A_Refire("AutoKick")
MBAL NNNNN 1 A_JumpIfInventory("CrocKickedBall",1,"CrocMegaBallKick")
CheckForKick:
MBAL N 0 A_GiveInventory("CrocBallKickCounter", 1)
MBAL N 1 A_JumpIfInventory("CrocKickedBall",1,"CrocMegaBallKick")
MBAL N 0 A_JumpIfInventory("CrocBallKickCounter", 10, "Ready0")
loop
AutoKick:
MBAL N 0 A_GiveInventory("CrocBallKickCounter", 1)
MBAL N 0 A_JumpIfInventory("CrocKickedBall",1,"CrocMegaBallKick")
MBAL N 0 A_JumpIfInventory("CrocBallKickCounter", 5, "CrocMegaBallKick")
MBAL N 1 A_Refire("AutoKick")
Goto Ready0
Altfire:
MBAL N 0 A_JumpIfNoAmmo("NoAmmo")
MBAL N 0 A_JumpIfInventory("CrocBallFlag",1,"AltNewBall")
MBAL N 0 A_GiveInventory("CrocBallFlag",100)
MBAL N 0 A_PlaySoundEx("weapons/mm8/megaballfire","Weapon")
MBAL N 0 A_TakeInventory("CrocMegaBallAmmo", 2, TIF_NOTAKEINFINITE)
MBAL N 0 A_SpawnItemEx("CrocMegaBallDroppedStart",0,8,32,1.0,0,0,0,SXF_MULTIPLYSPEED)
MBAL NOP 2
MBAL NNNNNNNNNNNNNNN 1 A_JumpIfInventory("CrocKickedBall",1,"CrocMegaBallKick")
Goto Ready0
CrocMegaBallKick:
MBAL N 1 A_JumpIfInventory("CrocBallFlag", 1, "KickConfirm")
Goto Ready0
KickConfirm:
MBAL Q 0 A_TakeInventory("CrocBallFlag",999)
MBAL Q 0 A_TakeInventory("CrocKickedBall",1)
MBAL Q 0 A_TakeInventory("CrocBallKickCounter", 9999)
MBAL Q 0 A_PlaySoundEx("weapons/mm8/megaballkick", "weapon")
MBAL Q 0 A_FireCustomMissile("CrocMegaBallKicked",0,0,0,0)
MBAL Q 2
MBAL R 5
MBAL Q 4
MBAL N 18
MBAL Q 0 A_TakeInventory("CrocBallFlag",999)
MBAL Q 0 A_TakeInventory("CrocKickedBall",1)
MBAL Q 0 A_TakeInventory("CrocBallKickCounter", 9999)
Goto Ready1
AltNewBall:
MBAL N 0
MBAL N 0 A_TakeInventory("CrocBallFlag",100)
MBAL NN 2
Goto Altfire+1
NewBall:
MBAL N 0
MBAL N 0 A_TakeInventory("CrocBallFlag",100)
MBAL NN 2
Goto Fire+1
NoAmmo:
MBAL N 1 ACS_NamedExecuteAlways("core_noammo",0)
Goto Ready1
}
}

actor CrocBallKickCounter : Inventory 
{
inventory.maxamount 9999
}

actor CrocMegaBallAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}

Actor CrocKickedBall : Once {}

Actor CrocBallFlag : Inventory
{
Inventory.Amount 1
Inventory.maxamount 999
}

actor CrocMegaBallStart : FastProjectile
{
+MISSILE
damagetype "MegaBall"
Obituary "$OB_MEGABALL"
+DONTREFLECT
+STEPMISSILE
maxstepheight 32
damage (0)
radius 12
height 12
speed 48
scale 2.5
reactiontime 1
States
{
Spawn:
TNT1 A 0
TNT1 A 1 A_CountDown
wait
Death:
TNT1 A 0 A_SpawnItemEx("CrocMegaBall",0,0,0,0,0,-1,0,SXF_ABSOLUTEMOMENTUM)
TNT1 A 2
stop
}
}

actor CrocMegaBallDroppedStart : CrocMegaBallStart
{
States
{
Death:
TNT1 A 0 A_SpawnItemEx("CrocMegaBallDropped",0,0,0,0,0,-1,0,SXF_ABSOLUTEMOMENTUM)
TNT1 A 2
stop
}
}

actor CrocMegaBall : BasicBouncer
{
damagetype "MegaBall"
Obituary "$OB_MEGABALL"
-NOGRAVITY
+NOBLOOD
+DONTSPLASH
//+FIXMAPTHINGPOS
+FORCEXYBILLBOARD
+DONTREFLECT

//+DOOMBOUNCE
+NOEXPLODEFLOOR
+USEBOUNCESTATE

BounceFactor 0.5
WallBounceFactor 0.65
BounceCount 25
reactiontime 1
damage (27)
radius 12
height 12
speed 0
var int user_TranslateWhiteColors;
States
{
Spawn:
MBAL B 0
MBAL B 0 ACS_NamedExecuteWithResult("core_copymomentum")
Spawn0:
MBAL B 0 A_Jump(256,1,5,9,13,17,21,25)
Spawn1:
MBAL HH 1 A_JumpIfCloser(24,"KickMe")
MBAL H 0 A_JumpIfInTargetInventory("CrocBallFlag",1,2)
MBAL H 1 A_CountDown
wait
MBAL GG 1 A_JumpIfCloser(24,"KickMe")
MBAL G 0 A_JumpIfInTargetInventory("CrocBallFlag",1,2)
MBAL G 1 A_CountDown
wait
MBAL FF 1 A_JumpIfCloser(24,"KickMe")
MBAL F 0 A_JumpIfInTargetInventory("CrocBallFlag",1,2)
MBAL F 1 A_CountDown
wait
MBAL EE 1 A_JumpIfCloser(24,"KickMe")
MBAL E 0 A_JumpIfInTargetInventory("CrocBallFlag",1,2)
MBAL E 1 A_CountDown
wait
MBAL DD 1 A_JumpIfCloser(24,"KickMe")
MBAL D 0 A_JumpIfInTargetInventory("CrocBallFlag",1,2)
MBAL D 1 A_CountDown
wait
MBAL CC 1 A_JumpIfCloser(24,"KickMe")
MBAL C 0 A_JumpIfInTargetInventory("CrocBallFlag",1,2)
MBAL C 1 A_CountDown
wait
MBAL BB 1 A_JumpIfCloser(24,"KickMe")
MBAL B 0 A_JumpIfInTargetInventory("CrocBallFlag",1,"Spawn1")
MBAL B 1 A_CountDown
wait

Bounce.Floor:
MBAL "#" 0 A_JumpIf(sqrt(momx*momx+momy*momy+momz*momz)<=1,"Spawn3Start")
goto Spawn0

Spawn3Start:
MBAL "#" 0 A_ChangeFlag("USEBOUNCESTATE",0)
MBAL "#" 0 A_ChangeFlag("BOUNCEONFLOORS",0)
MBAL "#" 0 A_ChangeVelocity(0,0,0,2)
MBAL "#" 0 ThrustThing(angle*256/360,0)//Sync
MBAL "#" 0 ThrustThingZ(0,0,0,1)
MBAL "##" 1 A_JumpIfCloser(24,"KickMe")
MBAL "#" 0 A_ChangeFlag("BOUNCEONFLOORS",1)
Spawn3:
MBAL "#" 0
MBAL "##" 1 A_JumpIfCloser(24,"KickMe")
MBAL "#" 0 A_TakeFromTarget("CrocBallFlag",1)
MBAL "##" 1 A_JumpIfInTargetInventory("CrocBallFlag",1,"Spawn3")
MBAL "#" 1 A_CountDown
wait

KickMe:
MBAL "#" 0 A_ChangeFlag("USEBOUNCESTATE",0)
MBAL "#" 1 A_GiveToTarget("CrocBallAction",1)
MBAL "#" 1 A_CountDown
wait
Death:
MBAL "#" 0 A_ChangeFlag("NOGRAVITY",1)
MBAL "#" 0 A_Stop
MBAL "#" 0 A_TakeFromTarget("CrocBallFlag",999)
MBAL "#" 2
TNT1 A 0 A_SpawnItemEx("ExplosionEffect1", 0, 0, 12.5, 0, 0, 0, 0, SXF_WEPFXCOLOR)
stop
}
}

actor CrocMegaBallDropped : MegaBall
{
States
{
Spawn:
MBAL B 0
MBAL B 0 ThrustThingZ(0,1,0,1)//Sync
MBAL B 0 ThrustThing(angle*256/360,0)
Spawn0:
MBAL B 0 A_Jump(256,1,5,9,13,17,21,25)
Spawn1:
MBAL HH 1 //A_JumpIfCloser(24,"KickMe")
MBAL H 0 A_JumpIfInTargetInventory("CrocBallFlag",1,2)
MBAL H 1 A_CountDown
wait
MBAL GG 1 //A_JumpIfCloser(24,"KickMe")
MBAL G 0 A_JumpIfInTargetInventory("CrocBallFlag",1,2)
MBAL G 1 A_CountDown
wait
MBAL FF 1 //A_JumpIfCloser(24,"KickMe")
MBAL F 0 A_JumpIfInTargetInventory("CrocBallFlag",1,2)
MBAL F 1 A_CountDown
wait
MBAL EE 1 //A_JumpIfCloser(24,"KickMe")
MBAL E 0 A_JumpIfInTargetInventory("CrocBallFlag",1,2)
MBAL E 1 A_CountDown
wait
MBAL DD 1 //A_JumpIfCloser(24,"KickMe")
MBAL D 0 A_JumpIfInTargetInventory("CrocBallFlag",1,2)
MBAL D 1 A_CountDown
wait
MBAL CC 1 //A_JumpIfCloser(24,"KickMe")
MBAL C 0 A_JumpIfInTargetInventory("CrocBallFlag",1,2)
MBAL C 1 A_CountDown
wait
MBAL BB 1 //A_JumpIfCloser(24,"KickMe")
MBAL B 0 A_JumpIfInTargetInventory("CrocBallFlag",1,"Spawn1")
MBAL B 1 A_CountDown
wait
}
}

actor CrocMegaBallKicked : BasicBouncer
{
bouncesound "weapons/mm8/megaballbounce"
damagetype "MegaBall"
Obituary "$OB_MEGABALL"
-NOGRAVITY
+FORCEXYBILLBOARD

+ALLOWBOUNCEONACTORS
+BOUNCEONACTORS
+USEBOUNCESTATE
bouncecount 8
bouncefactor 0.9
wallbouncefactor 0.9

Gravity 0.50
damage(27)
radius 16
height 16
speed 40
reactiontime 25
var int user_TranslateWhiteColors;
States
{
Spawn:
MBAL B 2
MBAL B 0 ThrustThingZ(0,20,0,1)
MBAL B 0 A_Countdown
MBAL B 0 A_SpawnItemEx("CrocMegaBallFX1", -0.00*momx, -0.00*momy, -0.00*momz+0.000, 0, 0, 0, 0, SXF_ABSOLUTEPOSITION|SXF_WEPFXCOLOR)
MBAL B 1 A_SpawnItemEx("CrocMegaBallFX1", -0.50*momx, -0.50*momy, -0.50*momz+0.250, 0, 0, 0, 0, SXF_ABSOLUTEPOSITION|SXF_WEPFXCOLOR)
MBAL C 0 A_SpawnItemEx("CrocMegaBallFX1", -0.00*momx, -0.00*momy, -0.00*momz+0.000, 0, 0, 0, 0, SXF_ABSOLUTEPOSITION|SXF_WEPFXCOLOR)
MBAL C 1 A_SpawnItemEx("CrocMegaBallFX1", -0.50*momx, -0.50*momy, -0.50*momz+0.250, 0, 0, 0, 0, SXF_ABSOLUTEPOSITION|SXF_WEPFXCOLOR)
MBAL D 0 A_SpawnItemEx("CrocMegaBallFX1", -0.00*momx, -0.00*momy, -0.00*momz+0.000, 0, 0, 0, 0, SXF_ABSOLUTEPOSITION|SXF_WEPFXCOLOR)
MBAL D 1 A_SpawnItemEx("CrocMegaBallFX1", -0.50*momx, -0.50*momy, -0.50*momz+0.250, 0, 0, 0, 0, SXF_ABSOLUTEPOSITION|SXF_WEPFXCOLOR)
MBAL E 0 A_SpawnItemEx("CrocMegaBallFX1", -0.00*momx, -0.00*momy, -0.00*momz+0.000, 0, 0, 0, 0, SXF_ABSOLUTEPOSITION|SXF_WEPFXCOLOR)
MBAL E 1 A_SpawnItemEx("CrocMegaBallFX1", -0.50*momx, -0.50*momy, -0.50*momz+0.250, 0, 0, 0, 0, SXF_ABSOLUTEPOSITION|SXF_WEPFXCOLOR)
MBAL F 0 A_SpawnItemEx("CrocMegaBallFX1", -0.00*momx, -0.00*momy, -0.00*momz+0.000, 0, 0, 0, 0, SXF_ABSOLUTEPOSITION|SXF_WEPFXCOLOR)
MBAL F 1 A_SpawnItemEx("CrocMegaBallFX1", -0.50*momx, -0.50*momy, -0.50*momz+0.250, 0, 0, 0, 0, SXF_ABSOLUTEPOSITION|SXF_WEPFXCOLOR)
MBAL G 0 A_SpawnItemEx("CrocMegaBallFX1", -0.00*momx, -0.00*momy, -0.00*momz+0.000, 0, 0, 0, 0, SXF_ABSOLUTEPOSITION|SXF_WEPFXCOLOR)
MBAL G 1 A_SpawnItemEx("CrocMegaBallFX1", -0.50*momx, -0.50*momy, -0.50*momz+0.250, 0, 0, 0, 0, SXF_ABSOLUTEPOSITION|SXF_WEPFXCOLOR)
MBAL H 0 A_SpawnItemEx("CrocMegaBallFX1", -0.00*momx, -0.00*momy, -0.00*momz+0.000, 0, 0, 0, 0, SXF_ABSOLUTEPOSITION|SXF_WEPFXCOLOR)
MBAL H 1 A_SpawnItemEx("CrocMegaBallFX1", -0.50*momx, -0.50*momy, -0.50*momz+0.250, 0, 0, 0, 0, SXF_ABSOLUTEPOSITION|SXF_WEPFXCOLOR)
MBAL I 0 A_SpawnItemEx("CrocMegaBallFX1", -0.00*momx, -0.00*momy, -0.00*momz+0.000, 0, 0, 0, 0, SXF_ABSOLUTEPOSITION|SXF_WEPFXCOLOR)
MBAL I 1 A_SpawnItemEx("CrocMegaBallFX1", -0.50*momx, -0.50*momy, -0.50*momz+0.250, 0, 0, 0, 0, SXF_ABSOLUTEPOSITION|SXF_WEPFXCOLOR)
Goto Spawn+2
Bounce.Actor:
MBAL B 0 A_Countdown
MBAL B 0 A_ChangeFlag("THRUACTORS", true)
MBAL B 1 A_ChangeVelocity(40.0,0,velz,CVF_RELATIVE|CVF_REPLACE)
MBAL CDEF 1
MBAL B 0 A_ChangeFlag("THRUACTORS", false)
Goto Spawn+7
Death:
TNT1 A 0 A_SpawnItemEx("ExplosionEffect1", 0, 0, 12.5)
stop
}
}

actor CrocSpawnMegaBallFX : CustomInventory
{
    States
    {
    Pickup:
        TNT1 A 0 // gravity: 0.5
        TNT1 A 0 A_SpawnItemEx("CrocMegaBallFX1", -0.00*momx, -0.00*momy, -0.00*momz+0.000, 0, 0, 0, 0, SXF_ABSOLUTEPOSITION)
        TNT1 A 0 A_SpawnItemEx("CrocMegaBallFX1", -0.50*momx, -0.50*momy, -0.50*momz+0.250, 0, 0, 0, 0, SXF_ABSOLUTEPOSITION) // 1/2 step
        stop
    }
}

actor CrocMegaBallFX1 : BasicGraphicEffect
{
RenderStyle "translucent"
Alpha 0.7
Speed 60
+BRIGHT
+FORCEXYBILLBOARD
States
{
SpawnFrame:
TNT1 A 2
MBAL JKLM 3
stop
}
}

actor CrocBallAction : CustomInventory
{
States
{
Pickup:
TNT1 A 0
TNT1 A 0 A_JumpIf(z-floorz==0, "KickTheBall")
TNT1 A 0 A_PlaySoundEx("weapons/mm8/megaballbounce","Voice")
TNT1 A 1 ThrustThingZ(0,60,0,0)
TNT1 A 1 A_TakeInventory("JumpCancler",1)
stop
KickTheBall:
TNT1 A 0
TNT1 A 0 A_GiveInventory("CrocKickedBall",1)
stop
}
}
