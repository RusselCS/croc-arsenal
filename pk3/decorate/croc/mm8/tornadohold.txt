actor CrocTornadoHoldWep_NormalBar : NormalBar { args 236, 194 }
actor CrocTornadoHoldWep_ScriptBar : ScriptBar {}

actor CrocTornadoHoldWep : BaseMM8BDMWep 11092
{
//$Category MM8BDM-Weapons/Close
//$Title Tornado Hold
//$Sprite WEA4C0
Weapon.AmmoUse 4
Weapon.AmmoGive 28
Weapon.SlotNumber 4
Inventory.Pickupmessage "$PU_TORNADOHOLD"
Obituary "$OB_TORNADOHOLD"
Tag "$TAG_TORNADOHOLD"
weapon.ammotype "CrocTornadoHoldAmmo"
inventory.icon "TORNAHI"
States
{
SpawnLoop:
WEA4 C -1
loop
Ready:
THOL I 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_TORNADOHOLD)
THOL I 0 A_JumpIfInventory("CrocTornadoHoldCooldownFlag",1,"ReadyCooldown")
THOL I 1 A_WeaponReady
Goto Ready+1
ReadyCooldown:
THOL I 1 A_WeaponReady(WRF_NOFIRE)
Goto Ready+1
Deselect:
THOL I 0
goto DeselectSwap
Select:
THOL I 0
goto SelectSwap
Fire:
THOL I 0 A_JumpIfNoAmmo("NoAmmo")
THOL I 0 A_PlaySoundEx("weapons/mm8/tornadoholdfire","Weapon")
THOL I 0 A_FireCustomMissile("CrocTornadoHoldPod",0,1,8,0)
THOL KJ 3
//THOL IIIIIIIIIIIIIIIIIIIIIIIIIIIIIII 1 A_WeaponReady(WRF_NOFIRE)
//THOL I 0 A_Refire
THOL I 0 A_GiveInventory("CrocTornadoHoldCooldownFlag",1)
THOL I 0 A_SpawnItemEx("CrocTornadoHoldCooldowner")
Goto Ready+1
NoAmmo:
THOL I 1 ACS_NamedExecuteAlways("core_noammo",0)
Goto Ready+1
}
}

actor CrocTornadoHoldCooldownFlag : Once {}

actor CrocTornadoHoldCooldowner : BasicCooldowner
{
reactiontime 31
States
{
Spawn:
TNT1 A 0
Spawn2:
TNT1 A 1 A_GiveInventory("CrocTornadoHoldCooldownerFunc",1)
loop
Death:
TNT1 A 0 A_TakeFromTarget("CrocTornadoHoldCooldownFlag",1)
stop
}
}

actor CrocTornadoHoldCooldownerFunc : BasicCooldownerFunc
{
States
{
Pickup:
TNT1 A 0 A_JumpIfInTargetInventory("CrocTornadoHoldCooldownFlag",1,"PickupBasic")
goto PickupRemove
}
}

actor CrocTornadoHoldRefresh : Powerup
{
    powerup.duration 5
}

actor CrocTornadoHoldAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}

actor CrocTornadoHoldPod : BasicBouncer
{
+DONTREFLECT
-NOGRAVITY
+EXPLODEONWATER
Bouncefactor 0.01
WallBounceFactor 0.01
Damage (13)
Obituary "$OB_TORNADOHOLD"
damagetype "TornadoHoldPod"
Height 5
Radius 5
speed 33
States
{
Spawn:
TORH B 0
TORH BBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBB 1 A_CheckFloor("Death")
TORH BBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBBB 1 A_CheckFloor("Death")
TORH B 0 A_ChangeFlag(NOGRAVITY, true)
ForceFall:
TORH B 0 ThrustThingZ(0, 5, 1, 1)
TORH B 1 A_CheckFloor("Death")
loop
Death:
TORH B 0 
TORH B 0 A_Stop
TORH B 0 A_SpawnItemEx("CrocTornadoHold",0,0,0)
stop
XDeath:
TORH B 0 A_SpawnItemEx("CrocTornadoHold",0,0,-(z-floorz))
stop
}
}

actor CrocTornadoHold : BasicProjectile
{
+NOEXPLODEFLOOR
+THRUACTORS
+DONTBLAST
Scale 2.5
Height 2
Radius 5
Speed 0
Damage (0)
reactiontime 30
States
{
Spawn:
TORH B 0
TORH B 1 A_CheckFloor("Change")
TORH B 0 A_CountDown
Loop
Change:
TORH B 0 A_ChangeFlag(FLOORHUGGER, true)
TORH BC 1
TORH D 0 A_PlaySoundEx("weapons/mm8/tornadoholdblow","Weapon")
SpawnLoop:
TORH D 0 A_SpawnItemEx("CrocHoldingTornado",0,0,15,0,0,0,0,SXF_NOCHECKPOSITION)
TORH DE 2 A_JumpIfInTargetInventory("CrocTornadoHoldRefresh", 1, "Death")
TORH D 0 A_CountDown
loop
Death:
TORH D 0 A_SpawnItemEx("ExplosionEffect1", 0, 0, 0, 0, 0, 0, 0, SXF_WEPFXCOLOR)
stop
}
}

actor CrocHoldingTornado : BasicProjectile
{
damage (13) 
+DONTREFLECT
+RIPPER
damagetype "TornadoHold"
Obituary "$OB_TORNADOHOLD"
height 6
radius 5
Scale 2.5
Speed 1
reactiontime 33
renderstyle "Translucent"
States
{
Spawn:
TORH F 0
TORH F 0 ACS_NamedExecuteWithResult("core_alphaprojectile", CallACS("core_getptrtid", AAPTR_TARGET), APROJ_TEAM, 16384, 192) // 16384 = 0.25 * 65536
TORH F 0 ThrustThingZ(0,40,0,0)
SpawnLoop:
TORH FFFGGGHHH 1 A_GiveInventory("CrocTornadoHoldFunc", 1)
goto SpawnLoop
Death:
TORH G 1 A_SpawnItemEx("CrocTornadoHoldEnd",0,0,0,0,0,0,0,SXF_WEPFXCOLOR)
stop
}
}

actor CrocTornadoHoldFunc : CustomInventory
{
    States
    {
    Pickup:
        TNT1 A 0 A_CountDown
        TNT1 A 0 A_Explode(16, 32, 0, 0, 32)
        TNT1 A 0 A_RadiusGive("CrocTornadoThruster", 30, RGF_PLAYERS)
        stop
    }
}

actor CrocTornadoHoldProtect : PowerProtection
{
    powerup.duration 7
    damagefactor "CrocTornadoHold", 0.0
}

actor CrocTornadoThrusterLOS
{
+MISSILE
+NOINTERACTION
radius 16
height 4
-SOLID
states
{
Spawn:
TNT1 A 0
PLAY A 2 A_JumpIFInTargetLOS("Lift", 360)
stop
Lift:
TNT1 A 0 A_GiveToTarget("CrocTornadoThruster",1)
stop
}
}

actor CrocTornadoHoldEnd : BasicGraphicEffect
{
Speed 40
States
{
SpawnFrame:
TORH HGFHGF 3
stop
}
}

actor CrocTornadoLifter : CrocHoldingTornado
{
+RIPPER
DAMAGE (0)
States
{
Death:
TORH H 0 
stop
XDeath:
TORH H 0 
stop
}
}

actor CrocTornadoThruster : CustomInventory
{
+INVENTORY.AUTOACTIVATE
States
{
Pickup:
TNT1 A 1 ThrustThingZ(0,40,0,0)
stop
}
}
