actor IceWaveWep_NormalBar : NormalBar { args 229, 205 }

actor IceWaveWep : BaseMM8BDMWep 11090
{
//$Category MM8BDM-Weapons/Ranged
//$Title Ice Wave
//$Sprite WEA4A0
Weapon.AmmoUse 2
Weapon.AmmoGive 28
Weapon.SlotNumber 2
Inventory.Pickupmessage "$PU_ICEWAVE"
Obituary "$OB_ICEWAVE"
Tag "$TAG_ICEWAVE"
weapon.ammotype "IceWaveAmmo"
inventory.icon "ICEWAVI"
States
{
SpawnLoop:
WEA4 A -1
loop
Ready:
ICEW I 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_ICEWAVE)
ICEW I 1 A_WeaponReady
Goto Ready+1
Deselect:
ICEW I 0
goto DeselectSwap
Select:
ICEW I 0
goto SelectSwap
Fire:
ICEW I 0 A_JumpIfNoAmmo("NoAmmo")
ICEW I 0 A_PlaySoundEx("weapons/mm8/icewavefire","Weapon")
ICEW I 0 A_FireCustomMissile("IceWaveDropper",0,1,8,0)
ICEW JK 5
ICEW I 24
ICEW I 0 A_Refire
Goto Ready+1
NoAmmo:
ICEW I 1 ACS_NamedExecuteAlways("core_noammo",0)
Goto Ready+1
}
}

actor IceWaveAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}


actor IceWaveDropper
{
PROJECTILE
+NOINTERACTION
height 1
radius 1
Speed 0
Scale 3.0
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_SpawnItemEx("IceWaveSpawner",32,0,0)
stop
}
}

actor IceWaveSpawner : BasicProjectile
{
-NOGRAVITY
Damagetype "IceWave"
Obituary "$OB_ICEWAVE"
Damage(20)
Scale 3.0
ReactionTime 35
States
{
Spawn:
ICEW B 0
ICEW A 1 ThrustThingZ(0, 50, 1, 0)
ICEW A 2 A_CountDown
Goto Spawn+2
Crash:
Death:
TNT1 A 0 A_PlaySoundEx("weapons/mm8/icewavestart", "Body")
TNT1 A 0 A_CustomMissile("IceWave",0,0,0,2,0)
stop
}
}

actor IceWave : BasicProjectile
{
+RIPPER
+DONTREFLECT
Damagetype "IceWave"
Obituary "$OB_ICEWAVE"
Damage (0)
Speed 20
Height 48
Radius 10
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_PlaySoundEx("weapons/mm8/icewavebreak", "Body", true)
TNT1 A 2 A_SpawnItemEx("IceWaveIcicleFloor")
TNT1 A 0 A_CheckFloor("MakeIcicle")
TNT1 AA 1 A_JumpIf(z-floorz!=0,"Down")
Goto Spawn+3
MakeIcicle:
TNT1 A 0 A_SpawnItemEx("IceWaveIcicleFloor")
Goto Spawn+4
Down: 
TNT1 A 0 A_StopSoundEx("Body")
TNT1 A 1 A_SpawnItemEx("IceWaveDown",0,0,0,0,0,-20)
stop
Death:
TNT1 A 0 A_StopSoundEx("Body")
TNT1 A 1 A_SpawnItemEx("IceWaveUp",0,0,0,0,0,20)//TNT1 A 0 A_CustomMissile("IceWave",0,0,0,2,0)
stop
}
}

actor IceWaveDown : IceWave
{
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_PlaySoundEx("weapons/mm8/icewavebreak", "Body", true)
TNT1 A 2 A_SpawnItemEx("IceWaveIcicleWall")
Goto Spawn+1
Death:
TNT1 A 0 A_StopSoundEx("Body")
TNT1 A 1 A_CustomMissile("IceWave",0,0,0,2,0)
stop
}
}

actor IceWaveUp : IceWave
{
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_PlaySoundEx("weapons/mm8/icewavebreak", "Body", true)
TNT1 A 2
TNT1 A 0 A_Stop
TNT1 A 0 A_SpawnItemEx("IceWaveIcicleWall",0,0,0,0,0,0,180)
TNT1 A 0 A_ChangeVelocity(20,0,0, CVF_REPLACE | CVF_RELATIVE) 
TNT1 A 1
TNT1 A 0 A_CustomMissile("IceWave",0,0,0,2,0)//A_SpawnItemEx("SearchSnake",0,0,0,18)
stop
Death:
TNT1 A 0 A_JumpIf(ceilingz-z > 48, "Continue")
TNT1 A 0 
stop
Continue:
TNT1 A 0 A_StopSoundEx("Body")
TNT1 A 1 A_SpawnItemEx("IceWaveUp",0,0,4,0,0,20)
stop
}
}

actor IceWaveIcicle : IceWave
{
+DONTBLAST
-RIPPER
Damage (22)
Scale 2.5
States
{
Spawn:
ICEW B 0
ICEW B 0 A_SpawnItemEx("IcePitFreeze")
ICEW B 0 A_Jump(80, "Icicle2")
ICEW B 0 A_Jump(64, "Icicle4")
ICEW BCDEE 3
Goto Death
Icicle2:
ICEW L 0
ICEW L 0 A_Jump(80, "Icicle3")
ICEW LMMNN 3
Goto Death
Icicle3:
ICEW LMNOO 3
Goto Death
Icicle4:
ICEW BCCDD 3
Goto Death
Death: 
ICEW B 0 A_SpawnItemEx("IceWaveBreak",0,0,0,0,0,0,0, SXF_WEPFXCOLOR)
stop
}
}

actor IceWaveIcicleWall : IceWaveIcicle
{
States
{
Spawn:
ICWV A 0
ICEW A 0 A_PlaySoundEx("weapons/mm8/icewavebreak", "Body")
ICEW B 0 A_SpawnItemEx("IcePitFreeze")
ICWV A 0 A_JumpIf(z-floorz <= 8,"SpawnFloor")
ICWV A 0 A_Jump(80, "Icicle2")
ICWV A 0 A_Jump(64, "Icicle4")
ICWV ABCDD 3
Goto Death
Icicle2:
ICWV E 0
ICWV E 0 A_Jump(80, "Icicle3")
ICWV EFFGG 3
Goto Death
Icicle3:
ICWV EFGHH 3
Goto Death
Icicle4:
ICWV ABBCC 3
Goto Death
SpawnFloor:
ICWV A 0 A_SpawnItemEx("IceWaveIcicleFloor",0,0,0,0,0,0,0)
stop
Death: 
ICWV A 0 A_SpawnItemEx("IceWaveBreak",0,0,0,0,0,0,0, SXF_WEPFXCOLOR)
stop
}
}

actor IceWaveIcicleFloor : IceWaveIcicle
{
+FLOORHUGGER
}

actor IceWaveBreak : BasicGraphicEffect
{
+RANDOMIZE
States
{
Spawn:
ICEW FGH 2 
stop
}
}