actor MetalBladeWep_NormalBar : NormalBar { args 210, 164 }

actor MetalBladeWep : BaseMM8BDMWep 10048
{
//$Category MM8BDM-Weapons/Rapid
//$Title Metal Blade
//$Sprite WEAPS0
Weapon.AmmoUse 1
Weapon.AmmoGive 28
Weapon.SlotNumber 3
Inventory.Pickupmessage "$PU_METALBLADE"
Obituary "$OB_METALBLADE"
Tag "$TAG_METALBLADE"
weapon.ammotype "MetalBladeAmmo"
inventory.icon "METLSI"
States
{
SpawnLoop:
WEAP S -1
loop
Ready:
BUSH J 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_METALBLADE)
BUSH J 0 A_JumpIfNoAmmo("ReadyNoAmmo")
BUSH J 1 A_WeaponReady
Goto Ready+2
ReadyNoAmmo:
BUS2 C 1 A_WeaponReady
BUS2 C 0 A_JumpIfNoAmmo("ReadyNoAmmo")
Goto Ready+1
Deselect:
BUSH J 0
goto DeselectSwap
Select:
BUSH J 0
goto SelectSwap
Fire:
BUSH J 0 A_JumpIfNoAmmo("NoAmmo")
BUSH J 0 A_PlaySoundEx("weapons/mm2/metalbladefire","Weapon")
BUSH J 0 A_FireCustomMissile("MetalBlade",0,1,8,0)
BUSH KLMNO 2
BUSH Q 0 A_JumpIfNoAmmo("NoAmmoDelay")
BUSH QQR 1 A_Refire
Goto Ready+1
NoAmmoDelay:
BUS2 GGH 1
Goto ReadyNoAmmo
NoAmmo:
BUS2 C 1 ACS_NamedExecuteAlways("core_noammo",0)
Goto ReadyNoAmmo
}
}

actor MetalBladeAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}

actor MetalBlade : BasicProjectile
{
+RIPPER
Speed 40
Radius 3
Height 4
Damage (16)
damagetype "MetalBlade"
Obituary "$OB_METALBLADE"
var int user_TranslateWhiteColors;
States
{
Spawn:
METL AB 3
loop
}
}

actor IGMetalBladeWep : BaseMM8BDMWep
{
Weapon.AmmoUse 0
Weapon.AmmoGive 28
Weapon.SlotNumber 1
Inventory.Pickupmessage "$PU_IGMETALBLADE"
Obituary "$OB_IGMETALBLADE"
Tag "$TAG_IGMETALBLADE"
weapon.ammotype "MetalBladeAmmo"
inventory.icon "NULLICON"
+WEAPON.CHEATNOTWEAPON
States
{
Spawn:
WEAP S 1
loop
Ready:
BUIN A 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_METALMANREAL, CLRF_SILENT)
BUIN A 1 A_WeaponReady
Goto Ready+1
Deselect:
BUIN A 0
goto DeselectSwap
Select:
BUIN A 0
goto SelectSwap
Hold:
Fire:
BUIN A 0 A_FireCustomMissile("IGMetalBlade",0,1,8,0)
BUIN A 0 A_PlayWeaponSound("weapons/mm2/metalbladefire")
BUIN BCDEF 2
BUIN GH 3
BUSH H 0 A_Refire
Goto Ready+1
}
}

actor IGMetalBladeWep_MugshotColor : MugshotColor { args 228, 41 }

actor IGMetalBlade : BasicFastProjectile
{
+RIPPER
+DONTBLAST
seesound "weapons/mm2/metalbladefire"
damagetype "Instagib"
Obituary "$OB_IGMETALBLADE"
Speed 100
Radius 3
Height 4
Damage (100)
var int user_TranslateWhiteColors;
States
{
Spawn:
METL AB 3
loop
}
}