actor CrocQuickBoomerangWep_NormalBar : NormalBar { args 34, 39 }

actor CrocQuickBoomerangWep : BaseMM8BDMWep
{
//$Category MM8BDM-Weapons/Rapid
//$Title Quick Boomerang
//$Sprite WEAPZ0
Weapon.AmmoUse 1
Weapon.AmmoGive 112
Weapon.SlotNumber 3
Inventory.Pickupmessage "$PU_QUICKBOOMERANG"
Obituary "$OB_QUICKBOOMERANG"
Tag "$TAG_QUICKBOOMERANG"
weapon.ammotype "CrocQuickBoomerangAmmo"
inventory.icon "QUICKSI"
States
{
SpawnLoop:
WEAP Z -1
loop
Ready:
QUIC E 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_QUICKBOOMERANG)
QUIC E 1 A_WeaponReady
Goto Ready+1
Deselect:
QUIC E 0
goto DeselectSwap
Select:
QUIC E 0
goto SelectSwap
Fire:
QUIC E 0 A_JumpIfNoAmmo("NoAmmo")
QUIC E 0 A_PlaySoundEx("weapons/mm2/quickboomerangfire","Weapon")
QUIC E 2 A_FireCustomMissile("CrocQuickBoomerang",0,1,8,0)
QUIC F 2
QUIC E 0 A_Refire
Goto Ready+1
Hold:
QUIC E 0 A_JumpIfNoAmmo("NoAmmo")
QUIC E 0 A_PlaySoundEx("weapons/mm2/quickboomerangfire","Weapon")
QUIC E 2 A_FireCustomMissile("CrocQuickBoomerang",0,1,8,0)
QUIC F 2
QUIC E 0 A_Refire
Goto Ready+1
NoAmmo:
QUIC E 1 ACS_NamedExecuteAlways("core_noammo",0)
Goto Ready+1
}
}

actor CrocQuickBoomerangAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 112
+INVENTORY.IGNORESKILL
}


actor CrocQuickBoomerang : BasicProjectile
{
damagetype "QuickBoomerang"
Obituary "$OB_QUICKBOOMERANG"
Radius 6
Height 7
damage (14)
speed 50
reactiontime 3
var int user_TranslateWhiteColors;
States
{
Spawn:
QUIC A 0 //A_CountDown
QUIC AABBCCDD 1 A_ChangeVelocity(-4,0,0,CVF_RELATIVE)
QUIC A 0 A_CountDown
loop
}
}
