actor CrashBombWep_NormalBar : NormalBar { args 4, 225 }

actor CrashBombWep : BaseMM8BDMWep 10054
{
//$Category MM8BDM-Weapons/Power
//$Title Crash Bomb
//$Sprite WEA2C0
Weapon.AmmoUse 3
Weapon.AmmoGive 27
Weapon.SlotNumber 5
Inventory.Pickupmessage "$PU_CRASHBOMB"
Obituary "$OB_CRASHBOMB"
Tag "$TAG_CRASHBOMB"
weapon.ammotype "CrashBombAmmo"
inventory.icon "CRASSI"
States
{
SpawnLoop:
WEA2 C -1
loop
Ready:
CRAS X 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_CRASHBOMB)
CRAS X 1 A_WeaponReady
Goto Ready+1
Deselect:
CRAS X 0
goto DeselectSwap
Select:
CRAS X 0
goto SelectSwap
Fire:
CRAS X 0 A_JumpIfNoAmmo("NoAmmo")
CRAS X 0 A_PlaySoundEx("weapons/mm2/crashbombfire","Weapon")
CRAS X 0 A_FireCustomMissile("CrashBomb",0,1,8,0)
CRAS ZY 2
CRAS X 21
CRAS X 0 A_Refire // 25 ticks
Goto Ready+1
NoAmmo:
CRAS X 1 ACS_NamedExecuteAlways("core_noammo",0)
Goto Ready+1
}
}

actor CrashBombAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 27
+INVENTORY.IGNORESKILL
}

actor CrashBomb : BasicProjectile
{
Radius 6
Height 6
DamageType "CrashBomb"
Obituary "$OB_CRASHBOMB"
damage 0
speed 40
States
{
Spawn:
CRAS A 1
loop
Death:
CRAS A 0 A_JumpIf(z-floorz<5, "Floor")
CRAS A 0 A_JumpIf(z-ceilingz==0-6, "Ceiling")
CRAS B 3 A_PlaySound("weapons/mm2/crashbombland")
CRAS C 3
CRAS DCDCDCDCDCDCDC 3
Goto XDeath
Floor:
CRAS E 3 A_PlaySound("weapons/mm2/crashbombland")
CRAS F 3
CRAS GFGFGFGFGFGFGF 3
Goto XDeath
Ceiling:
CRAS H 3 A_PlaySound("weapons/mm2/crashbombland")
CRAS I 3
CRAS JIJIJIJIJIJIJI 3
Goto XDeath
XDeath:
CRAS A 0 A_PlaySound("weapons/mm2/crasbombexplode")
CRAS A 0 A_SpawnItemEx("CrashBombExplode", 0, 0, 0, 0, 0, 0, 0, SXF_WEPFXCOLOR)
TNT1 AAAA 9 A_Explode(24,64,0)
stop
}
}

actor CrashBombExplode : BasicGraphicEffect
{
    +FORCEXYBILLBOARD
    +BRIGHT
    States
    {
    SpawnFrame:
        CRAS KKLMNNOPQQRSTTVWKKLMNNOPQQRSTTVW 1
        stop
    }
}
