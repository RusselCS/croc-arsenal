// CrocReggae
actor CrocReggaeCall : BaseMM8BDMUseItem 10009
{
//$Category MM8BDM-Assists/Weapon
//$Title CrocReggae Call
//$Sprite WEA9Z0
inventory.amount 1
inventory.maxamount 1
inventory.pickupmessage "$PU_REGGAECALL"
inventory.icon "REGGS"
Tag "$TAG_REGGAECALL"
Inventory.PickupSound "item/1up"
scale 2.0
+COUNTITEM
+INVBAR
states
{
SpawnLoop:
WEA9 Z -1
loop
Use:
TNT1 A 0 A_PlaySoundEx("item/refill","Voice")
TNT1 A 0 A_SpawnItemEx("CrocReggaeTeleport", 80, 0, 256, 0, 0, -24, 0, SXF_NOCHECKPOSITION, 0)
stop
Nope:
TNT1 A 0 
Fail
}
}

actor CrocReggaeCall_Respawn : 8BDMItemRespawn
{
	translation "192:192=216:216", "198:198=0:0"
	mass 1050
}
actor CrocReggaeCall_RespawnShadow : 8BDMItemRespawnShadow
{
	mass 1050
	States
	{
	Spawn:
		WEA9 Z 0
		goto Super::Spawn
	}
}

actor CrocReggaeTeleport : BasicProjectile
{
+RIPPER
+NOINTERACTION
+DONTBLAST
+SKYEXPLODE
damage (0)
Radius 12
Height 32
speed 0
states
{
Spawn:
REGG E 0
REGG E 7
REGG E 0 A_ChangeFlag("NOINTERACTION", 0)
REGG EEEE 1 A_CheckFloor("Next")
Next:
TNT1 A 0 A_ChangeFlag("NOINTERACTION", 1)
TNT1 A 0 A_Stop//A_ChangeFlag("NOINTERACTION", 0)
Goto Death
Death:
TNT1 A 1 A_SpawnItemEx("CrocReggae",0,0,3,0,0,momz,0,SXF_NOCHECKPOSITION,0)
stop
}
}

actor CrocReggae : ProjSpawnFuncActor
{
+NOINTERACTION
-SOLID
-SHOOTABLE
-COUNTKILL
+NOGRAVITY
+MISSILE
+NOCLIP
+SKYEXPLODE
+LOOKALLAROUND
+DONTBLAST
+NOBLOCKMONST
+HEXENBOUNCE
+THRUACTORS
damage 0
speed 18
scale 2.5
Radius 12
Height 32
reactiontime 250
meleerange 80
states
{
Spawn:
TNT1 A 0 
TNT1 A 0 A_ChangeFlag("MISSILE",0)
TNT1 A 0 ThrustThingZ(0, 80, 1, 0)
REGG EEEEEEEEEEEEE 1 A_CheckFloor("Land")
FlyLand:
TNT1 A 0 A_PlaySoundEx("item/reggae", "Voice", 0)
REGG E 0 A_ChangeFlag("NOINTERACTION",0)
REGG E 0 A_ChangeFlag("NOCLIP",0)
REGG E 1 A_Stop
REGG BBBCCCDDDCCCBBBCCCDDDCCC 1 
REGG B 1 A_ChangeVelocity(9.5, 0.0, 0.0, CVF_REPLACE|CVF_RELATIVE)
Goto Presents
Land:
TNT1 A 0 A_PlaySoundEx("item/reggae", "Voice", 0)
REGG E 0 A_ChangeFlag("NOINTERACTION",0)
REGG E 0 A_ChangeFlag("NOCLIP",0)
REGG E 1 A_Stop
REGG FEGG 1
REGG A 25
REGG B 0 A_ChangeVelocity(0.0, 0.0, 2.5, CVF_REPLACE|CVF_RELATIVE)
REGG BBBCCCDDDCCC 1 
REGG B 1 A_ChangeVelocity(9.5, 0.0, 0.0, CVF_REPLACE|CVF_RELATIVE)
Presents:
TNT1 A 0 A_ChangeFlag("MISSILE",1)
REGG B 0 A_PlaySoundex("assists/reggaecandrop", "Voice", 0, 0)
REGG B 0 A_SpawnItemEx("CrocReggaeCan", 0, 0, random(3,18), 8, 0, 8, random(0,360))
REGG BBBCCCDDDCCC 1
REGG A 0 A_GiveInventory("CrocCutterFlag",1)
REGG A 0 A_JumpIfInventory("CrocCutterFlag",15,"Death")
loop
Death:
REGG F 0 A_SpawnItemEx("CrocReggaeDeath",0,0,1,0,0,0,0,SXF_NOCHECKPOSITION|SXF_WEPFXCOLOR)
stop
}
}

actor CrocReggaeCan : BasicProjectile
{
+SKYEXPLODE
+DONTSPLASH
+NOEXPLODEFLOOR
Damage (33)
scale 2.0
-NOGRAVITY
+DOOMBOUNCE
DamageType "CrocReggae"
Obituary "$OB_REGGAE"
wallbouncefactor 0.0
bouncefactor 0.0
radius 18
height 18
translation "192:192=4:4", "198:198=39:39"
states
{
Spawn:
TNT1 A 0
TNT1 A 0 ACS_NamedExecuteWithResult("core_reggaetid", 999)
TNT1 A 1
Mine:
EBAL E 4 A_JumpIfInventory("CrocCutterFlag", 95, "Exit")
REGG H 4 A_GiveInventory("CrocCutterFlag",1)
loop
Death:
TNT1 A 0 A_SpawnItemEx("ExplosionEffect2", 0, 0, 32)
TNT1 A 35 A_PlaySoundEx("assists/reggaecanexplode", "Voice", 0)
stop
Exit:
TNT1 A 0
stop
}
}

actor CrocReggaeBanish : CustomInventory 
{
inventory.amount 1
inventory.maxamount 1
states
{
Pickup:
TNT1 A 0 A_GiveInventory("CrocReggaeBanishTeleporter")
TNT1 A 0 A_StopSoundEx("Weapon")
stop
}
}

actor CrocReggaeBanishTeleporter : ArtiTeleport 
{
inventory.amount 1
inventory.maxamount 1
+INVENTORY.AUTOACTIVATE
}

actor CrocReggaeDeath : BasicProjectile
{
+RIPPER
+BLOODLESSIMPACT
+CLIENTSIDEONLY
+DONTBLAST
Damage (0)
Speed 30
Radius 4
Height 60
reactiontime 35
States
{
Spawn:
REGG GEF 3
REGG E 0 ThrustThingZ(0, 40, 0, 1)
REGG E 1 A_CountDown
Goto Spawn+4
Death:
REGG E 0
stop
}
}
