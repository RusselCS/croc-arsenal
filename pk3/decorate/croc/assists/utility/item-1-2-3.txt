actor Item1 : BaseMM8BDMUseItem 10147
{
//$Category MM8BDM-Assists/Mobility
//$Title Item-1
//$Sprite WEA2A0
Inventory.RespawnTics 210
inventory.amount 3
inventory.maxamount 3
inventory.pickupmessage "$PU_ITEM1"
inventory.icon "ITEM1"
Tag "$TAG_ITEM1"
Inventory.PickupSound "item/1up"
scale 2.0
+COUNTITEM
+INVBAR
states
{
SpawnLoop:
WEA2 A -1
loop
Use:
WEA2 D 0 A_Jumpif(!CallACS("core_CheckFooting"), "Under")
WEA2 D 0 A_SpawnItemEx("Item1Platform", 56, 0, 28, 1)
stop
Under:
WEA2 D 0 A_SpawnItemEx("Item1PlatformUnder", 0, 0, 0, 1)
WEA2 D 0 A_ChangeVelocity(momx, momy, 0, CVF_REPLACE)
stop
}
}

actor Item1_Respawn : 8BDMItemRespawn
{
	translation "192:192=4:4", "198:198=42:42"
	mass 210
}
actor Item1_RespawnShadow : 8BDMItemRespawnShadow
{
	mass 210
	States
	{
	Spawn:
		WEA2 A 0
		goto Super::Spawn
	}
}

actor Item1Platform : ProjSpawnFuncActor
{
height 4
radius 24
scale 2.5
+FORCEYBILLBOARD
+NOGRAVITY
-SHOOTABLE
+SOLID
+THRUSPECIES
+THRUGHOST
+DONTBLAST
+DONTREFLECT
+MISSILE
species "MovingPlatform"
+THRUSPECIES
States
{
Spawn:
ITEM A 1
ITEM A 0 A_Stop
ITEM A 0 A_ChangeFlag(MISSILE,0)
ITEM A 0 A_ChangeFlag(THRUGHOST,0)
ITEM A 0 A_PlaySoundEx("assists/item1spawn", "Body")
ITEM ABABABABABABABAB 3
ITEM AZBZAZBZAZ 3
Goto Death
Death:
TNT1 A 0 A_SpawnItemEx("ExplosionEffect1", 0, 0, 0, 0, 0, 0, 0, SXF_WEPFXCOLOR)
stop
}
}

actor Item1PlatformUnder : Item1Platform
{
    States
    {
    Death:
        TNT1 A 0 A_CheckFlag(MISSILE, "DeathRetry")
        goto Super::Death
    DeathRetry:
        TNT1 A 0 A_SpawnItemEx("Item1PlatformUnderWall")
        stop
    }
}

actor Item1PlatformUnderWall : Item1Platform
{
    radius 16
}

actor Item2 : BaseMM8BDMUseItem 10148
{
//$Category MM8BDM-Assists/Mobility
//$Title Item-2
//$Sprite WEA2D0
Inventory.RespawnTics 210
inventory.amount 2
inventory.maxamount 2
inventory.pickupmessage "$PU_ITEM2"
inventory.icon "ITEM2"
Tag "$TAG_ITEM2"
Inventory.PickupSound "item/1up"
scale 2.0
+COUNTITEM
+INVBAR
states
{
SpawnLoop:
WEA2 D -1
loop
Use:
WEA2 D 0 A_Jumpif(floorz<z, "Under")
WEA2 D 0 A_SpawnItemEx("Item2Platform", 56, 0, 28, 1)
stop
Under:
WEA2 D 0 A_Stop
WEA2 D 0 A_SpawnItemEx("Item2Platform", 0, 0, 0, 1)
stop
}
}

actor Item2_Respawn : 8BDMItemRespawn
{
	translation "192:192=4:4", "198:198=42:42"
	mass 210
}
actor Item2_RespawnShadow : 8BDMItemRespawnShadow
{
	mass 210
	States
	{
	Spawn:
		WEA2 D 0
		goto Super::Spawn
	}
}

actor Item2Platform : ProjSpawnFuncActor
{
height 4
radius 24
scale 2.5
+FORCEYBILLBOARD
+NOGRAVITY
+SHOOTABLE
+NOTAUTOAIMED
+THRUSPECIES
+THRUGHOST
+CANPASS
+NOBLOCKMONST
+DONTBLAST
+SOLID
+DONTREFLECT
+MISSILE
+NODAMAGE
+INVULNERABLE
+NOTAUTOAIMED
+NOTARGETSWITCH
species "MovingPlatform"
+THRUSPECIES
var int user_playerRiding[64];
mass 99999999
//ReactionTime 50
var int user_countdown;
maxstepheight 0
damagefactor "Normal", 0.0
States
{
Spawn:
ITEM C 1
ITEM C 0 A_Stop
ITEM C 0 A_ChangeFlag(RIPPER,1)
ITEM C 0 A_Cleartarget
Waiting:
ITEM C 0 A_SpawnItemEx("Item2Detect", 0, 0, 32)
ITEM C 3 A_SetUserVar("user_countdown", user_countdown + 1)
ITEM C 0 A_JumpIf(user_countdown == 50, "Death")
ITEM C 0 A_JumpIfInventory("Item2Flag", 1, "MoveStart")
Loop
MoveStart:
ITEM D 0 ACS_NamedExecuteAlways("core_stickyLifts",0,STICKYMODE_GUTSLIFT)
ITEM D 0 A_PlaySoundEx("assists/item2launch", "weapon")
ITEM D 3 A_ChangeVelocity(2, 0, 0, CVF_REPLACE|CVF_RELATIVE)
ITEM E 3 A_ChangeVelocity(10, 0, 0, CVF_REPLACE|CVF_RELATIVE)
ITEM D 3 A_ChangeVelocity(18, 0, 0, CVF_REPLACE|CVF_RELATIVE)
Spawn2:
ITEM ED 3
ITEM D 0 A_ChangeVelocity(20, 0, 0, CVF_REPLACE|CVF_RELATIVE)
goto Spawn2
Death:
TNT1 A 0 A_SpawnItemEx("ExplosionEffect1", 0, 0, 0, 0, 0, 0, 0, SXF_WEPFXCOLOR)
stop
}
}

actor IsRidingPlatform : Inventory
{
inventory.amount 1
inventory.maxamount 999
}

actor Item2Detect
{
PROJECTILE
+LOOKALLAROUND
+SKYEXPLODE
+DONTBLAST
+DONTREFLECT
Speed 0
Damage (0)
radius 8
height 4
scale 2.5
States
{
Spawn:
TNT1 A 3
stop
Death:
TNT1 A 0 
TNT1 A 0 A_GiveToTarget("Item2Flag", 1)
stop
}
}

Actor Item2Flag : Once {}

// ITEM 3
actor Item3 : BaseMM8BDMUseItem 10161
{
    //$Category MM8BDM-Assists/Mobility
    //$Title Item-3
    //$Sprite ITM3A0
    Inventory.RespawnTics 210
    inventory.amount 2
    inventory.maxamount 2
    inventory.pickupmessage "$PU_ITEM3"
    inventory.icon "ITEM3"
    Tag "$TAG_ITEM3"
    Inventory.PickupSound "item/1up"
    scale 2.0
    +COUNTITEM
    +INVBAR
    states
    {
    SpawnLoop:
        ITM3 A -1
        loop
    Use:
        ITM3 A 0 A_SpawnItemEx("Item3StartCheck", 0, 1, 0, 1, 0, 0, 0, SXF_ABSOLUTEANGLE|SXF_MULTIPLYSPEED)
        ITM3 A 0 A_SpawnItemEx("Item3StartCheck", 0, 1, 0, 1, 0, 0, 90, SXF_ABSOLUTEANGLE|SXF_MULTIPLYSPEED)
        ITM3 A 0 A_SpawnItemEx("Item3StartCheck", 0, 1, 0, 1, 0, 0, 180, SXF_ABSOLUTEANGLE|SXF_MULTIPLYSPEED)
        ITM3 A 0 A_SpawnItemEx("Item3StartCheck", 0, 1, 0, 1, 0, 0, 270, SXF_ABSOLUTEANGLE|SXF_MULTIPLYSPEED)
        fail
    }
}

actor Item3_Respawn : 8BDMItemRespawn
{
	translation "192:192=4:4", "198:198=42:42"
	mass 210
}
actor Item3_RespawnShadow : 8BDMItemRespawnShadow
{
	mass 210
	States
	{
	Spawn:
		ITM3 A 0
		goto Super::Spawn
	}
}

actor Item3StartCheck
{
    PROJECTILE
    +THRUACTORS
    +SKYEXPLODE
    +DONTBLAST
    +DONTREFLECT
    +NOTARGETSWITCH
    Speed 32
    Damage (0)
    radius 8
    height 4
    States
    {
    Spawn:
        TNT1 A 2
        stop
    Death:
        TNT1 A 0
        TNT1 A 0 A_GiveToTarget("Item3Spawn", 1)
        stop
    }
}

actor Item3Spawn : CustomInventory
{
    inventory.maxamount 1
    States
    {
    Pickup:
        TNT1 A 0 A_JumpIfInventory("Item3Delay", 1, "End")
        TNT1 A 0 A_GiveInventory("Item3Delay", 1)
        TNT1 A 0 A_ChangeVelocity(0.1, 0, momz, CVF_RELATIVE|CVF_REPLACE)
        TNT1 A 0 A_SpawnItemEx("Item3Platform", 0, 0, 0, 0, 0, 1)
        TNT1 A 0 A_TakeInventory("Item3", 1)
        stop
    End:
        TNT1 A 0
        stop
    }
}

actor Item3Delay : PowerUp
{
    powerup.duration 5
}

actor Item3Platform : ProjSpawnFuncActor
{
    height 4
    radius 16
    scale 2.5
    +FORCEYBILLBOARD
    +NOGRAVITY
    +SHOOTABLE
    +NOTAUTOAIMED
    +THRUSPECIES
    +THRUGHOST
    +NOBLOCKMONST
    +DONTBLAST
    +SOLID
    +DONTREFLECT
    +MISSILE
    +NODAMAGE
    +INVULNERABLE
    +NOTAUTOAIMED
    species "MovingPlatform"
    +THRUSPECIES
    mass 99999999
    //ReactionTime 50
    var int user_countdown;
    maxstepheight 0
    reactiontime 7
    damagefactor "Normal", 0.0
    States
    {
    Spawn:
        ITEM F 1
        ITEM F 0 A_Stop
        ITEM F 0 A_Cleartarget
    SpawnLoop:
        ITEM F 0 A_SpawnItemEx("Item3DetectE", 0, 1, 0, 1, 0, 0, 0, SXF_ABSOLUTEANGLE|SXF_MULTIPLYSPEED)
        ITEM F 0 A_SpawnItemEx("Item3DetectN", 0, 1, 0, 1, 0, 0, 90, SXF_ABSOLUTEANGLE|SXF_MULTIPLYSPEED)
        ITEM F 0 A_SpawnItemEx("Item3DetectW", 0, 1, 0, 1, 0, 0, 180, SXF_ABSOLUTEANGLE|SXF_MULTIPLYSPEED)
        ITEM F 0 A_SpawnItemEx("Item3DetectS", 0, 1, 0, 1, 0, 0, 270, SXF_ABSOLUTEANGLE|SXF_MULTIPLYSPEED)
        ITEM F 4
        ITEM F 0 A_JumpIfInventory("Item3Flag", 1, "RiseStart")
        ITEM G 1 A_JumpIf(true, "Death")
        wait
    RiseStart:
        ITEM F 0 A_JumpIfInventory("Item3FlagN", 1, "RiseStartN")
        ITEM F 0 A_JumpIfInventory("Item3FlagS", 1, "RiseStartS")
        ITEM F 0 A_JumpIfInventory("Item3FlagW", 1, "RiseStartW")
        ITEM F 0 A_JumpIfInventory("Item3FlagE", 1, "RiseStartE")
        ITEM F 1 A_JumpIf(true, "Death")
        wait
    RiseStartE:
        ITEM F 0 A_SetAngle(0)
        goto Rise1
    RiseStartW:
        ITEM F 0 A_SetAngle(180)
        goto Rise1
    RiseStartN:
        ITEM F 0 A_JumpIfInventory("Item3FlagW", 1, "RiseStartNW")
        ITEM F 0 A_JumpIfInventory("Item3FlagE", 1, "RiseStartNE")
        ITEM F 0 A_SetAngle(90)
        ITEM F 1 A_JumpIf(true, "Rise1")
        wait
    RiseStartNW:
        ITEM F 0 A_SetAngle(135)
        goto Rise1
    RiseStartNE:
        ITEM F 0 A_SetAngle(45)
        goto Rise1
    RiseStartS:
        ITEM F 0 A_JumpIfInventory("Item3FlagW", 1, "RiseStartSW")
        ITEM F 0 A_JumpIfInventory("Item3FlagE", 1, "RiseStartSE")
        ITEM F 0 A_SetAngle(270)
        ITEM F 1 A_JumpIf(true, "Rise1")
        wait
    RiseStartSW:
        ITEM F 0 A_SetAngle(225)
        goto Rise1
    RiseStartSE:
        ITEM F 0 A_SetAngle(315)
        goto Rise1
    
    Rise1:
        ITEM F 0 ThrustThingZ(0, 24, 0, 0)
    RiseF1:
        ITEM F 0 A_PlaySoundEx("item/item3climb", "voice")
        ITEM F 0 A_TakeInventory("Item3Flag", 1)
        ITEM F 2 A_SpawnItemEx("Item3Detect", 0, 1, 0, 1, 0, 0, 0, SXF_MULTIPLYSPEED)
        ITEM F 0 A_JumpIf(ceilingz-z < 128+height, "Halt")
        ITEM F 0 A_JumpIfInventory("Item3Flag", 1, "RiseF2")
        ITEM F 1 A_JumpIf(true, "Halt")
        wait
    RiseF2:
        ITEM F 0 A_TakeInventory("Item3Flag", 1)
        ITEM F 2 A_SpawnItemEx("Item3Detect", 0, 1, 0, 1, 0, 0, 0, SXF_MULTIPLYSPEED)
        ITEM F 0 A_JumpIf(ceilingz-z < 128+height, "Halt")
        ITEM F 0 A_JumpIfInventory("Item3Flag", 1, "RiseFG")
        ITEM F 1 A_JumpIf(true, "Halt")
        wait
    RiseFG:
        ITEM F 0 A_TakeInventory("Item3Flag", 1)
        ITEM FG 1 A_SpawnItemEx("Item3Detect", 0, 1, 0, 1, 0, 0, 0, SXF_MULTIPLYSPEED)
        ITEM F 0 A_JumpIf(ceilingz-z < 128+height, "Halt")
        ITEM F 0 A_JumpIfInventory("Item3Flag", 1, "RiseG1")
        ITEM F 1 A_JumpIf(true, "Halt")
        wait
    RiseG1:
        ITEM F 0 A_TakeInventory("Item3Flag", 1)
        ITEM G 2 A_SpawnItemEx("Item3Detect", 0, 1, 0, 1, 0, 0, 0, SXF_MULTIPLYSPEED)
        ITEM G 0 A_JumpIf(ceilingz-z < 128+height, "Halt")
        ITEM G 0 A_JumpIfInventory("Item3Flag", 1, "RiseG2")
        ITEM F 1 A_JumpIf(true, "Halt")
        wait
    RiseG2:
        ITEM F 0 A_TakeInventory("Item3Flag", 1)
        ITEM G 2 A_SpawnItemEx("Item3Detect", 0, 1, 0, 1, 0, 0, 0, SXF_MULTIPLYSPEED)
        ITEM G 0 A_JumpIf(ceilingz-z < 128+height, "Halt")
        ITEM G 0 A_JumpIfInventory("Item3Flag", 1, "RiseF1")
        ITEM F 1 A_JumpIf(true, "Halt")
        wait
    Halt:
        ITEM F 0 A_Stop
        ITEM F 0 A_PlaySoundEx("item/item3wait", "voice")
        ITEM FG 5
        ITEM F 0 A_Countdown
        goto Halt+1
    Death:
        TNT1 A 0 A_SpawnItemEx("ExplosionEffect1", 0, 0, 0, 0, 0, 0, 0, SXF_WEPFXCOLOR)
        stop
    }
}

actor Item3Detect : Item3StartCheck
{
    States
    {
    Spawn:
        TNT1 A 2
        stop
    Death:
        TNT1 A 0
        TNT1 A 0 A_GiveToTarget("Item3Flag", 1)
        stop
    }
}

actor Item3DetectE : Item3Detect
{
    States
    {
    Death:
        TNT1 A 0
        TNT1 A 0 A_GiveToTarget("Item3Flag", 1)
        TNT1 A 0 A_GiveToTarget("Item3FlagE", 1)
        stop
    }
}

actor Item3DetectW : Item3Detect
{
    States
    {
    Death:
        TNT1 A 0
        TNT1 A 0 A_GiveToTarget("Item3Flag", 1)
        TNT1 A 0 A_GiveToTarget("Item3FlagW", 1)
        stop
    }
}

actor Item3DetectS : Item3Detect
{
    States
    {
    Death:
        TNT1 A 0
        TNT1 A 0 A_GiveToTarget("Item3Flag", 1)
        TNT1 A 0 A_GiveToTarget("Item3FlagS", 1)
        stop
    }
}

actor Item3DetectN : Item3Detect
{
    States
    {
    Death:
        TNT1 A 0
        TNT1 A 0 A_GiveToTarget("Item3Flag", 1)
        TNT1 A 0 A_GiveToTarget("Item3FlagN", 1)
        stop
    }
}

Actor Item3Flag : Once {}
Actor Item3FlagE : Item3Flag {}
Actor Item3FlagW : Item3Flag {}
Actor Item3FlagS : Item3Flag {}
Actor Item3FlagN : Item3Flag {}
