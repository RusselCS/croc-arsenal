actor CrocEddieSummon : BaseMM8BDMUseItem
{
//$Category MM8BDM-Assists/Utility
//$Title CrocEddie
//$Sprite WEAPQ0
inventory.amount 1
inventory.maxamount 1
inventory.pickupmessage "$PU_EDDIECALL"
Tag "$TAG_EDDIE"
inventory.icon "EDDIEC"
Inventory.PickupSound "item/1up"
scale 2.0
+COUNTITEM
+INVBAR
states
{
SpawnLoop:
WEAP Q -1
Loop
Use:
EDDI O 0 A_PlaySoundEx("item/refill","Voice")
EDDI O 0 A_SpawnItemEx("CrocEddieTeleport", 80, 0, 256, 0, 0, -24, 0, SXF_NOCHECKPOSITION, 0)
stop
Death:
TNT1 A 0
stop
}
}

actor CrocEddieSummon_Respawn : 8BDMItemRespawn
{
	translation "192:192=4:4", "198:198=42:42"
	mass 1050
}
actor CrocEddieSummon_RespawnShadow : 8BDMItemRespawnShadow
{
	mass 1050
	States
	{
	Spawn:
		WEAP Q 0
		goto Super::Spawn
	}
}

actor CrocEddieTeleport : BasicProjectile
{
+RIPPER
+NOINTERACTION
+DONTBLAST
+SKYEXPLODE
damage (0)
Radius 8
Height 8
speed 0
states
{
Spawn:
RUSH D 0
RUSH D 9
RUSH D 0 A_ChangeFlag("NOINTERACTION", 0)
RUSH DDDDDDDDDD 1 A_CheckFloor("Next")
Goto Next
Next:
RUSH D 0 //A_ChangeFlag(NOGRAVITY, false)
Goto Death
Death:
TNT1 A 0 A_SpawnItemEx("CrocEddie",0,0,3,0,0,momz,0,SXF_NOCHECKPOSITION,0) 
stop
}
}

actor CrocEddie : ProjSpawnFuncActor
{
-SOLID
+DONTBLAST
+NOCLIP
+SKYEXPLODE
+MISSILE
+DONTBLAST
-ISMONSTER
scale 2.5
Radius 24
Height 32
speed 6
meleerange 80
reactiontime 250
states
{
Spawn:
RUSH D 0
RUSH D 0 A_JumpIfInventory("Once",1,"Death")
RUSH D 0 A_GiveInventory("Once",1)
RUSH D 0 A_ChangeFlag("MISSILE",0)
RUSH D 0 A_ChangeVelocity(0, 0, -32, CVF_REPLACE)
RUSH D 1 A_JumpIf(z-floorz<=0, "Search")
Goto Spawn+2
Search:
EDDI A 0 A_ChangeFlag("NOCLIP",0)
EDDI A 3
EDDI AAAABBBBAAAACCCCDDDD 1 A_Chase
EDDI A 0 A_CountDown
Goto Search+3
Melee:
EDDI E 4 A_FaceTarget
EDDI F 18 A_SpawnItemEx("CrocRandomWeapon",0,0,32,0,0,0)
EDDI F 0 A_Die
Goto Death
Death:
RUSH D 0 ThrustThingZ(0, 34, 0, 1)
RUSH D 0 A_ChangeFlag("NOGRAVITY", 1)
RUSH D 1 A_JumpIf(z-ceilingz==0-32, "Bye")
Goto Death+2
Bye:
RUSH D 1
stop
}
}

actor CrocRandomWeapon
{
+NOINTERACTION
States
{
Spawn:
TNT1 A 0
TNT1 A 0 ACS_NamedExecuteAlways("core_eddiedropper", 0)
TNT1 A 2
stop
}
}
