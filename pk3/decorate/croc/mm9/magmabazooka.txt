actor CrocMagmaBazookaWep_ScriptBar : ScriptBar {}

actor CrocMagmaBazookaWep : BaseMM8BDMWep
{
//$Category MM8BDM-Weapons/Close
//$Title Magma Bazooka
//$Sprite MGM1A0
Weapon.AmmoUse 2
Weapon.AmmoGive 30
weapon.ammotype "CrocMagmaBazookaAmmo"
Weapon.SlotNumber 4
Obituary "$OB_MAGMABAZOOKA"
Tag "$TAG_MAGMABAZOOKA"
Inventory.Pickupmessage "$PU_MAGMABAZOOKA"
inventory.icon "MAGMAI"
States
{
SpawnLoop:
MGM1 A -1
loop
Ready:
MGHD A 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_MAGMABAZOOKA)
MGHD A 1 A_WeaponReady
Goto Ready+1
Deselect:
MGHD A 0
goto DeselectSwap
Select:
MGHD A 0
goto SelectSwap
Altfire:
MGHD A 0 A_JumpIfNoAmmo("NoAmmo")
MGHD A 0 A_PlaySoundEx("weapons/mm9/magmabazookafire","Weapon")
MGHD A 0 A_FireCustomMissile("CrocMagmaBazooka1",0,1,8,0)
MGHD A 0 A_FireCustomMissile("CrocMagmaBazooka1",-16,0,8,0)
MGHD A 0 A_FireCustomMissile("CrocMagmaBazooka1",16,0,8,0)
MGHD DE 3
MGHD A 7
MGHD A 0 A_Refire("Altfire")
Goto Ready+1
Fire:
TNT1 A 0 A_StopSoundEx("SoundSlot5")
MGHD A 0 A_TakeInventory("WeaponCharge", 999)
MGHD A 0 ACS_NamedExecuteWithResult("core_chargecolor",CLR_MAGMABAZOOKA,TCLR_NONE)
MGHD A 0 A_JumpIfNoAmmo("NoAmmo")
MGHD A 0 A_Refire
MGHD A 0 A_PlaySoundEx("weapons/mm9/magmabazookafire","Weapon")
MGHD A 0 A_FireCustomMissile("CrocMagmaBazooka1",0,1,8,0)
MGHD A 0 A_FireCustomMissile("CrocMagmaBazooka1",-16,0,8,0)
MGHD A 0 A_FireCustomMissile("CrocMagmaBazooka1",16,0,8,0)
MGHD DE 3
MGHD A 4
MGHD A 0 A_Refire
Goto Ready+1
Fire2:
TNT1 A 0 A_StopSoundEx("SoundSlot5")
MGHD A 0 A_TakeInventory("WeaponCharge", 999)
MGHD A 0 ACS_NamedExecuteWithResult("core_chargecolor",CLR_MAGMABAZOOKA,TCLR_NONE)
MGHD A 0 A_PlaySoundEx("weapons/mm9/magmabazookafire","Weapon")
MGHD A 0 A_FireCustomMissile("CrocMagmaBazooka2",0,0,8,0)
MGHD A 0 A_FireCustomMissile("CrocMagmaBazooka2",-16,0,8,0)
MGHD A 0 A_FireCustomMissile("CrocMagmaBazooka2",16,0,8,0)
MGHD A 0 A_TakeInventory("CrocMagmaBazookaAmmo", 6, TIF_NOTAKEINFINITE)
MGH0 ABCDEF 2
MGHD A 5
MGHD A 0 A_Refire
Goto Ready+1 // 17 frames
Hold:
MGHD A 0 A_JumpIfNoAmmo("NoAmmo")
MGHD A 0 A_GunFlash("Sound.Magma", GFF_NOEXTCHANGE)
Hold.b:
MGHD A 0 A_JumpIfInventory("WeaponCharge", 19, "Charge2Start")// A_PlaySoundEx("weapons/mm9/magmabazookacharge", "Weapon")
MGHD A 0 A_GiveInventory("WeaponCharge", 1 + CallACS("core_checkrune", RUNE_RAGE))
MGHD A 1
MGHD A 0 A_Refire("Hold.a")
goto fire
Hold.a:
MGHD F 1
MGHD A 0 A_Refire("Hold.b")
goto Fire
BotFire:
MGHD G 0 A_Jump(16, "Fire2")
MGHD G 0 A_Refire("Charge2")
Goto Fire
Charge2Start:
Charge2:
MGHD B 1 ACS_NamedExecuteWithResult("core_chargecolor",CLR_MAGMACHARGE1,TCLR_CHARGE2)
MGHD G 0 A_Refire("Charge2.a")
MGHD A 0 A_JumpIfInventory("CrocMagmaBazookaAmmo", 6, "Fire2")
Goto Fire
Charge2.a:
MGHD A 1 ACS_NamedExecuteWithResult("core_chargecolor",CLR_MAGMABAZOOKA,TCLR_CHARGE3)
MGHD A 0 A_Refire("Charge2.b")
MGHD A 0 A_JumpIfInventory("CrocMagmaBazookaAmmo", 6, "Fire2")
goto Fire
Charge2.b:
MGHD C 1 ACS_NamedExecuteWithResult("core_chargecolor",CLR_MAGMACHARGE2,TCLR_NONE)
MGHD A 0 A_JumpIfInventory("IsBot", 1, "BotFire")
MGHD A 0 A_Refire("Charge2")
MGHD A 0 A_JumpIfInventory("CrocMagmaBazookaAmmo", 6, "Fire2")
goto Fire
NoAmmo:
MGHD A 1 ACS_NamedExecuteAlways("core_noammo",0)
Goto Ready+1

Sound.Magma:
TNT1 A 0 A_JumpIfInventory("WeaponCharge",19,"Sound.Magma2")
TNT1 A 5 A_PlaySoundEx("weapons/mm9/magmabazookacharge", "Weapon", 0)
TNT1 A 1 A_JumpIfInventory("WeaponCharge",1,"Sound.Magma")
stop
Sound.Magma2:
TNT1 A 11 A_PlaySoundEx("weapons/mm9/magmabazookacharge2", "Weapon", 0)
TNT1 A 1 A_JumpIfInventory("WeaponCharge",1,"Sound.Magma")
stop
}
}

actor CrocMagmaBazookaAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 30
+INVENTORY.IGNORESKILL
}

actor CrocMagmaBazooka1 : BasicProjectile
{
+BRIGHT
Damage (15)
Speed 26
Obituary "$OB_MAGMABAZOOKA"
damagetype "MagmaBazooka1"
Radius 4
Height 8
States
{
Spawn:
MGM1 BCDEFG 2
loop
Death:
TNT1 A 0 A_SpawnItemEx("OilPitIgnite",0,0,8)
stop
}
}

actor CrocMagmaBazooka2 : CrocMagmaBazooka1
{
Damage (33)
damagetype "MagmaBazooka2"
Radius 24
Height 16
States
{
Spawn:
MGM2 ABCDEF 2
loop
}
}
