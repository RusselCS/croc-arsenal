actor CrocJewelSatelliteWep_NormalBar : NormalBar { args 4, 193 }
actor CrocJewelSatelliteWep_ScriptBar : ScriptBar {}

actor CrocJewelSatelliteWep : BaseMM8BDMWep 10026
{
//$Category MM8BDM-Weapons/Shield
//$Title Jewel Satellite
//$Sprite JEWEA0
Weapon.Ammouse 7
Weapon.AmmoGive 28
Weapon.SlotNumber 7
Obituary "$OB_JEWELSATELLITE"
Tag "$TAG_JEWELSATELLITE"
Inventory.Pickupmessage "$PU_JEWELSATELLITE"
weapon.ammotype "CrocJewelSatelliteAmmo"
inventory.icon "JEWELI"
States
{
SpawnLoop:
JEWE A -1
loop
Ready:
JEWE J 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_JEWELSATELLITE)
JEWE J 0 A_ClearRefire
JEWE J 1 A_WeaponReady
Goto Ready+2
Deselect:
JEWE J 0 A_JumpIfInventory("CrocJewelShieldCheck",1,1)
goto DeselectSwap
JEWE M 0 A_GiveInventory("CrocJewelSatelliteDisable", 1)
goto DeselectSwap
Select:
JEWE K 0
goto SelectSwap
Fire:
JEWE M 0 A_JumpIfInventory("CrocJewelShieldCheck",1,"ShieldThrow")
JEWE M 0 A_JumpIfNoAmmo("NoAmmo")
JEWE M 0 A_PlaySoundEx("weapons/mm9/jewelsatelliteactivate", "Weapon")
JEWE M 0 A_TakeInventory("CrocJewelSatelliteAmmo", 7,TIF_NOTAKEINFINITE)
JEWE M 0 //A_TakeInventory("BasicArmor", 9999)
JEWE M 0 A_GiveInventory("ShieldCheck", 1)
JEWE M 0 A_GiveInventory("CrocJewelShieldCheck", 4)
JEWE M 0 A_GiveInventory("CrocJewelSatelliteEnable", 1)
JEWE M 0 A_SpawnItemEx("CrocJewelSatelliteWarper")
JEWE M 0 A_SpawnItemEx("CrocJewelSatelliteWarper2")
JEWE M 0 A_SpawnItemEx("CrocJewelSatelliteWarper3")
JEWE M 0 A_SpawnItemEx("CrocJewelSatelliteWarper4")
JEWE KLMNOP 2
JEWE R 8
goto ShieldActive
ShieldActive:
JEWE R 1 A_WeaponReady
JEWE M 0 A_JumpIfInventory("CrocJewelShieldCheck", 1, "ShieldActive")
goto ShieldEnd
ShieldThrow:
JEWE J 0 A_GiveInventory("CrocSpreadDrillTrigger", 1)
JEWE J 0 A_GiveInventory("CrocGyroAttackTrigger", 1)
JEWE J 0 A_GiveInventory("RemoteMineFlag", 10)
JEWE J 0 A_GiveInventory("CrocBlackHoleBombTrigger", 1)
JEWE M 0 A_PlaySoundEx("weapons/mm9/jewelsatellitefire", "weapon")
JEWE M 0 A_FireCustomMissile("CrocJewelShot",0,0,8,0)
JEWE M 0 A_TakeInventory("CrocJewelShieldCheck", 1)
JEWE TSR 5
JEWE J 0 A_TakeInventory("RemoteMineFlag", 999)
goto ShieldEnd
ShieldEnd:
JEWE M 0 A_JumpIfInventory("CrocJewelShieldCheck", 1, "ShieldActive")
JEWE M 0 A_TakeInventory("ShieldCheck", 1)
JEWE M 0 A_TakeInventory("CrocJewelShieldCheck", 4)
JEWE M 0 A_GiveInventory("CrocJewelSatelliteDisable", 1)
JEWE J 17
goto Ready+1
NoAmmo:
JEWE J 1 ACS_NamedExecuteAlways("core_noammo",0)
goto Ready+1
}
}

actor CrocJewelSatelliteEnable : CustomInventory
{
	States
	{
	Pickup:
		TNT1 A 0 A_ChangeFlag(REFLECTIVE, true)
		TNT1 A 0 A_ChangeFlag(DONTRIP, true)
		TNT1 A 0 A_ChangeFlag(DEFLECT, true)
		stop
	}
}

actor CrocJewelSatelliteDisable : CustomInventory
{
	States
	{
	Pickup:
		TNT1 A 0 A_TakeInventory("ShieldCheck", 1)
		TNT1 A 0 A_TakeInventory("CrocJewelShieldCheck", 4)
		TNT1 A 0 A_ChangeFlag(REFLECTIVE, false)
		TNT1 A 0 A_ChangeFlag(DONTRIP, false)
		TNT1 A 0 A_ChangeFlag(DEFLECT, false)
		stop
	}
}

actor CrocJewelSatelliteAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}

actor CrocJewelShieldCheck : Inventory
{
inventory.amount 1
inventory.maxamount 4
}

actor CrocJewelSatelliteWarper : BasicShieldWarper
{
	States
	{
	Spawn:
	TNT1 A 0
	TNT1 A 1 A_GiveInventory("MM8BDMProjSpawnFuncClient",1)
	TNT1 A 0 ACS_NamedExecuteAlways("core_shieldwarper_jewel", 0, 0)
	ShieldLoop:
	JEWS AABBCCDDEEFFGGHHIIJJKKLLMMNNOOPP 1 A_Warp(AAPTR_TARGET, 0, 0, 0, 0, WARPF_INTERPOLATE|WARPF_NOCHECKPOSITION)
	loop
	}
}

actor CrocJewelSatelliteTrailExecute : CustomInventory
{
	States
	{
	Pickup:
		TNT1 A 0 ACS_NamedExecuteAlways("core_jewelsatellitetrail_client", 0)
		stop
	}
}

actor CrocJewelSatelliteWarper2 : JewelSatelliteWarper
{
	States
	{
	Spawn:
	TNT1 A 0
	TNT1 A 1 A_GiveInventory("MM8BDMProjSpawnFuncClient",1)
	TNT1 A 0 ACS_NamedExecuteAlways("core_shieldwarper_jewel", 0, 1)
	Goto ShieldLoop+8
	}
}

actor CrocJewelSatelliteWarper3 : JewelSatelliteWarper
{
	States
	{
	Spawn:
	TNT1 A 0
	TNT1 A 1 A_GiveInventory("MM8BDMProjSpawnFuncClient",1)
	TNT1 A 0 ACS_NamedExecuteAlways("core_shieldwarper_jewel", 0, 2)
	Goto ShieldLoop+16
	}
}

actor CrocJewelSatelliteWarper4 : JewelSatelliteWarper
{
	States
	{
	Spawn:
	TNT1 A 0
	TNT1 A 1 A_GiveInventory("MM8BDMProjSpawnFuncClient",1)
	TNT1 A 0 ACS_NamedExecuteAlways("core_shieldwarper_jewel", 0, 3)
	Goto ShieldLoop+24
	}
}

actor CrocJewelSatelliteReflectSound : BasicWatcher
{
	States
	{
	Spawn:
		TNT1 A 0
		TNT1 A 0 A_PlaySoundEx("weapons/mm9/jewelsatellitereflect", "body")
		TNT1 A 20
		stop
	}
}

actor CrocJewelSatelliteTrailFlag : Once {}

actor CrocJewelShot : BasicProjectile
{
Obituary "$OB_JEWELSATELLITE"
Damagetype "JewelSatellite"
Speed 30
Damage (20)
Radius 16
Height 7
States
{
Spawn:
JEWS QRST 2 A_SpawnItemEx("CrocJewelShotFX", random(-10, 10), random(-10, 10), random(-10, 10), 0, 0, 0, 0, SXF_WEPFXCOLOR)
loop
XDeath:
JEWS QQQQQ 0 A_SpawnItemEx("CrocJewelShotFX", random(-32, 32), random(-32, 32), random(-32, 32), 0, 0, 0, 0, SXF_WEPFXCOLOR)
JEWS Q 0 A_PlaySoundEx("weapons/mm9/jewelsatellitehit", "body")
TNT1 A 20
stop
}
}

actor CrocJewelShotFX : BasicGraphicEffect
{
+FORCEXYBILLBOARD
+BRIGHT
Speed 80
States
{
Spawn:
JEWS UVW 3
stop
}
}
