actor PlugBallWep_NormalBar : NormalBar { args 4, 39 }

actor PlugBallWep : BaseMM8BDMWep 10025
{
//$Category MM8BDM-Weapons/Rapid
//$Title Plug Ball
//$Sprite PBALA0
Weapon.AmmoUse 1
Weapon.AmmoGive 28
Weapon.SlotNumber 3
Obituary "$OB_PLUGBALL"
Tag "$TAG_PLUGBALL"
Inventory.Pickupmessage "$PU_PLUGBALL"
weapon.ammotype "PlugBallAmmo"
inventory.icon "PLUGI"
States
{
SpawnLoop:
PBAL A -1
loop
Ready:
PLUG B 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_PLUGBALL)
PLUG B 1 A_WeaponReady
Goto Ready+1
Deselect:
PLUG B 0
goto DeselectSwap
Select:
PLUG B 0
goto SelectSwap
Fire:
PLUG B 0 A_JumpIfNoAmmo("NoAmmo")
PLUG B 0 A_PlaySoundEx("weapons/mm9/plugballfire","Weapon")
PLUG B 0 A_FireCustomMissile("PlugBallStart",0,1,8,0)
PLUG BCD 3
PLUG B 3
PLUG B 0 A_Refire
Goto Ready+1
NoAmmo:
PLUG B 1 ACS_NamedExecuteAlways("core_noammo",0)
Goto Ready+1
}
}

actor PlugBallAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}

actor PlugBallStart : BasicProjectile
{
+FORCEXYBILLBOARD
+BRIGHT
Obituary "$OB_PLUGBALL"
Damage (20)
damagetype "PlugBall"
Height 5
Radius 6
speed 0
States
{
Spawn:
TNT1 A 0
TNT1 A 0 ACS_NamedExecuteWithResult("core_changeClientProperty", APROP_Speed, 60 * FIXED_VAL, AAPTR_TARGET)
TNT1 A 0 ThrustThingZ(0, 24*4, 1, 0)
TNT1 A 2
SpawnLoop:
PBAL BCDE 1
loop
Death:
TNT1 A 0 A_SpawnItemEX("ElectrifyEffect")
TNT1 A 0 A_SpawnItemEx("PlugBall",0,0,0,24,0,0,0,32)
stop
XDeath:
TNT1 A 0 A_SpawnItemEX("ElectrifyEffect")
TNT1 A 0
stop
}
}

actor PlugBall : BasicProjectile // "Basic"
{
var int user_x;
var int user_y;
damagetype "PlugBall"
Obituary "$OB_PLUGBALL"
-NOGRAVITY
-BOUNCEONFLOORS
+BOUNCEONWALLS
+CANBOUNCEWATER
+DONTREFLECT
+FORCEXYBILLBOARD
+STEPMISSILE
+NOEXPLODEFLOOR
+BRIGHT
maxstepheight 24
bouncecount 10
bouncefactor 1.0
wallbouncefactor 1.0
gravity 4.0
reactiontime 50
Damage (20)
Radius 16
Height 32
Speed 24
States
{
Spawn:
PBAL B 0
PBAL B 0 ThrustThingZ(0,0,0,1)//Resync
PBAL B 0 ThrustThing(angle*256/360,0,1,0)
PBAL B 0 A_SetUserVar(user_x, x)
PBAL B 0 A_SetUserVar(user_y, y)
SpawnL:
PBAL B 0
PBAL B 1 A_JumpIf(z-floorz>0,"SpawnFB")
PBAL C 1 A_JumpIf(z-floorz>0,"SpawnFC")
PBAL D 1 A_JumpIf(z-floorz>0,"SpawnFD")
PBAL E 1 A_JumpIf(z-floorz>0,"SpawnFE")
PBAL B 0 A_CountDown

PBAL B 0 A_JumpIf((user_x-x < 2 && user_x-x > -2) && (user_y-y < 2 && user_y-y > -2), "Death2")
PBAL B 0 A_SetUserVar(user_x, x)
PBAL B 0 A_SetUserVar(user_y, y)
loop
SpawnFB:
PBAL B 0 A_ChangeVelocity(0,0,-24,3)//CVF_RELATIVE|CVF_REPLACE
PBAL B 0 ThrustThing(angle*256/360,0,1,0)//Resync
goto SpawnF+1
SpawnFC:
PBAL C 0 A_ChangeVelocity(0,0,-24,3)
PBAL C 0 ThrustThing(angle*256/360,0,1,0)
goto SpawnF+2
SpawnFD:
PBAL D 0 A_ChangeVelocity(0,0,-24,3)
PBAL D 0 ThrustThing(angle*256/360,0,1,0)
goto SpawnF+3
SpawnFE:
PBAL E 0 A_ChangeVelocity(0,0,-24,3)
PBAL E 0 ThrustThing(angle*256/360,0,1,0)
goto SpawnF+4
SpawnF:
PBAL B 0
PBAL B 1 A_JumpIf(z-floorz==0,"SpawnLB")
PBAL C 1 A_JumpIf(z-floorz==0,"SpawnLC")
PBAL D 1 A_JumpIf(z-floorz==0,"SpawnLD")
PBAL E 1 A_JumpIf(z-floorz==0,"SpawnLE")
PBAL B 0 A_CountDown
loop
SpawnLB:
PBAL B 0 A_JumpIf(CallACS("core_plugballskycheck"),"Death3")
PBAL B 0 A_ChangeVelocity(24,0,0,3)
PBAL B 1
goto SpawnL+1
SpawnLC:
PBAL C 0 A_JumpIf(CallACS("core_plugballskycheck"),"Death3")
PBAL C 0 A_ChangeVelocity(24,0,0,3)
PBAL C 1
goto SpawnL+2
SpawnLD:
PBAL D 0 A_JumpIf(CallACS("core_plugballskycheck"),"Death3")
PBAL D 0 A_ChangeVelocity(24,0,0,3)
PBAL D 1
goto SpawnL+3
SpawnLE:
PBAL E 0 A_JumpIf(CallACS("core_plugballskycheck"),"Death3")
PBAL E 0 A_ChangeVelocity(24,0,0,3)
PBAL E 1
goto SpawnL+4

Death3:
TNT1 A 0 //A_Log("OH NO A SCARY PIT")
Death2:
TNT1 A 1 A_CountDownArg(0)
wait
Death:
TNT1 A 0 A_SpawnItemEX("ElectrifyEffect")
TNT1 A 0
stop
}
}
