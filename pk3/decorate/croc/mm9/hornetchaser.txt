actor CrocHornetChaserWep_NormalBar : NormalBar { args 4, 228 }
actor CrocHornetChaserWep_ScriptBar : ScriptBar {}

actor CrocHornetChaserWep : BaseMM8BDMWep 10027
{
//$Category MM8BDM-Weapons/Close
//$Title Hornet Chaser
//$Sprite HCHSA0
Weapon.AmmoUse 2
Weapon.AmmoGive 28
Weapon.SlotNumber 4
Obituary "$OB_HORNETCHASER"
Tag "$TAG_HORNETCHASER"
Inventory.Pickupmessage "$PU_HORNETCHASER"
weapon.ammotype "CrocHornetChaserAmmo"
inventory.icon "HORNETI"
States
{
SpawnLoop:
HCHS A -1
loop
Ready:
HCHS D 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_HORNETCHASER)
HCHS D 0 A_JumpIfInventory("CrocHornetChaserCooldownFlag",1,"ReadyCooldown")
HCHS D 1 A_WeaponReady
Goto Ready+1
ReadyCooldown:
HCHS D 1 A_WeaponReady(WRF_NOFIRE)
Goto Ready+1
Deselect:
HCHS D 0
goto DeselectSwap
Select:
HCHS D 0
goto SelectSwap
Fire:
HCHS D 0 A_JumpIfNoAmmo("NoAmmo")
HCHS D 0 A_PlaySoundEx("weapons/mm9/hornetchaserfire","Weapon")
HCHS D 0 A_FireCustomMissile("CrocHornetChaser",0,1,8,0)
HCHS FE 2
HCHS D 0 A_GiveInventory("CrocHornetChaserCooldownFlag",1)
HCHS D 0 A_SpawnItemEx("CrocHornetChaserCooldowner")
Goto Ready+1
NoAmmo:
HCHS D 1 ACS_Execute(979,0)
Goto Ready+1
}
}

actor CrocHornetChaserCooldownFlag : Once {}

actor CrocHornetChaserCooldowner : BasicCooldowner
{
reactiontime 23
States
{
Spawn:
TNT1 A 0
Spawn2:
TNT1 A 1 A_GiveInventory("CrocHornetChaserCooldownerFunc",1)
loop
Death:
TNT1 A 0 A_TakeFromTarget("CrocHornetChaserCooldownFlag",1)
stop
}
}

actor CrocHornetChaserCooldownerFunc : BasicCooldownerFunc
{
States
{
Pickup:
TNT1 A 0 A_JumpIfInTargetInventory("CrocHornetChaserCooldownFlag",1,"PickupBasic")
goto PickupRemove
}
}

actor CrocHornetChaserAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}

actor CrocHornetChaser : BasicBouncer
{
+SEEKERMISSILE
+LOOKALLAROUND
Obituary "$OB_HORNETCHASER"
damagetype "CrocHornetChaser"
Radius 5
Height 5
damage (20)
speed 10
ReactionTime 175
renderstyle "translucent"
States
{
Spawn:
HCHS B 0
HCHS B 0 ACS_NamedExecuteWithResult("core_alphaprojectile", CallACS("core_getptrtid", AAPTR_TARGET), APROJ_TEAM, 6553, 192) // 6553 = 0.1 * 65536
HCHS B 0 A_ScaleVelocity(2.0)
HCHS C 0 A_SetArg(0, 140)
HCHS BC 1
HCHS BCBCBC 1 A_SeekerMissile(4, 10, SMF_LOOK|SMF_PRECISE, 256, 2)
HCHS B 0 A_Stop
SpawnLook:
HCHS B 0 A_PlaySoundEx("weapons/mm9/hornetchaserhover", "weapon")
HCHS B 1 A_SeekerMissile(4, 10, SMF_LOOK|SMF_PRECISE, 256, 2)
HCHS B 0 A_JumpIfInTargetInventory("IsDead", 1, "Death")
HCHS C 0 A_Countdown
HCHS C 0 A_JumpIfTargetInLOS("SpawnChase", 0, JLOSF_PROJECTILE)
HCHS C 1 A_SeekerMissile(4, 10, SMF_LOOK|SMF_PRECISE, 256, 2)
HCHS B 0 A_JumpIfInTargetInventory("IsDead", 1, "Death")
HCHS C 0 A_Countdown
HCHS C 0 A_JumpIfTargetInLOS("SpawnChase", 0, JLOSF_PROJECTILE)
loop
SpawnChase:
HCHS B 0 A_PlaySoundEx("weapons/mm9/hornetchaserhover", "weapon")
HCHS B 1 A_SeekerMissile(4, 10, SMF_PRECISE)
HCHS B 0 A_JumpIfInTargetInventory("IsDead", 1, "Death")
HCHS B 0 A_CountDownArg(0)
HCHS C 1 A_SeekerMissile(4, 10, SMF_PRECISE)
HCHS B 0 A_JumpIfInTargetInventory("IsDead", 1, "Death")
HCHS B 0 A_CountDownArg(0)
loop
Death:
TNT1 A 0 A_SpawnItemEx("ExplosionEffect1", 0, 0, 0, 0, 0, 0, 0, SXF_WEPFXCOLOR)
stop
}
}
