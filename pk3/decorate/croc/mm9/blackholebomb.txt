actor CrocBlackHoleBombWep_NormalBar : NormalBar { args 59, 62 }
actor CrocBlackHoleBombWep_ScriptBar : ScriptBar {}

actor CrocBlackHoleBombWep : BaseMM8BDMWep 10029
{
//$Category MM8BDM-Weapons/AoE
//$Title Black Hole Bomb
//$Sprite BLKHA0
Weapon.AmmoUse 4
Weapon.AmmoGive 28
Weapon.SlotNumber 6
Obituary "$OB_BLACKHOLEBOMB"
Tag "$TAG_BLACKHOLEBOMB"
Inventory.Pickupmessage "$PU_BLACKHOLEBOMB"
weapon.ammotype "CrocBlackHoleBombAmmo"
inventory.icon "BLAKHOLI"
States
{
SpawnLoop:
BLKH A -1
loop
Ready:
BHUD A 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_BLACKHOLEBOMB)
BHUD A 1 A_GiveInventory("CrocBlackHoleBomb_Ready_P")//A_WeaponReady
wait
Deselect:
BHUD A 0 A_GiveInventory("CrocBlackHoleBombKill", 1)
goto DeselectSwap
Select:
BHUD A 0
goto SelectSwap
Fire:
BHUD A 0 A_JumpIfInventory("CrocBlackHoleBombExists",1,"Detonate")
BHUD A 0 A_JumpIfNoAmmo("NoAmmo")
BHUD A 0 A_GiveInventory("CrocBlackHoleBomb_Fire_P")
BHUD BC 3
BHUD A 10
goto Ready+1
Detonate:
BHUD A 0 A_GiveInventory("CrocBlackHoleBombTrigger", 1)
BHUD DA 3
goto Ready+1
NoAmmo:
BHUD A 1 ACS_NamedExecuteAlways("core_noammo",0)
Goto Ready+1
}
}

actor CrocBlackHoleBombAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}

actor CrocBlackHoleBombExists : Powerup
{
    powerup.duration 5
}

actor CrocBlackHoleBombKill : CrocBlackHoleBombExists {}

actor CrocBlackHoleBomb_Ready_P : CustomInventory
{
    States
    {
    Pickup:
        TNT1 A 0 A_JumpIfInventory("CrocBlackHoleBombCooldown",1,2)
        TNT1 A 0 A_WeaponReady
        stop
        TNT1 A 0 A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY|WRF_NOSWITCH)
        stop
    }
}

actor CrocBlackHoleBomb_Fire_P : CustomInventory
{
    States
    {
    Pickup:
		TNT1 A 0 A_TakeInventory("CrocBlackHoleBombAmmo", 4, TIF_NOTAKEINFINITE)
        TNT1 A 0 A_SpawnItemEx("CrocBlackHoleBomb", 0, 8, 32, cos(-pitch)*10, 0, sin(-pitch)*10, 0, SXF_TRANSFERTRANSLATION|SXF_TRANSFERPITCH)
        TNT1 A 0 A_JumpIfInventory("PowerSpreadRune", 1, "PickupSpread")
        stop
    PickupSpread:
        TNT1 A 0 A_SpawnItemEx("CrocBlackHoleBomb", -48, 8+32, 32, cos(-pitch)*10, 0, sin(-pitch)*10, 0, SXF_TRANSFERTRANSLATION|SXF_TRANSFERPITCH)
        TNT1 A 0 A_SpawnItemEx("CrocBlackHoleBomb", -48, 8-32, 32, cos(-pitch)*10, 0, sin(-pitch)*10, 0, SXF_TRANSFERTRANSLATION|SXF_TRANSFERPITCH)
        stop
    }
}

actor CrocBlackHoleBomb : BasicProjectile
{
+NOTARGETSWITCH
+FORCEXYBILLBOARD
+RIPPER
+BRIGHT
Obituary "$OB_BLACKHOLEBOMB"
damagetype "CrocBlackHoleBomb"
Radius 15
Height 8
damage (0)
speed 12
reactiontime 1
States
{
Spawn:
BLKH B 0
BLKH B 0 A_GiveInventory("CrocBlackHoleBombFlyFunc2", 1)
BLKH B 1 A_PlaySoundEx("weapons/mm9/blackholebombfly","Weapon",1)
SpawnLoop:
BLKH BBBCCCCDDDDEEEEB 1 A_GiveInventory("CrocBlackHoleBombFlyFunc", 1)
loop
Explode:
BLKH B 0 A_StopSound
BLKH B 0 A_SpawnItemEx("CrocBlackHoleBombExplosion",0,0,0,0,0,0,0,SXF_TRANSFERTRANSLATION|SXF_NOCHECKPOSITION)
stop
Death:
BLKH B 0 A_StopSound
BLKH B 0 A_TakeFromTarget("CrocBlackHoleBombExists", 999)
BLKH B 0 A_JumpIf(reactiontime >= 0, "Dying")
BLKH B 0 A_JumpIf(reactiontime < 0, "Explode")
BLKH B 1
wait
Dying:
BLKH B 0 A_SetScale(scalex, scaley - 0.15)
BLKH B 1 A_FadeOut(0.1) 
BLKH B 0 A_SetScale(scalex, scaley - 0.15)
BLKH B 1 A_FadeOut(0.1) 
BLKH B 0 A_SetScale(scalex, scaley - 0.15)
BLKH C 1 A_FadeOut(0.1) 
BLKH B 0 A_SetScale(scalex, scaley - 0.15)
BLKH C 1 A_FadeOut(0.1) 
BLKH B 0 A_SetScale(scalex, scaley - 0.15)
BLKH D 1 A_FadeOut(0.1) 
BLKH B 0 A_SetScale(scalex, scaley - 0.15)
BLKH D 1 A_FadeOut(0.1) 
BLKH B 0 A_SetScale(scalex, scaley - 0.15)
BLKH E 1 A_FadeOut(0.1)
BLKH B 0 A_SetScale(scalex, scaley - 0.15)
BLKH E 1 A_FadeOut(0.1)
loop 
}
}

actor CrocBlackHoleBombDirect_CD : Powerup { powerup.duration 4 }

actor CrocBlackHoleBombFlyFunc : CustomInventory
{
	States
	{
	Pickup:
		TNT1 A 0 A_GiveToTarget("CrocBlackHoleBombExists", 1)
		TNT1 A 0 A_JumpIfInTargetInventory("CrocBlackHoleBombTrigger", 1, "Detonate")
		TNT1 A 0 A_JumpIfInTargetInventory("CrocBlackHoleBombKill", 1, "End")
		TNT1 A 0 A_JumpIfInventory("CrocBlackHoleBombDirect_CD", 1, "Skip")
		TNT1 A 0 ACS_NamedExecuteAlways("core_targetyawpitch", 0, 1)
		TNT1 A 0 A_ChangeVelocity(cos(-pitch)*15, 0, sin(-pitch)*15, CVF_RELATIVE|CVF_REPLACE)
		TNT1 A 0 A_GiveInventory("CrocBlackHoleBombDirect_CD", 1)
		stop
	Skip:
		TNT1 A 0
		stop
	Detonate:
		TNT1 AA 0 A_CountDown
		stop
	End:
		TNT1 A 0 A_CountDown
		stop
	}
}

// This version skips the BlackHoleExists flag for desync reasons
actor CrocBlackHoleBombFlyFunc2 : BlackHoleBombFlyFunc
{
States
{
Pickup:
goto CrocBlackHoleBombFlyFunc::Pickup+1
}
}

actor CrocBlackHoleBombExplosion : BasicExplosion
{
Obituary "$OB_BLACKHOLEBOMB"
damagetype "CrocBlackHoleBomb"
reactiontime 5
Scale 3.0
States
{
Spawn:
BLKH F 0
BLKH F 0 A_GiveToTarget("CrocBlackHoleBombCooldown", 1)
BLKH F 0 A_SpawnItemEx("CrocBlackHoleBombFXSpawner", 0, 0, 0, 0, 0, 0, 0, SXF_TRANSFERTRANSLATION)
BLKH F 0 A_PlaySoundEx("weapons/mm9/blackholebombexplode", "body", 1)
BLKH F 0 ACS_NamedExecuteAlways("core_radiuspull", 0, 7, 256)
BLKH F 0 A_Explode(5,128,0)
BLKH FFGG 1
BLKH F 0 ACS_NamedExecuteAlways("core_radiuspull", 0, 7, 256)
BLKH F 0 A_Explode(5,128,0)
BLKH HHII 1
BLKH F 0 ACS_NamedExecuteAlways("core_radiuspull", 0, 7, 256)
BLKH F 0 A_Explode(5,128,0)
BLKH JJKK 1
BLKH F 0 ACS_NamedExecuteAlways("core_radiuspull", 0, 7, 256)
BLKH F 0 A_Explode(5,128,0)
BLKH LLMM 1
BLKH F 0 ACS_NamedExecuteAlways("core_radiuspull", 0, 7, 256)
BLKH F 0 A_Explode(5,128,0)
BLKH NNOO 1
BLKH F 0 ACS_NamedExecuteAlways("core_radiuspull", 0, 7, 256)
BLKH F 0 A_Explode(5,128,0)
BLKH PPQQ 1
BLKH Q 0 A_Countdown
goto Spawn+3
Death:
TNT1 A 3//Finish the sound effect before stopping
TNT1 A 0 A_StopSound
stop
}
}

actor CrocBlackHoleFlag : Inventory
{
inventory.amount 1
inventory.maxamount 3
}

actor CrocBlackHoleBombFXSpawner : BasicGraphicEffect
{
reactiontime 18
States
{
	SpawnFrame:
		TNT1 A 0
		TNT1 AA 0 A_SpawnItemEx("CrocBlackHoleBombFX", 256, 0, 0, 0, 0, 0, random(0, 359), SXF_TRANSFERTRANSLATION)
		TNT1 A 1 A_SpawnItemEx("CrocBlackHoleBombFX", 256, 0, 0, 0, 0, 0, random(0, 359), SXF_TRANSFERTRANSLATION)
		TNT1 AA 0 A_SpawnItemEx("CrocBlackHoleBombFX", 256, 0, 0, 0, 0, 0, random(0, 359), SXF_TRANSFERTRANSLATION)
		TNT1 A 1 A_SpawnItemEx("CrocBlackHoleBombFX", 256, 0, 0, 0, 0, 0, random(0, 359), SXF_TRANSFERTRANSLATION)
		TNT1 AA 0 A_SpawnItemEx("CrocBlackHoleBombFX", 256, 0, 0, 0, 0, 0, random(0, 359), SXF_TRANSFERTRANSLATION)
		TNT1 A 1 A_SpawnItemEx("CrocBlackHoleBombFX", 256, 0, 0, 0, 0, 0, random(0, 359), SXF_TRANSFERTRANSLATION)
		TNT1 AA 0 A_SpawnItemEx("CrocBlackHoleBombFX", 256, 0, 0, 0, 0, 0, random(0, 359), SXF_TRANSFERTRANSLATION)
		TNT1 A 1 A_SpawnItemEx("CrocBlackHoleBombFX", 256, 0, 0, 0, 0, 0, random(0, 359), SXF_TRANSFERTRANSLATION)
		TNT1 AA 0 A_SpawnItemEx("CrocBlackHoleBombFX", 256, 0, 0, 0, 0, 0, random(0, 359), SXF_TRANSFERTRANSLATION)
		TNT1 A 1 A_SpawnItemEx("CrocBlackHoleBombFX", 256, 0, 0, 0, 0, 0, random(0, 359), SXF_TRANSFERTRANSLATION)
		TNT1 AA 0 A_SpawnItemEx("CrocBlackHoleBombFX", 256, 0, 0, 0, 0, 0, random(0, 359), SXF_TRANSFERTRANSLATION)
		TNT1 A 1 A_SpawnItemEx("CrocBlackHoleBombFX", 256, 0, 0, 0, 0, 0, random(0, 359), SXF_TRANSFERTRANSLATION)
		TNT1 A 0 A_Countdown
		loop
	Death:
		TNT1 A 0
		stop
}
}

actor CrocBlackHoleBombFX : BasicGraphicEffect
{
+FORCEXYBILLBOARD
+BRIGHT
States
{
Spawn:
BLKH TS 3
BLKH S 0 A_ChangeVelocity(-16, 0, 0, CVF_RELATIVE|CVF_REPLACE)
BLKH RRRRRRRRRRRR 1 A_SpawnItemEx("CrocBlackHoleBombFX2",0,0,0,momx/1.5,momy/1.5,0,0,SXF_ABSOLUTEMOMENTUM|SXF_TRANSFERTRANSLATION)
stop
}
}

actor CrocBlackHoleBombFX2 : BlackHoleBombFX
{
States
{
Spawn:
BLKH R 5
stop
}
}

actor CrocBlackHoleBombTrigger : Powerup
{
	powerup.duration 3
}

actor CrocBlackHoleBombCooldown : Powerup
{
	powerup.duration 132
}
