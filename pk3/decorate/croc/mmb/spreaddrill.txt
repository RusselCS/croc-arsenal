actor SpreadDrillWep_NormalBar : NormalBar { args 87, 220 }

actor SpreadDrillWep : BaseMM8BDMWep 10014
{
//$Category MM8BDM-Weapons/Power
//$Title Spread Drill
//$Sprite SPREX0
Weapon.AmmoUse 2
Weapon.AmmoGive 28
Weapon.SlotNumber 5
Inventory.Pickupmessage "$PU_SPREADDRILL"
Obituary "$OB_SPREADDRILL"
Tag "$TAG_SPREADDRILL"
weapon.ammotype "SpreadDrillAmmo"
inventory.icon "SPREI"
+AMMO_OPTIONAL
+ALT_AMMO_OPTIONAL
States
{
SpawnLoop:
SPRE X -1
loop
Ready:
SPRE H 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_SPREADDRILL)
SPRE H 1 A_GiveInventory("SpreadDrill_Ready_P", 1) //A_WeaponReady
wait
Deselect:
SPRE H 0
goto DeselectSwap
Select:
SPRE H 0
goto SelectSwap
Altfire:
SPRE H 0 A_JumpIfNoAmmo("NoAmmo")
SPRE H 0 A_GiveInventory("SpreadDrill_Fire_P", 1)
SPRE IJ 2
SPRE H 4
Goto Ready+1
Fire:
SPRE H 0 A_JumpIfInventory("SpreadDrillWait",1,"Spread")
SPRE H 0 A_JumpIfNoAmmo("NoAmmo")
SPRE H 0 A_GiveInventory("SpreadDrill_Fire_P", 1)
SPRE IJ 2
SPRE H 4
SPRE J 0 A_Refire("Spread")//A_Refire
Goto Ready+1
Spread:
SPRE K 0 A_GiveInventory("SpreadDrillTrigger", 1)
SPRE KH 3
goto Ready+1
NoAmmo:
SPRE H 1 ACS_NamedExecuteAlways("core_noammo",0)
Goto Ready+1
}
}

actor SpreadDrillAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}

actor SpreadDrillFlag : Inventory
{
inventory.amount 1
inventory.maxamount 2
}

actor SpreadDrillBuffer : Powerup
{
powerup.duration 23
}

actor SpreadDrillTrigger : Powerup
{
    powerup.duration 5
}

actor SpreadDrillWait : Powerup
{
    powerup.duration 20
}

actor SpreadDrillAltWait : Once {}

actor SpreadDrillAltWaitCooldowner : BasicCooldowner
{
    reactiontime 26
    States
    {
    Spawn:
        TNT1 A 0
    Spawn2:
        TNT1 A 1 A_GiveInventory("SpreadDrillAltWaitCooldownerFunc",1)
        loop
    Death:
        TNT1 A 0 A_TakeFromTarget("SpreadDrillAltWait",1)
        stop
    }
}

actor SpreadDrillAltWaitCooldownerFunc : BasicCooldownerFunc
{
    States
    {
    Pickup:
        TNT1 A 0 A_JumpIfInTargetInventory("SpreadDrillAltWait",1,"PickupBasic")
        goto PickupRemove
    }
}

actor SpreadDrill_Ready_P : CustomInventory
{
    States
    {
    Pickup:
        TNT1 A 0 A_JumpIfInventory("SpreadDrillAltWait",1,2)
        TNT1 A 0 A_WeaponReady
        stop
        TNT1 A 0 A_JumpIfInventory("SpreadDrillWait",1,2)
        TNT1 A 0 A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY)
        stop
        TNT1 A 0 A_WeaponReady(WRF_NOSECONDARY)
        stop
    }
}

actor SpreadDrill_Fire_P : CustomInventory
{
    States
    {
    Pickup:
        TNT1 A 0 //A_GiveInventory("SpreadDrillWait",1)
        TNT1 A 0 A_GiveInventory("SpreadDrillWait", 1)
        TNT1 A 0 A_GiveInventory("SpreadDrillAltWait", 1)
        TNT1 A 0 A_TakeInventory("SpreadDrillAmmo",2,TIF_NOTAKEINFINITE)
        TNT1 A 0 A_SpawnItemEx("SpreadDrillAltWaitCooldowner")

        TNT1 A 0 A_PlaySoundEx("weapons/mmb/spreaddrillfire","Weapon")
        TNT1 A 0 A_SpawnItemEx("SpreadDrill",cos(pitch)*32,0,28-(sin(pitch)*32),cos(pitch)*13,0,-sin(pitch)*13,0,0,0)
        TNT1 A 0 A_JumpIfInventory("PowerSpreadRune", 1, "PickupSpread")
        stop
    PickupSpread:
        TNT1 A 0 A_SpawnItemEx("SpreadDrill",cos(pitch)*32,0,28-(sin(pitch)*32),cos(pitch)*13,0,-sin(pitch)*13,-15,0,0)
        TNT1 A 0 A_SpawnItemEx("SpreadDrill",cos(pitch)*32,0,28-(sin(pitch)*32),cos(pitch)*13,0,-sin(pitch)*13,15,0,0)
        stop
    }
}

actor SpreadDrill : BasicProjectile
{
+RIPPER
radius 32
height 16
speed 13
DamageType "SpreadDrill"
Obituary "$OB_SPREADDRILL"
damage (30)
Gravity 0.25
states
{
Spawn:
TNT1 A 0
SPRE AA 1 
Waiting:
SPRE F 2 A_GiveInventory("CutterFlag", 1)
SPRE F 0 A_JumpIfInTargetInventory("SpreadDrillTrigger",1,"Spread")
TNT1 B 0 A_GiveToTarget("SpreadDrillWait", 1)
SPRE A 2 A_GiveInventory("CutterFlag", 1)
SPRE A 0 A_JumpIfInTargetInventory("SpreadDrillTrigger",1,"Spread")
TNT1 B 0 A_GiveToTarget("SpreadDrillWait", 1)
SPRE A 0 A_JumpIfInventory("CutterFlag",5,"AddGravity")
Loop
AddGravity:
SPRE A 0 A_ChangeFlag("NOGRAVITY", 0)
Goto Waiting2
Waiting2:
SPRE AA 1 A_JumpIf(z-floorz < 1, "Death")
SPRE A 0 A_JumpIfInTargetInventory("SpreadDrillTrigger",1,"Spread")
TNT1 B 0 A_GiveToTarget("SpreadDrillWait", 1)
SPRE FF 1 A_JumpIf(z-floorz < 1, "Death") // A_Checkfloor doesn't work here
SPRE B 0 A_JumpIfInTargetInventory("SpreadDrillTrigger",1,"Spread")
TNT1 B 0 A_GiveToTarget("SpreadDrillWait", 1)
Loop
Spread:
TNT1 A 0 A_SpawnItemEx("LargeDrillShell",-8,0,0,5,0,0,0,SXF_WEPFXCOLOR)
TNT1 A 0 A_SpawnItemEx("MediumDrill1",0,0,0,20,13,0,0)
TNT1 A 0 A_SpawnItemEx("MediumDrill1",0,0,0,20,-13,0,0)
stop
Death:
TNT1 A 0
stop
}
}

actor MediumDrill1 : SpreadDrill
{
+NOGRAVITY
radius 20
damage (13)
DamageType "SpreadDrill2"
states
{
Spawn:
SPRE B 0
SPRE CB 2
Goto Death
Death:
TNT1 A 0 A_CustomMissile("MediumDrill2",0,0,0, 2,0)
stop
}
}

actor MediumDrill2 : SpreadDrill
{
+NOGRAVITY
radius 16
Speed 20
DamageType "SpreadDrill2"
damage (13)
states
{
Spawn:
SPRE CB 2 
SPRE B 0 A_JumpIfInTargetInventory("SpreadDrillTrigger",1,"Spread")
TNT1 A 0 A_GiveToTarget("SpreadDrillWait", 1)
loop
Spread:
TNT1 A 0 A_SpawnItemEx("MediumDrillShell",-4,0,0,5,0,0,0,SXF_WEPFXCOLOR)
TNT1 A 0 A_SpawnItemEx("SmallDrill1",0,0,0,20,8,0,0)
TNT1 A 0 A_SpawnItemEx("SmallDrill1",0,0,0,20,-8,0,0)
stop
Death:
TNT1 A 0
stop
}
}

actor SmallDrill1 : MediumDrill1
{
radius 12
height 8
damage (11)
DamageType "SpreadDrill3"
states
{
Spawn:
SPRE D 0
SPRE ED 2
Goto Death
Death:
TNT1 A 0 A_CustomMissile("SmallDrill2",0,0,0, 2,0)//A_SpawnItemEx("SmallDrill2",0,0,0,27,0,0,0)
stop
}
}

actor SmallDrill2 : MediumDrill2
{
radius 8
height 8
damage (11)
Speed 28
DamageType "SpreadDrill3"
states
{
Spawn:
SPRE ED 2
loop
Death:
TNT1 A 0 
stop
}
}

actor LargeDrillShell : BasicGraphicEffect
{
-NOGRAVITY
-NOINTERACTION
+RIPPER
damage (0)
Gravity 0.3
States
{
SpawnFrame:
TNT1 A 0
SHEL A 1 A_PlaySoundEx("weapons/mmb/spreaddrillsplitmid","Weapon")
TNT1 A 1 
Spawn2:
SHEL A 1
TNT1 A 1 A_CheckFloor("Death")
loop
Death:
TNT1 A 0
stop
}
}

actor MediumDrillShell : LargeDrillShell
{
States
{
SpawnFrame:
TNT1 A 0 
SHEL B 1 A_PlaySoundEx("weapons/mmb/spreaddrillsplitsmall","Weapon")
TNT1 A 1 
Spawn2:
SHEL B 1
TNT1 A 1 A_CheckFloor("Death")
loop
}
}

actor SpreadDrillProtect : PowerProtection
{
Powerup.Duration 3
DamageFactor "SpreadDrill", 0.1
DamageFactor "SpreadDrill2", 0.5
DamageFactor "SpreadDrill3", 0.5
}
