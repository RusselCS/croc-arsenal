actor CrocSpreadDrillWep_NormalBar : NormalBar { args 87, 220 }

actor CrocSpreadDrillWep : BaseMM8BDMWep 10014
{
//$Category MM8BDM-Weapons/Power
//$Title Spread Drill
//$Sprite SPREX0
Weapon.AmmoUse 2
Weapon.AmmoGive 28
Weapon.SlotNumber 5
Inventory.Pickupmessage "$PU_SPREADDRILL"
Obituary "$OB_SPREADDRILL"
Tag "$TAG_SPREADDRILL"
weapon.ammotype "CrocSpreadDrillAmmo"
inventory.icon "SPREI"
+AMMO_OPTIONAL
+ALT_AMMO_OPTIONAL
States
{
SpawnLoop:
SPRE X -1
loop
Ready:
SPRE H 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_SPREADDRILL)
SPRE H 1 A_GiveInventory("CrocSpreadDrill_Ready_P", 1) //A_WeaponReady
wait
Deselect:
SPRE H 0
goto DeselectSwap
Select:
SPRE H 0
goto SelectSwap
Altfire:
SPRE H 0 A_JumpIfNoAmmo("NoAmmo")
SPRE H 0 A_GiveInventory("CrocSpreadDrill_Fire_P", 1)
SPRE IJ 2
SPRE H 4
Goto Ready+1
Fire:
SPRE H 0 A_JumpIfInventory("CrocSpreadDrillWait",1,"Spread")
SPRE H 0 A_JumpIfNoAmmo("NoAmmo")
SPRE H 0 A_GiveInventory("CrocSpreadDrill_Fire_P", 1)
SPRE IJ 2
SPRE H 4
SPRE J 0 A_Refire("Spread")//A_Refire
Goto Ready+1
Spread:
SPRE K 0 A_GiveInventory("CrocSpreadDrillTrigger", 1)
SPRE KH 3
goto Ready+1
NoAmmo:
SPRE H 1 ACS_NamedExecuteAlways("core_noammo",0)
Goto Ready+1
}
}

actor CrocSpreadDrillAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}

actor CrocSpreadDrillFlag : Inventory
{
inventory.amount 1
inventory.maxamount 2
}

actor CrocSpreadDrillBuffer : Powerup
{
powerup.duration 23
}

actor CrocSpreadDrillTrigger : Powerup
{
    powerup.duration 5
}

actor CrocSpreadDrillWait : Powerup
{
    powerup.duration 20
}

actor CrocSpreadDrillAltWait : Once {}

actor CrocSpreadDrillAltWaitCooldowner : BasicCooldowner
{
    reactiontime 26
    States
    {
    Spawn:
        TNT1 A 0
    Spawn2:
        TNT1 A 1 A_GiveInventory("CrocSpreadDrillAltWaitCooldownerFunc",1)
        loop
    Death:
        TNT1 A 0 A_TakeFromTarget("CrocSpreadDrillAltWait",1)
        stop
    }
}

actor CrocSpreadDrillAltWaitCooldownerFunc : BasicCooldownerFunc
{
    States
    {
    Pickup:
        TNT1 A 0 A_JumpIfInTargetInventory("CrocSpreadDrillAltWait",1,"PickupBasic")
        goto PickupRemove
    }
}

actor CrocSpreadDrill_Ready_P : CustomInventory
{
    States
    {
    Pickup:
        TNT1 A 0 A_JumpIfInventory("CrocSpreadDrillAltWait",1,2)
        TNT1 A 0 A_WeaponReady
        stop
        TNT1 A 0 A_JumpIfInventory("CrocSpreadDrillWait",1,2)
        TNT1 A 0 A_WeaponReady(WRF_NOPRIMARY|WRF_NOSECONDARY)
        stop
        TNT1 A 0 A_WeaponReady(WRF_NOSECONDARY)
        stop
    }
}

actor CrocSpreadDrill_Fire_P : CustomInventory
{
    States
    {
    Pickup:
        TNT1 A 0 //A_GiveInventory("CrocSpreadDrillWait",1)
        TNT1 A 0 A_GiveInventory("CrocSpreadDrillWait", 1)
        TNT1 A 0 A_GiveInventory("CrocSpreadDrillAltWait", 1)
        TNT1 A 0 A_TakeInventory("CrocSpreadDrillAmmo",2,TIF_NOTAKEINFINITE)
        TNT1 A 0 A_SpawnItemEx("CrocSpreadDrillAltWaitCooldowner")

        TNT1 A 0 A_PlaySoundEx("weapons/mmb/spreaddrillfire","Weapon")
        TNT1 A 0 A_SpawnItemEx("CrocSpreadDrill",cos(pitch)*32,0,28-(sin(pitch)*32),cos(pitch)*13,0,-sin(pitch)*13,0,0,0)
        TNT1 A 0 A_JumpIfInventory("PowerSpreadRune", 1, "PickupSpread")
        stop
    PickupSpread:
        TNT1 A 0 A_SpawnItemEx("CrocSpreadDrill",cos(pitch)*32,0,28-(sin(pitch)*32),cos(pitch)*13,0,-sin(pitch)*13,-15,0,0)
        TNT1 A 0 A_SpawnItemEx("CrocSpreadDrill",cos(pitch)*32,0,28-(sin(pitch)*32),cos(pitch)*13,0,-sin(pitch)*13,15,0,0)
        stop
    }
}

actor CrocSpreadDrill : BasicProjectile
{
+RIPPER
radius 32
height 16
speed 13
DamageType "SpreadDrill"
Obituary "$OB_SPREADDRILL"
damage (30)
Gravity 0.25
states
{
Spawn:
TNT1 A 0
SPRE AA 1 
Waiting:
SPRE F 2 A_GiveInventory("CrocCutterFlag", 1)
SPRE F 0 A_JumpIfInTargetInventory("CrocSpreadDrillTrigger",1,"Spread")
TNT1 B 0 A_GiveToTarget("CrocSpreadDrillWait", 1)
SPRE A 2 A_GiveInventory("CrocCutterFlag", 1)
SPRE A 0 A_JumpIfInTargetInventory("CrocSpreadDrillTrigger",1,"Spread")
TNT1 B 0 A_GiveToTarget("CrocSpreadDrillWait", 1)
SPRE A 0 A_JumpIfInventory("CrocCutterFlag",5,"AddGravity")
Loop
AddGravity:
SPRE A 0 A_ChangeFlag("NOGRAVITY", 0)
Goto Waiting2
Waiting2:
SPRE AA 1 A_JumpIf(z-floorz < 1, "Death")
SPRE A 0 A_JumpIfInTargetInventory("CrocSpreadDrillTrigger",1,"Spread")
TNT1 B 0 A_GiveToTarget("CrocSpreadDrillWait", 1)
SPRE FF 1 A_JumpIf(z-floorz < 1, "Death") // A_Checkfloor doesn't work here
SPRE B 0 A_JumpIfInTargetInventory("CrocSpreadDrillTrigger",1,"Spread")
TNT1 B 0 A_GiveToTarget("CrocSpreadDrillWait", 1)
Loop
Spread:
TNT1 A 0 A_SpawnItemEx("CrocLargeDrillShell",-8,0,0,5,0,0,0,SXF_WEPFXCOLOR)
TNT1 A 0 A_SpawnItemEx("CrocMediumDrill1",0,0,0,20,13,0,0)
TNT1 A 0 A_SpawnItemEx("CrocMediumDrill1",0,0,0,20,-13,0,0)
stop
Death:
TNT1 A 0
stop
}
}

actor CrocMediumDrill1 : CrocSpreadDrill
{
+NOGRAVITY
radius 20
damage (13)
DamageType "SpreadDrill2"
states
{
Spawn:
SPRE B 0
SPRE CB 2
Goto Death
Death:
TNT1 A 0 A_CustomMissile("CrocMediumDrill2",0,0,0, 2,0)
stop
}
}

actor CrocMediumDrill2 : CrocSpreadDrill
{
+NOGRAVITY
radius 16
Speed 20
DamageType "SpreadDrill2"
damage (13)
states
{
Spawn:
SPRE CB 2 
SPRE B 0 A_JumpIfInTargetInventory("CrocSpreadDrillTrigger",1,"Spread")
TNT1 A 0 A_GiveToTarget("CrocSpreadDrillWait", 1)
loop
Spread:
TNT1 A 0 A_SpawnItemEx("CrocMediumDrillShell",-4,0,0,5,0,0,0,SXF_WEPFXCOLOR)
TNT1 A 0 A_SpawnItemEx("CrocSmallDrill1",0,0,0,20,8,0,0)
TNT1 A 0 A_SpawnItemEx("CrocSmallDrill1",0,0,0,20,-8,0,0)
stop
Death:
TNT1 A 0
stop
}
}

actor CrocSmallDrill1 : CrocMediumDrill1
{
radius 12
height 8
damage (11)
DamageType "SpreadDrill3"
states
{
Spawn:
SPRE D 0
SPRE ED 2
Goto Death
Death:
TNT1 A 0 A_CustomMissile("CrocSmallDrill2",0,0,0, 2,0)//A_SpawnItemEx("CrocSmallDrill2",0,0,0,27,0,0,0)
stop
}
}

actor CrocSmallDrill2 : CrocMediumDrill2
{
radius 8
height 8
damage (11)
Speed 28
DamageType "SpreadDrill3"
states
{
Spawn:
SPRE ED 2
loop
Death:
TNT1 A 0 
stop
}
}

actor CrocLargeDrillShell : BasicGraphicEffect
{
-NOGRAVITY
-NOINTERACTION
+RIPPER
damage (0)
Gravity 0.3
States
{
SpawnFrame:
TNT1 A 0
SHEL A 1 A_PlaySoundEx("weapons/mmb/spreaddrillsplitmid","Weapon")
TNT1 A 1 
Spawn2:
SHEL A 1
TNT1 A 1 A_CheckFloor("Death")
loop
Death:
TNT1 A 0
stop
}
}

actor CrocMediumDrillShell : CrocLargeDrillShell
{
States
{
SpawnFrame:
TNT1 A 0 
SHEL B 1 A_PlaySoundEx("weapons/mmb/spreaddrillsplitsmall","Weapon")
TNT1 A 1 
Spawn2:
SHEL B 1
TNT1 A 1 A_CheckFloor("Death")
loop
}
}

actor CrocSpreadDrillProtect : PowerProtection
{
Powerup.Duration 3
DamageFactor "CrocSpreadDrill", 0.1
DamageFactor "CrocSpreadDrill2", 0.5
DamageFactor "CrocSpreadDrill3", 0.5
}
