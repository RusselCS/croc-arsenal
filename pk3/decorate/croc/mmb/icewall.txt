actor IceWallWep_NormalBar : NormalBar { args 200, 75 }
actor IceWallWep_ScriptBar : ScriptBar {}

actor IceWallWep : BaseMM8BDMWep 10018
{
//$Category MM8BDM-Weapons/Shield
//$Title Ice Wall
//$Sprite ICWLX0
Weapon.AmmoUse 2
Weapon.AmmoGive 28
Weapon.SlotNumber 7
Inventory.Pickupmessage "$PU_ICEWALL"
Obituary "$OB_ICEWALL"
Tag "$TAG_ICEWALL"
weapon.ammotype "IceWallAmmo"
inventory.icon "IWALL"
States
{
SpawnLoop:
ICWL X -1
loop
Ready:
ICWL H 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_ICEWALL)
ICWL H 0 A_JumpIfInventory("IceWallCooldownFlag",1,"ReadyCooldown")
ICWL H 1 A_WeaponReady
Goto Ready+1
ReadyCooldown:
ICWL H 1 A_WeaponReady(WRF_NOFIRE)
Goto Ready+1
Deselect:
ICWL H 0
goto DeselectSwap
Select:
ICWL H 0
goto SelectSwap
Fire:
ICWL H 0 A_JumpIfNoAmmo("NoAmmo")
ICWL H 0 A_GiveInventory("IceWallFlag",1)
ICWL H 0 A_PlaySoundEx("weapons/mmb/icewallfire","Weapon")
ICWL H 0 A_Stop
ICWL H 0 A_GiveInventory("FireIceWall", 1)
ICWL H 0 A_TakeInventory("IceWallAmmo",2,TIF_NOTAKEINFINITE)
ICWL JI 2
//ICWL HHHHHHHHHHHHHHHHHHHH 1 A_WeaponReady(WRF_NOFIRE)
//ICWL HHHHHHHHHHHHHHHHHHHH 1 A_WeaponReady(WRF_NOFIRE)
//ICWL H 0 A_Refire
ICWL H 0 A_GiveInventory("IceWallCooldownFlag",1)
ICWL H 0 A_SpawnItemEx("IceWallCooldowner")
goto Ready+1
NoAmmo:
ICWL H 0 ACS_Execute(979,0)
Goto Ready+1
}
}

actor IceWallCooldownFlag : Once {}

actor IceWallCooldowner : BasicCooldowner
{
reactiontime 40
States
{
Spawn:
TNT1 A 0
Spawn2:
TNT1 A 1 A_GiveInventory("IceWallCooldownerFunc",1)
loop
Death:
TNT1 A 0 A_TakeFromTarget("IceWallCooldownFlag",1)
stop
}
}

actor IceWallCooldownerFunc : BasicCooldownerFunc
{
States
{
Pickup:
TNT1 A 0 A_JumpIfInTargetInventory("IceWallCooldownFlag",1,"PickupBasic")
goto PickupRemove
}
}

actor FireIceWall : CustomInventory
{
    States
    {
    Pickup:
        TNT1 A 0 A_SpawnItemEx("IceWallSpawner",0,0,8,32)
        TNT1 A 0 A_JumpIfInventory("PowerSpreadRune", 1, "PickupSpread")
        stop
    PickupSpread:
        TNT1 A 0 A_SpawnItemEx("IceWallSpawner",0,-4,8,32,0,0,-60)
        TNT1 A 0 A_SpawnItemEx("IceWallSpawner",0,4,8,32,0,0,60)
        stop
    }
}
 
actor IceWallSpawner : ProjSpawnFuncActor
{
scale 2.5
+MISSILE
+RIPPER
+NOGRAVITY
+DONTBLAST
+NOBLOOD
+STEPMISSILE
+NOEXPLODEFLOOR
species "MovingPlatform"
+THRUSPECIES
+DONTREFLECT
maxstepheight 32
Mass 9999999
DamageType "IceWall"
Damage (0)
Height 64
Radius 24
States
{
Spawn:
TNT1 A 0
TNT1 A 1
TNT1 A 1 A_Stop
ICWL ABCDE 1
ICWL F 0 A_ChangeFlag("NOGRAVITY",0)
ICWL F 0 A_Explode(11, 64, 0)
TNT1 A 0 A_SpawnItemEx("IcePitFreeze")
ICWL F 0 A_SpawnItemEx("IceWall")
stop
Death:
ICWL K 0
ICWL K 0 A_Stop
ICWL LM 5
stop
}
}
 
actor IceWall : ProjSpawnFuncActor
{
scale 2.1
Obituary "$OB_ICEWALL"
+MISSILE
+RIPPER
+PUSHABLE
+SHOOTABLE
+SOLID
+NODAMAGETHRUST
+NOTARGETSWITCH
+NOBLOOD
+MOVEWITHSECTOR
+CANBOUNCEWATER
+DONTSPLASH
+DONTRIP
+NOEXPLODEFLOOR
+STEPMISSILE
+DONTREFLECT
+NOTAUTOAIMED
+BOUNCEONWALLS
+ALLOWBOUNCEONACTORS
+FORCEYBILLBOARD
+DONTDRAIN
+EXPLODEONDEATH
Maxstepheight 24
Mass 9999999
DamageType "IceWall"
Health 200
Damage (11)
Radius 24
Height 64
speed 0
//Gravity 2.0
BounceFactor 0.91
WallBounceFactor 0.91
DamageFactor "IceWall", 0.0
species "MovingPlatform"
+THRUSPECIES
var int user_playerRiding[64];
+USEDAMAGEEVENTSCRIPT
var int user_NoOwnerDamage;
States
{
Spawn:
ICWL F 0
ICWL F 1 A_TakeInventory("IceWallFlag",1,0,AAPTR_TARGET)
SpawnLoop:
ICWL F 1 A_JumpIfInventory("IceWallFlag",1,"Death",AAPTR_TARGET)
ICWL F 0 A_JumpIf(momx>1||momy>1||momx<-1||momy<-1,"MoveBegin")
ICWL F 0 Thing_Damage(0, 1)
ICWL F 0 A_Explode(11, 64, 0)
ICWL F 0 A_SpawnItemEx("IcePitFreeze")
loop
MoveBegin:
ICWL F 0 ACS_NamedExecuteAlways("core_stickyLifts",0,STICKYMODE_GUTSLIFT)
ICWL F 0 A_ChangeFlag("PUSHABLE",0)
ICWL F 0 ACS_NamedExecuteAlways("core_icewallmanage", 0, 1)
ICWL F 1 A_JumpIfInventory("IceWallFlag",1,"Death",AAPTR_TARGET)
Accelerate:
ICWL F 0 A_Recoil(-3)
ICWL FFFFF 1 A_JumpIfInventory("IceWallFlag",1,"Death",AAPTR_TARGET)
ICWL F 0 A_Recoil(-3)
ICWL FFFFF 1 A_JumpIfInventory("IceWallFlag",1,"Death",AAPTR_TARGET)
ICWL F 0 A_Recoil(-3)
ICWL FFFFF 1 A_JumpIfInventory("IceWallFlag",1,"Death",AAPTR_TARGET)
ICWL F 0 A_Recoil(-2)
ICWL FFFFF 1 A_JumpIfInventory("IceWallFlag",1,"Death",AAPTR_TARGET)
ICWL F 0 A_Recoil(-2)
ICWL FFFFF 1 A_JumpIfInventory("IceWallFlag",1,"Death",AAPTR_TARGET)
Moving:
ICWL F 0 A_JumpIfInventory("IceWallFlag",1,"Death",AAPTR_TARGET)
ICWL A 0 A_SpawnItemEx("IcePitFreeze")
ICWL F 0 Thing_Damage(0, 1)
ICWL F 1 A_JumpIfHealthLower(75,"MovingLow")
loop
MovingLow:
ICWL K 0 A_JumpIfInventory("IceWallFlag",1,"Death",AAPTR_TARGET)
ICWL A 0 A_SpawnItemEx("IcePitFreeze")
ICWL K 1 Thing_Damage(0, 1)
loop
Death:
ICWL K 0 A_TakeInventory("CutterFlag", 1)
ICWL K 0 A_Stop
ICWL K 0 A_ChangeFlag("NOGRAVITY", 1)
ICWL K 0 A_ChangeFlag("SOLID", 0)
ICWL K 0 A_ChangeFlag("SHOOTABLE", 0)
ICWL K 10
ICWL LM 5
stop
}
}
 
actor IceWallAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}
 
actor IceWallFlag : Once {}
