actor CrocCopyVisionWep_NormalBar : NormalBar { args 201, 110 }

actor CrocCopyVisionWep : BaseMM8BDMWep // 10015
{
//$Category MM8BDM-Weapons/Ranged
//$Title Copy Vision
//$Sprite COPYX0
Weapon.AmmoUse 7
Weapon.AmmoGive 28
Weapon.SlotNumber 2
Inventory.Pickupmessage "$PU_COPYVISION"
Obituary "$OB_COPYVISION"
Tag "$TAG_COPYVISION"
weapon.ammotype "CrocCopyVisionAmmo"
inventory.icon "COPYVI"
+WEAPON.NOAUTOAIM
States
{
SpawnLoop:
COPY X -1
loop
Ready:
COPB A 0 ACS_NamedExecuteWithResult("core_weaponcolor",CLR_COPYVISION)
COPB A 1 A_WeaponReady
Goto Ready+1
Deselect:
COPB A 0 A_TakeInventory("CrocCopyVisionRedirect", 9)
goto DeselectSwap
Select:
COPB A 0
goto SelectSwap
Fire:
COPB A 0 A_JumpIfInventory("CrocCopyVisionTimer",1,"Buster")
COPB A 0 A_JumpIfNoAmmo("Buster")
COPB A 0 A_FireCustomMissile("CrocCopyVisionSpawner",0,0,8,0,0,pitch)//A_SpawnItemEx("CopyVision",30,0,0,0,0,0,0,0,0)
COPB A 0 A_TakeInventory("CrocCopyVisionAmmo",7,TIF_NOTAKEINFINITE)
COPB A 0 A_GiveInventory("CrocCopyVisionTimer", 40)
COPB A 11
COPB A 2 A_Refire
Goto Ready+1
Buster:
COPB A 0 A_GiveInventory("CrocCopyVisionRedirect", 1)
Altfire:
COPB A 0 A_PlaySoundEx("weapons/mmb/copyvisionbuster","Weapon")
COPB A 0 A_FireCustomMissile("CopyVisionBusterShot",0,0,8,0)
COPB BC 3
COPB A 2
COPB A 0 A_Refire
Goto Ready+1
NoAmmo:
COPB A 1 ACS_NamedExecuteAlways("core_noammo",0)
goto Ready+1
}
}

actor CrocCopyVisionAmmo : Ammo
{
inventory.amount 1
inventory.maxamount 28
+INVENTORY.IGNORESKILL
}

actor CrocCopyVisionFlag : Once {}

actor CrocCopyVisionRedirect : CrocCopyVisionFlag {}

actor CrocCopyVisionTimer : Inventory
{
    inventory.amount 1
    inventory.maxamount 40
}

actor CrocCopyVisionSpawner
{
PROJECTILE
+DONTBLAST
+THRUACTORS
Damage(0)
Height 1
Radius 16
Speed 19
States
{
Spawn:
TNT1 A 0
TNT1 A 1
Death:
TNT1 A 0 A_SpawnItemEx("CrocCopyVision",0,0,-32,0,0,0,0,SXF_TRANSFERTRANSLATIOn,0)
stop
}
}

actor CrocCopyVision : BasicProjectile
{
RenderStyle "Translucent"
Alpha 0.90
+THRUGHOST
+RIPPER
+NOCLIP
+DONTBLAST
+BRIGHT
+SEEKERMISSILE
+LOOKALLAROUND
Speed 0
Radius 16
Height 32
Damage (0)
// reactiontime 40
States
{
Spawn:
TNT1 A 0
TNT1 A 0 A_PlaySoundEx("weapons/mmb/copyvisionactivate", "weapon")
TNT1 A 0 ACS_NamedExecuteAlways("core_targetyawpitch", 0, 0)
COPY FGHI 2
Goto Shooting+1
Shooting:
COPY C 0 A_TakeFromTarget("CrocCopyVisionTimer", 1)
COPY C 0 A_GiveInventory("CrocCopyVisionHome", 1)
COPY C 0 A_PlaySoundEx("weapons/mmb/copyvisionshoot", "body")
COPY C 0 A_CustomMissile("CrocCopyShot",32,0,0,2,-pitch)
COPY CCCCCC 1 // A_JumpIfInTargetInventory("CrocCopyVisionRedirect", 1, "Redirect")
COPY C 0 A_JumpIfInTargetInventory("IsDead", 1, "Death")
COPY C 0 A_JumpIfInTargetInventory("CrocCopyVisionTimer", 1, "Shooting")
COPY C 0 A_JumpIf(true, "Death")
COPY C 1
wait
Redirect:
COPY C 0 A_GiveToTarget("CrocTargetMarker", 1)
COPY C 3
COPY C 0 ACS_NamedExecuteAlways("core_aimattargetcursor", 0, CallACS("core_gettarget")+PLN_TARGETPOINT_OFFSET, 32)
COPB A 0 A_TakeFromTarget("CrocCopyVisionRedirect", 1)
goto Shooting
Death:
TNT1 A 0 A_TakeFromTarget("CrocCopyVisionTimer", 99)
COPB A 0 A_TakeFromTarget("CrocCopyVisionRedirect", 1)
COPY IJK 3
Stop
}
}

actor CrocCopyVisionHome : CustomInventory
{
    States
    {
    Pickup:
        TNT1 A 0 A_RearrangePointers(AAPTR_TARGET, AAPTR_NULL, AAPTR_NULL)
        TNT1 A 0 A_SeekerMissile(4, 10, SMF_LOOK, 256, 20)
        TNT1 A 0 A_JumpIfTargetInLOS("Aim", 0, JLOSF_PROJECTILE)
        stop
    Aim:
        TNT1 A 0 ACS_NamedExecuteWithResult("core_predicthome", 35, AAPTR_TRACER, 0)
        stop
    }
}

actor CrocCopyShot : BasicProjectile
{
+FORCEXYBILLBOARD
+BRIGHT
Damage (7)
Speed 35
DamageType "CopyVision"
Obituary "$OB_COPYVISION"
radius 10
height 5
States
{
Spawn:
TNT1 A 0
TNT1 A 1
COPY DE 2
Goto Spawn+2
Death:
TNT1 A 0 
stop
}
}

actor CopyVisionBusterShot : BasicExplosion
{
    speed 35
    var int user_CompositePierceRipper;
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_SpawnItemEx("CopyVisionBusterShotL",-momx,-momy,-momz,momx,momy,momz,0,SXF_TRANSFERPOINTERS|SXF_ABSOLUTEMOMENTUM|SXF_ABSOLUTEPOSITION)
        TNT1 A 0 A_SpawnItemEx("CopyVisionBusterShotR",-momx,-momy,-momz,momx,momy,momz,0,SXF_TRANSFERPOINTERS|SXF_ABSOLUTEMOMENTUM|SXF_ABSOLUTEPOSITION)
        stop
    }
}

const int WEPC_BCV_SPEED = 6;

actor CopyVisionBusterShotL : BasicProjectile
{
    +FORCEXYBILLBOARD
    +BRIGHT
    damagetype "Buster"
    Obituary "$OB_COPYVISIONSHOT"
    Damage (10)
    radius 14
    height 5
    var int user_CompositePierceRipper;

    var int user_DIRECTION;
    var int user_SPEED;
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_SetUserVar("user_DIRECTION",1)
        TNT1 A 0 A_SetUserVar("user_SPEED", WEPC_BCV_SPEED)
        goto SpawnStart
    SpawnStart:
        COPY LL 1 A_Changevelocity(0, -(user_SPEED/2)*user_DIRECTION,0,1)
        COPY L 0 A_Changevelocity(0, 3*user_SPEED*user_DIRECTION,0,1)
        goto SpawnLoop
    SpawnLoop:
        // 6 ticks
        COPY LLL 1 A_SpawnItemEx("CopyVisionBusterTrail")

        // 12 ticks
        COPY L 0 A_Changevelocity(0,-user_SPEED*user_DIRECTION,0,1)
        COPY LL 1 A_SpawnItemEx("CopyVisionBusterTrail")
        COPY L 0 A_Changevelocity(0,-user_SPEED*user_DIRECTION,0,1)
        COPY L 1 A_SpawnItemEx("CopyVisionBusterTrail")
        COPY L 0 A_Changevelocity(0,-user_SPEED*user_DIRECTION,0,1)
        COPY LL 1 A_SpawnItemEx("CopyVisionBusterTrail")
        COPY L 0 A_Changevelocity(0,-user_SPEED*user_DIRECTION,0,1)
        COPY L 1 A_SpawnItemEx("CopyVisionBusterTrail")

        // 6 ticks
        COPY LLL 1 A_SpawnItemEx("CopyVisionBusterTrail")

        // 12 ticks
        COPY L 0 A_Changevelocity(0,user_SPEED*user_DIRECTION,0,1)
        COPY LL 1 A_SpawnItemEx("CopyVisionBusterTrail")
        COPY L 0 A_Changevelocity(0,user_SPEED*user_DIRECTION,0,1)
        COPY L 1 A_SpawnItemEx("CopyVisionBusterTrail")
        COPY L 0 A_Changevelocity(0,user_SPEED*user_DIRECTION,0,1)
        COPY LL 1 A_SpawnItemEx("CopyVisionBusterTrail")
        COPY L 0 A_Changevelocity(0,user_SPEED*user_DIRECTION,0,1)
        COPY L 1 A_SpawnItemEx("CopyVisionBusterTrail")
        loop
    Death:
        TNT1 A 0
        stop
    }
}

actor CopyVisionBusterShotR : CopyVisionBusterShotL
{
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 0 A_SetUserVar("user_DIRECTION",-1)
        TNT1 A 0 A_SetUserVar("user_SPEED", WEPC_BCV_SPEED)
        goto SpawnStart
    }
}

actor CopyVisionBusterTrail : BasicGraphicEffect
{
    +FORCEXYBILLBOARD
    States
    {
    Spawn:
        TNT1 A 0
        TNT1 A 2
        COPY MN 2
        stop
    }
}

